'ThisWorkbook	Document



'uImageMso	UserForm





Dim Labels() As New clsMsoImage
Dim off As Long
Dim rCount As Long
Dim cCount As Long
Dim Size As Long
Dim matrix As Long
Dim totalImages As Long

Sub SetLabelEvents()
    Dim counter As Integer, obj As MSForms.control
    For Each obj In Me.Controls
        If TypeOf obj Is MSForms.Label Then
            counter = counter + 1
            ReDim Preserve Labels(1 To counter)
            Set Labels(counter).LabelEvents = obj
        End If
    Next
    Set obj = Nothing
End Sub

Private Sub CommandButton1_Click()
ActiveCell.Value = TextBox1.Value
Me.Hide
End Sub

Private Sub UserForm_Initialize()
SetUImageMsoVariables
LoadMSO off
Me.Caption = off + 1 & " to " & off + matrix & " of " & totalImages
SetLabelEvents
End Sub

Sub SetUImageMsoVariables()
    Me.Width = 1020
    Me.Height = 550
    
    PreviousBatch.Top = Me.Height - PreviousBatch.Height - 35
    '    PreviousBatch.left = Me.Width / 2 - PreviousBatch.Width - 50
    NextBatch.Top = Me.Height - NextBatch.Height - 35
    '    NextBatch.left = Me.Width / 2 + NextBatch.Width - 50
    
    CommandButton1.Top = Me.Height - CommandButton1.Height - 35
    '    CommandButton1.left = Me.Width / 2 + CommandButton1.Width + 50
    
    TextBox1.Top = Me.Height - TextBox1.Height - 35
    '    TextBox1.left = Me.Width / 2 - TextBox1.Width - 150
    rCount = 15
    cCount = 30
    matrix = rCount * cCount
    totalImages = WorksheetFunction.CountA(ThisWorkbook.Sheets("msoImages").Range("A1").CurrentRegion)
    Size = 32
    off = 0
End Sub

Private Sub NextBatch_MouseDown(ByVal Button As Integer, ByVal Shift As Integer, ByVal X As Single, ByVal Y As Single)
off = off + matrix
If off > totalImages - matrix Then off = totalImages - matrix
Restart
LoadMSO off
Me.Caption = off + 1 & " to " & off + matrix & " of " & totalImages
SetLabelEvents
End Sub

Private Sub PreviousBatch_MouseDown(ByVal Button As Integer, ByVal Shift As Integer, ByVal X As Single, ByVal Y As Single)
off = off - matrix
If off < 0 Then off = 0
Restart
LoadMSO off
Me.Caption = off + 1 & " to " & off + matrix & " of " & totalImages
SetLabelEvents
End Sub

Sub Restart()
    Dim c As MSForms.control
    For Each c In Me.Controls
        If TypeName(c) = "Label" Then Me.Controls.Remove c.Name
    Next
End Sub

Sub LoadMSO(Optional OFFSET As Long = 0)
    Dim cell As Range
    Set cell = ThisWorkbook.Sheets("msoImages").Range("A1")
    Set cell = cell.OFFSET(OFFSET)
    Dim rows As Long, cols As Long
    Dim counter As Long
    Dim newImage As MSForms.Label
    On Error GoTo Skip
    For rows = 1 To rCount
        For cols = 1 To cCount
            Set newImage = Me.Controls.Add("Forms.Label.1", "Image" & counter, True)
            newImage.Top = Size * rows - Size + 5
            newImage.Left = Size * cols - Size + 5
            newImage.Height = 36
            newImage.Width = 36
            newImage.Caption = cell
            newImage.Font.Size = 0
            newImage.Picture = Application.CommandBars.GetImageMso(cell, Size, Size)
Skip:
            Set cell = cell.OFFSET(1)
            If cell = "" Then Exit Sub
        Next
    Next
    Debug.Print Me.Controls.count
End Sub


'clsMsoImage	Class

Public WithEvents LabelEvents As MSForms.Label

Private Sub LabelEvents_Click()
uImageMso.TextBox1.Text = LabelEvents.Caption
uImageMso.TextBox1.SelStart = 0
uImageMso.TextBox1.SelLength = Len(uImageMso.TextBox1.Text)
End Sub

'Private Sub LabelEvents_DblClick(ByVal Cancel As MSForms.ReturnBoolean)
'    ActiveCell.Text = uImageMso.TextBox1.Text
'End Sub

'msoImages	Document



'README	Document



'm_LoadPictureGDI	Module

Option Explicit

'This module provides a LoadPictureGDI function, which can
'be used instead of VBA's LoadPicture, to load a wide variety
'of image types from disk - including png.
'
'The png format is used in Office 2007-2010 to provide images that
'include an alpha channel for each pixel's transparency
'
'Author:    Stephen Bullen
'Date:      31 October, 2006
'Email:     stephen@oaltd.co.uk

'Updated :  30 December, 2010
'By :       Rob Bovey
'Reason :   Also working now in the 64 bit version of Office 2010


'Declare a UDT to store a GUID for the IPicture OLE Interface
Private Type GUID
    Data1 As Long
    Data2 As Integer
    Data3 As Integer
    Data4(0 To 7) As Byte
End Type


#If VBA7 Then
    'Declare a UDT to store the bitmap information
    Private Type PICTDESC
        Size As Long
        Type As Long
        hPic As LongPtr
        hPal As LongPtr
    End Type
    
    'Declare a UDT to store the GDI+ Startup information
    Private Type GDIPlusStartupInput
        GdiPlusVersion As Long
        DebugEventCallback As LongPtr
        SuppressBackgroundThread As Long
        SuppressExternalCodecs As Long
    End Type
    
    'Windows API calls into the GDI+ library
    Private Declare PtrSafe Function GdiplusStartup Lib "GDIPlus" (token As LongPtr, inputbuf As GDIPlusStartupInput, Optional ByVal outputbuf As LongPtr = 0) As Long
    Private Declare PtrSafe Function GdipCreateBitmapFromFile Lib "GDIPlus" (ByVal FileName As LongPtr, BITMAP As LongPtr) As Long
    Private Declare PtrSafe Function GdipCreateHBITMAPFromBitmap Lib "GDIPlus" (ByVal BITMAP As LongPtr, hbmReturn As LongPtr, ByVal background As LongPtr) As Long
    Private Declare PtrSafe Function GdipDisposeImage Lib "GDIPlus" (ByVal image As LongPtr) As Long
    Private Declare PtrSafe Sub GdiplusShutdown Lib "GDIPlus" (ByVal token As LongPtr)
    Private Declare PtrSafe Function OleCreatePictureIndirect Lib "oleaut32" (PicDesc As PICTDESC, RefIID As GUID, ByVal fPictureOwnsHandle As Long, iPic As IPicture) As Long
#Else
    'Declare a UDT to store the bitmap information
    Private Type PICTDESC
        Size As Long
        Type As Long
        hPic As Long
        hPal As Long
    End Type
    
    'Declare a UDT to store the GDI+ Startup information
    Private Type GDIPlusStartupInput
        GdiPlusVersion As Long
        DebugEventCallback As Long
        SuppressBackgroundThread As Long
        SuppressExternalCodecs As Long
    End Type
    
    'Windows API calls into the GDI+ library
    Private Declare Function GdiplusStartup Lib "GDIPlus" (token As Long, inputbuf As GdiplusStartupInput, Optional ByVal outputbuf As Long = 0) As Long
    Private Declare Function GdipCreateBitmapFromFile Lib "GDIPlus" (ByVal filename As Long, bitmap As Long) As Long
    Private Declare Function GdipCreateHBITMAPFromBitmap Lib "GDIPlus" (ByVal bitmap As Long, hbmReturn As Long, ByVal background As Long) As Long
    Private Declare Function GdipDisposeImage Lib "GDIPlus" (ByVal image As Long) As Long
    Private Declare Sub GdiplusShutdown Lib "GDIPlus" (ByVal token As Long)
    Private Declare Function OleCreatePictureIndirect Lib "oleaut32" (PicDesc As PICTDESC, RefIID As GUID, ByVal fPictureOwnsHandle As Long, IPic As IPicture) As Long
#End If

' Procedure:    LoadPictureGDI
' Purpose:      Loads an image using GDI+
' Returns:      The image as an IPicture Object
Public Function LoadPictureGDI(ByVal sFilename As String) As IPicture

    Dim uGdiInput As GDIPlusStartupInput
    Dim lResult As Long
#If VBA7 Then
    Dim hGdiPlus As LongPtr
    Dim hGdiImage As LongPtr
    Dim hBitmap As LongPtr
#Else
    Dim hGdiPlus As Long
    Dim hGdiImage As Long
    Dim hBitmap As Long
#End If

    'Initialize GDI+
    uGdiInput.GdiPlusVersion = 1
    lResult = GdiplusStartup(hGdiPlus, uGdiInput)

    If lResult = 0 Then

        'Load the image
        lResult = GdipCreateBitmapFromFile(StrPtr(sFilename), hGdiImage)

        If lResult = 0 Then

            'Create a bitmap handle from the GDI image
            lResult = GdipCreateHBITMAPFromBitmap(hGdiImage, hBitmap, 0)

            'Create the IPicture object from the bitmap handle
            Set LoadPictureGDI = CreateIPicture(hBitmap)

            'Tidy up
            GdipDisposeImage hGdiImage
        End If

        'Shutdown GDI+
        GdiplusShutdown hGdiPlus
    End If

End Function


' Procedure:    CreateIPicture
' Purpose:      Converts a image handle into an IPicture object.
' Returns:      The IPicture object
#If VBA7 Then
Private Function CreateIPicture(ByVal hPic As LongPtr) As IPicture
#Else
Private Function CreateIPicture(ByVal hPic As Long) As IPicture
#End If
    Dim lResult As Long
    Dim uPicinfo As PICTDESC
    Dim IID_IDispatch As GUID
    Dim iPic As IPicture

    'OLE Picture types
    Const PICTYPE_BITMAP = 1

    ' Create the Interface GUID (for the IPicture interface)
    With IID_IDispatch
        .Data1 = &H7BF80980
        .Data2 = &HBF32
        .Data3 = &H101A
        .Data4(0) = &H8B
        .Data4(1) = &HBB
        .Data4(2) = &H0
        .Data4(3) = &HAA
        .Data4(4) = &H0
        .Data4(5) = &H30
        .Data4(6) = &HC
        .Data4(7) = &HAB
    End With

    ' Fill uPicInfo with necessary parts.
    With uPicinfo
        .Size = Len(uPicinfo)
        .Type = PICTYPE_BITMAP
        .hPic = hPic
        .hPal = 0
    End With

    ' Create the Picture object.
    lResult = OleCreatePictureIndirect(uPicinfo, IID_IDispatch, True, iPic)

    ' Return the new Picture object.
    Set CreateIPicture = iPic

End Function


'm_Ribbon	Module

Option Explicit

'Public myRibbon As IRibbonUI

#If VBA7 Then
    Public Declare PtrSafe Sub CopyMemory Lib "kernel32" Alias "RtlMoveMemory" (Destination As Any, source As Any, ByVal Length As LongPtr)
#Else
    Public Declare Sub CopyMemory Lib "kernel32" Alias "RtlMoveMemory" (Destination As Any, source As Any, ByVal Length As Long)
#End If

Sub Ribbon_OnLoad(Ribbon As IRibbonUI)
    'PURPOSE: Run code when Ribbon loads the UI to store Ribbon Object's Pointer ID code
    #If VBA7 Then
        Dim StoreRibbonPointer As LongPtr
    #Else
        Dim StoreRibbonPointer As Long
    #End If
'    Set myRibbon = ribbon                  'Store Ribbon Object to Public variable
    StoreRibbonPointer = ObjPtr(Ribbon)     'Store pointer to IRibbonUI
    IniWriteKey RibbonIni, "Ribbon", "Pointer", StoreRibbonPointer
    If IniReadKey(RibbonIni, "T1", "Label") = "" Then
        createRibbonIni
    End If
    FoldersCreate IMAGE_FOLDER
End Sub

#If VBA7 Then
Function GetRibbon(ByVal lRibbonPointer As LongPtr) As Object
#Else
Function GetRibbon(ByVal lRibbonPointer As Long) As Object
#End If
    Dim objRibbon As Object
    CopyMemory objRibbon, lRibbonPointer, LenB(lRibbonPointer)
    Set GetRibbon = objRibbon
    Set objRibbon = Nothing
End Function

Public Function Ribbon() As IRibbonUI
    If IniReadKey(RibbonIni, "Ribbon", "Pointer") = "" Then GoTo ErrorHandler
    On Error GoTo ErrorHandler
    Set Ribbon = GetRibbon(IniReadKey(RibbonIni, "Ribbon", "Pointer"))
    On Error GoTo 0
    Exit Function
ErrorHandler:
    MsgBox "Could not get the Ribbon"
End Function

Sub Ribbon_RefreshRibbon()
    If IniReadKey(RibbonIni, "Ribbon", "Pointer") = "" Then GoTo ErrorHandler
    On Error GoTo ErrorHandler
    Ribbon.Invalidate
    On Error GoTo 0
    Exit Sub
ErrorHandler:
    MsgBox "Failed to refresh the Ribbon UI"
End Sub

Sub createRibbonIni()
    MsgBox "The ribbon ini was not found. It will be created now." & vbNewLine & _
              "It will take only a moment, do not interrupt!"

    Dim iniFile As String
        iniFile = RibbonIni
        
    IniWriteKey iniFile, "T1", "Label", "vbArc"
    IniWriteKey iniFile, "T1", "Visible", True
    
    'the following loop can faster
    'change to append a string variable and write it to the file
    Dim g As Long, b As Long, s As Long, m As Long
    For g = 1 To 5
        IniWriteKey iniFile, "T1_G" & g, "Label", "Group" & g
        IniWriteKey iniFile, "T1_G" & g, "Visible", IIf(g = 1, True, False)
        For b = 1 To 10
            IniWriteKey iniFile, "T1_G" & g & "_B" & b, "Size", True
            IniWriteKey iniFile, "T1_G" & g & "_B" & b, "Label", "T1_G" & g & "_B" & b
            IniWriteKey iniFile, "T1_G" & g & "_B" & b, "ShowLabel", True
            IniWriteKey iniFile, "T1_G" & g & "_B" & b, "Visible", IIf(g = 1, True, False)
            IniWriteKey iniFile, "T1_G" & g & "_B" & b, "Enabled", True
            IniWriteKey iniFile, "T1_G" & g & "_B" & b, "Description", ""
            IniWriteKey iniFile, "T1_G" & g & "_B" & b, "ScreenTip", ""
            IniWriteKey iniFile, "T1_G" & g & "_B" & b, "SuperTip", ""
            IniWriteKey iniFile, "T1_G" & g & "_B" & b, "Image", "MicrosoftExcel"
            IniWriteKey iniFile, "T1_G" & g & "_B" & b, "ShowImage", False
        Next
        
        For s = 1 To 9
            IniWriteKey iniFile, "T1_G" & g & "_S" & s, "Size", False
            IniWriteKey iniFile, "T1_G" & g & "_S" & s, "Visible", IIf(g = 1, True, False)
            
            For b = 1 To 10
                IniWriteKey iniFile, "T1_G" & g & "_S" & s & "_B", "Label", "T1_G" & g & "_S" & s & "_B"
                IniWriteKey iniFile, "T1_G" & g & "_S" & s & "_B", "ShowLabel", True
                IniWriteKey iniFile, "T1_G" & g & "_S" & s & "_B", "Enabled", True
                IniWriteKey iniFile, "T1_G" & g & "_S" & s & "_B", "Description", ""
                IniWriteKey iniFile, "T1_G" & g & "_S" & s & "_B", "ScreenTip", ""
                IniWriteKey iniFile, "T1_G" & g & "_S" & s & "_B", "SuperTip", ""
                IniWriteKey iniFile, "T1_G" & g & "_S" & s & "_B", "Image", "MicrosoftExcel"
                IniWriteKey iniFile, "T1_G" & g & "_S" & s & "_B", "ShowImage", False

                For m = 1 To 10
                    IniWriteKey iniFile, "T1_G" & g & "_S" & s & "_M" & m, "Label", "T1_G" & g & "_S" & s & "_M" & m
                    IniWriteKey iniFile, "T1_G" & g & "_S" & s & "_M" & m, "ShowLabel", True
                    IniWriteKey iniFile, "T1_G" & g & "_S" & s & "_M" & m, "Visible", True
                    IniWriteKey iniFile, "T1_G" & g & "_S" & s & "_M" & m, "Enabled", True
                    IniWriteKey iniFile, "T1_G" & g & "_S" & s & "_M" & m, "Description", ""
                    IniWriteKey iniFile, "T1_G" & g & "_S" & s & "_M" & m, "ScreenTip", ""
                    IniWriteKey iniFile, "T1_G" & g & "_S" & s & "_M" & m, "SuperTip", ""
                    IniWriteKey iniFile, "T1_G" & g & "_S" & s & "_M" & m, "Image", "MicrosoftExcel"
                    IniWriteKey iniFile, "T1_G" & g & "_S" & s & "_M" & m, "ShowImage", False
                Next
            Next
        Next
    Next
    
    Dim content As Variant
        content = Split(TxtRead(iniFile), vbLf)
    Dim output As String
    Dim element As String
    Dim i As Long
    For i = LBound(content) To UBound(content)
        element = content(i)
        If Left(element, 1) = "[" Then
            output = output & element
        Else
            output = output & vbTab & element   'REPLACE(ELEMENT,"=",VBTAB & "=" & VBTAB)
        End If
        If i < UBound(content) Then output = output & vbLf
    Next
    TxtOverwrite iniFile, output
    Debug.Print "Ini created"
End Sub

Function RibbonIni() As String
    RibbonIni = ThisWorkbook.Path & "\" & getFileName(ThisWorkbook.Name) & "_ribbon.ini"
End Function

Sub OpenRibbonINI()
    FollowLink RibbonIni
End Sub

Function Ribbon_SetValue(id, TargetProperty, NewValue)
    IniWriteKey RibbonIni, id, TargetProperty, NewValue
End Function

Function Ribbon_GetValue(id, TargetProperty)
    Ribbon_GetValue = IniReadKey(RibbonIni, id, TargetProperty)
End Function

Sub Ribbon_ButtonClick(control As IRibbonControl)
    Dim ControlLabel As String
    ControlLabel = Ribbon_GetValue(control.id, "Label")
    If ProcedureExists(ThisWorkbook.vbProject, ControlLabel) Then
        Application.Run ControlLabel
    Else
        Select Case control.id
            Case "T1_G1_B1": FollowLink ThisWorkbook.Path
            Case "T1_G1_B2": uAuthor.Show
            Case "T1_G1_B3": uIniEditor.Show vbModeless
            Case "T1_G1_B4": Ribbon_RefreshRibbon
            Case Else
                Select Case ControlLabel
                    
                End Select
        End Select
    End If
End Sub

Function IMAGE_FOLDER() As String
    IMAGE_FOLDER = ThisWorkbook.Path & "\Ribbon Images\"
End Function

Sub Ribbon_getImage(control As IRibbonControl, ByRef returnedVal)
    Dim image
    Dim ImageName As String
    ImageName = Ribbon_GetValue(control.id, "Image")
    If InStr(1, ImageName, ".") > 0 Then
        On Error GoTo ErrorHandler
        Dim strPath As String
        strPath = ThisWorkbook.Path & "\Ribbon Images\"
        If FileExists(strPath & ImageName) Then
            Set returnedVal = LoadPictureGDI(strPath & ImageName)
        Else
            returnedVal = "FileSaveAsPowerPointPptx"
        End If
    Else
        returnedVal = ImageName
    End If
ErrorHandler:
End Sub

Sub Ribbon_CheckboxClick(control As IRibbonControl, pressed As Boolean)
    Ribbon_SetValue control, "pressed", pressed
    Select Case Ribbon_GetValue(control.id, "Label")
    Case "Button1"
        '<CODE>
    End Select
End Sub

Sub Ribbon_TogglebuttonClick(control As IRibbonControl, pressed As Boolean)
    Ribbon_SetValue control, "pressed", pressed
    Select Case Ribbon_GetValue(control.id, "Label")
    Case "Button1"
        '<CODE>
    End Select
End Sub

Sub Ribbon_EditboxChanged(control As IRibbonControl, Text As String)
    Ribbon_SetValue control, "Text", Text
    Select Case Ribbon_GetValue(control.id, "Label")
    Case Is = "EditBox1"
        Select Case Text
            '<CODE>
        End Select
    End Select
End Sub

Sub Ribbon_ComboboxChanged(control As IRibbonControl, Text As String)
    Ribbon_SetValue control, "Text", Text
    Select Case Ribbon_GetValue(control.id, "Label")
    Case "Combo 1"
        Select Case Text
        Case "Item 1"
            '<CODE>
        End Select
    End Select
End Sub

'Sub Ribbon_GalleryClick(control As IRibbonControl, id As String, index As Integer)
'    Ribbon_SetValue control, "Text", LabelOf(id)
'    Select Case LabelOf(control.id)
'    Case "Gallery 1"
'        Select Case LabelOf(id)
'        Case "G1 Item 1"
'        '<CODE>
'        End Select
'    End Select
'End Sub

'Sub Ribbon_DropdownClick(control As IRibbonControl, id As String, index As Integer)
'    Ribbon_SetValue control, "Text", LabelOf(id)
'    Select Case LabelOf(control.id)
'    Case "DD1"
'        Select Case LabelOf(id)
'        Case "Item1"
'            '<CODE>
'        End Select
'    End Select
'End Sub

Sub Ribbon_getVisible(control As IRibbonControl, ByRef returnedVal)
    returnedVal = Ribbon_GetValue(control.id, "Visible")
End Sub

Sub Ribbon_getSize(control As IRibbonControl, ByRef returnedVal)
    returnedVal = Ribbon_GetValue(control.id, "Size")
End Sub

Sub Ribbon_getLabel(control As IRibbonControl, ByRef returnedVal)
    returnedVal = Ribbon_GetValue(control.id, "Label")
End Sub

Sub Ribbon_getScreenTip(control As IRibbonControl, ByRef returnedVal)
    returnedVal = Ribbon_GetValue(control.id, "ScreenTip")
End Sub

Sub Ribbon_getSuperTip(control As IRibbonControl, ByRef returnedVal)
    returnedVal = Ribbon_GetValue(control.id, "superTip")
End Sub

Sub Ribbon_getDescription(control As IRibbonControl, ByRef returnedVal)
    returnedVal = Ribbon_GetValue(control.id, "Description")
End Sub

Sub Ribbon_getShowLabel(control As IRibbonControl, ByRef returnedVal)
    returnedVal = Ribbon_GetValue(control.id, "ShowLabel")
End Sub

Sub Ribbon_getShowImage(control As IRibbonControl, ByRef returnedVal)
    returnedVal = Ribbon_GetValue(control.id, "ShowImage")
End Sub

Sub Ribbon_getEnabled(control As IRibbonControl, ByRef returnedVal)
    returnedVal = Ribbon_GetValue(control.id, "Enabled")
End Sub

Sub Ribbon_getPressed(control As IRibbonControl, ByRef returnedVal)
    returnedVal = Ribbon_GetValue(control.id, "Pressed")
End Sub

Sub Ribbon_getEditboxText(control As IRibbonControl, ByRef returnedVal)
    returnedVal = Ribbon_GetValue(control.id, "Text")
End Sub


'uAuthor	UserForm



'* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
'* Module     : uAuthor
'* Created    : 06-10-2022 10:34
'* Author     : Anastasiou Alex
'* Contacts   : AnastasiouAlex@gmail.com
'*
'* GITHUB     : https://github.com/AlexOfRhodes
'* YOUTUBE    : https://www.youtube.com/channel/UC5QH3fn1zjx0aUjRER_rOjg
'* VK         : https://vk.com/video/playlist/735281600_1
'* DONATE     : http://paypal.me/alexofrhodes
'* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *

Option Explicit

Const AUTHOR_EMAIL = "anastasioualex@gmail.com"

#If VBA7 Then
    Private Declare PtrSafe Function InternetGetConnectedState Lib "wininet.dll" (ByRef dwFlags As Long, ByVal dwReserved As Long) As Long
    Private Declare PtrSafe Function GetSystemMetrics Lib "user32" (ByVal nIndex As Long) As Long
    
    Private Declare PtrSafe Function SetWindowLong Lib "user32" Alias "SetWindowLongA" (ByVal hwnd As Long, ByVal nIndex As Long, ByVal dwNewLong As Long) As Long
    Private Declare PtrSafe Function FindWindow Lib "user32" Alias "FindWindowA" (ByVal lpClassName As String, ByVal lpWindowName As String) As Long
    Private Declare PtrSafe Function FindWindowA Lib "user32" (ByVal lpClassName As String, ByVal lpWindowName As String) As Long
    Private Declare PtrSafe Function GetWindowLong Lib "user32" Alias "GetWindowLongA" (ByVal hwnd As Long, ByVal nIndex As Long) As Long
    Private Declare PtrSafe Function DrawMenuBar Lib "user32" (ByVal hwnd As Long) As Long
    Private Declare PtrSafe Function SetLayeredWindowAttributes Lib "user32" (ByVal hwnd As Long, ByVal crKey As Long, ByVal bAlpha As Byte, ByVal dwFlags As Long) As Long
#Else
    Private Declare Function InternetGetConnectedState Lib "wininet.dll" (ByRef dwflags As Long, ByVal dwReserved As Long) As Long
    Private Declare Function GetSystemMetrics Lib "user32" (ByVal nIndex As Long) As Long
    
    
    Private Declare Function SetWindowLong Lib "user32" Alias "SetWindowLongA" (ByVal hWnd As Long, ByVal nIndex As Long, ByVal dwNewLong As Long) As Long
    Private Declare Function FindWindow Lib "user32" Alias "FindWindowA" (ByVal lpClassName As String, ByVal lpWindowName As String) As Long
    Private Declare Function FindWindowA Lib "user32" (ByVal lpClassName As String, ByVal lpWindowName As String) As Long
    Private Declare Function GetWindowLong Lib "user32" Alias "GetWindowLongA" (ByVal hWnd As Long, ByVal nIndex As Long) As Long
    Private Declare Function DrawMenuBar Lib "user32" (ByVal hWnd As Long) As Long
    Private Declare Function SetLayeredWindowAttributes Lib "user32" (ByVal hWnd As Long, ByVal crKey As Long, ByVal bAlpha As Byte, ByVal dwFlags As Long) As Long
#End If


Private Const GWL_STYLE As Long = (-16)
Private Const GWL_EXSTYLE As Long = (-20)
Private Const WS_CAPTION As Long = &HC00000
Private Const WS_EX_DLGMODALFRAME As Long = &H1
Private Const WS_EX_LAYERED = &H80000
Private Const LWA_COLORKEY = &H1
Private Const LWA_ALPHA = &H2
Private m_sngDownX As Single
Private m_sngDownY As Single

Public Function GetInternetConnectedState() As Boolean
    GetInternetConnectedState = InternetGetConnectedState(0&, 0&)
End Function

Private Sub MakeFormTransparent(Frm As Object, Optional Color As Variant)
    Dim formhandle As Long
    Dim bytOpacity As Byte
    formhandle = CLng(FindWindow(vbNullString, Frm.Caption))
    If IsMissing(Color) Then Color = vbWhite
    bytOpacity = 100
    SetWindowLong formhandle, GWL_EXSTYLE, GetWindowLong(formhandle, GWL_EXSTYLE) Or WS_EX_LAYERED
    Frm.BackColor = Color
    SetLayeredWindowAttributes formhandle, Color, bytOpacity, LWA_COLORKEY
End Sub

Private Sub MakeFormBorderless(Frm As Object)
    Dim lngWindow As Long
    Dim lFrmHdl As Long
    lFrmHdl = CLng(FindWindow(vbNullString, Frm.Caption))
    lngWindow = GetWindowLong(lFrmHdl, GWL_STYLE)
    lngWindow = lngWindow And (Not WS_CAPTION)
    SetWindowLong lFrmHdl, GWL_STYLE, lngWindow
    lngWindow = GetWindowLong(lFrmHdl, GWL_EXSTYLE)
    lngWindow = lngWindow And Not WS_EX_DLGMODALFRAME
    SetWindowLong lFrmHdl, GWL_EXSTYLE, lngWindow
    DrawMenuBar lFrmHdl
End Sub

Private Sub LVK_Click()
    FollowLink ("https://vk.com/video/playlist/735281600_1")
End Sub

Private Sub LExit_Click()
    Unload Me
End Sub

Private Sub LBLOG_Click()
    FollowLink ("https://alexofrhodes.github.io")
End Sub

Private Sub LGitHub_Click()
    FollowLink ("https://github.com/alexofrhodes")
End Sub

Private Sub LYouTube_Click()
    FollowLink ("https://www.youtube.com/channel/UC5QH3fn1zjx0aUjRER_rOjg")
End Sub

Private Sub LBuyMeACoffee_Click()
    FollowLink ("http://paypal.me/alexofrhodes")
End Sub

Private Function CLIP(Optional StoreText As String) As String
    Dim X As Variant
    X = StoreText
    With CreateObject("htmlfile")
        With .parentWindow.clipboardData
            Select Case True
            Case Len(StoreText)
                .SetData "text", X
            Case Else
                CLIP = .GetData("text")
            End Select
        End With
    End With
End Function

Private Sub LEmail_Click()
    If GetInternetConnectedState = False Then
        MsgBox "Seems Internet is not available"
        Exit Sub
    End If
    If OutlookCheck = True Then
        MailDev
    Else
        Dim out As String
        out = AUTHOR_EMAIL
        CLIP out
        MsgBox ("Seems Outlook is not available" & Chr(10) & _
        "DEV's email address " & vbNewLine & out & vbNewLine & "copied to clipboard")
    End If
End Sub

Function OutlookCheck() As Boolean
    Dim xOLApp As Object
    Set xOLApp = CreateObject("Outlook.Application")
    If Not xOLApp Is Nothing Then
        OutlookCheck = True
        Set xOLApp = Nothing
        Exit Function
    End If
    OutlookCheck = False
End Function


Sub MailDev()
    Dim OutApp As Object
    Dim OutMail As Object
    Dim strBody As String
    Set OutApp = CreateObject("Outlook.Application")
    Set OutMail = OutApp.CreateItem(0)
    On Error Resume Next
    With OutMail
        .To = AUTHOR_EMAIL
        .cc = vbNullString
        .BCC = vbNullString
        .Subject = "DEV REQUEST OR FEEDBACK FOR -CODE ARCHIVE-"
        .body = strBody
        .Display
    End With
    On Error GoTo 0
    Set OutMail = Nothing
    Set OutApp = Nothing
End Sub

Private Sub UserForm_Initialize()

    Dim screenWidth As Long
    Dim screenHeight As Long
    
    screenWidth = GetSystemMetrics(0)  ' SM_CXSCREEN
    screenHeight = GetSystemMetrics(1)  ' SM_CYSCREEN
    
    Me.Width = screenWidth
    Me.Height = screenHeight
    
    Frame1.Left = Me.Width / 2 - Image1.Width
    Frame1.Top = Me.Height / 2 - Image1.Height
        MakeFormBorderless Me
'    MakeFormTransparent Me, vbYellow
End Sub




'uIniEditor	UserForm


Option Explicit

Private WithEvents Emitter As EventListenerEmitter

Private Sub Label1_Click()
    FollowLink ThisWorkbook.Path & "\ExtraIniPaths.txt"
End Sub

Private Sub UserForm_Initialize()
    cbIniFiles.columnCount = 2
    cbIniFiles.ColumnWidths = "200;0"
    cbIniFiles.Font.Size = 12
    Dim out As New Collection
    FolderContents ThisWorkbook.Path, False, True, True, out, "*.ini"
    
    Dim fileArray
    fileArray = Split(TxtRead("ExtraIniPaths.txt"), vbLf)
    Dim item
    For Each item In fileArray
        If Trim(item) <> "" And FileExists(item) Then out.Add item, item
    Next
    If out.count > 0 Then
        Dim FileList()
        ReDim FileList(1 To out.count, 1 To 2)
        Dim i As Long
        For i = 1 To out.count
            FileList(i, 1) = Mid(out(i), InStrRev(out(i), "\") + 1)
            FileList(i, 2) = out(i)
        Next
        cbIniFiles.List = FileList
    End If
    
    cbSections.Font.Size = 12
    
    If out.count = 1 Then cbIniFiles.ListIndex = 0
    
    TextBox1.MultiLine = True
    TextBox1.Font.Size = 12
    
    bSaveChanges.Caption = vbNewLine & bSaveChanges.Caption
    
    Set Emitter = New EventListenerEmitter
    Emitter.AddEventListenerAll Me
End Sub

Private Sub bSaveChanges_Click()
    Dim ini As String: ini = SelectedIniFile
    Dim Changes, aChange, aKey, aValue
        Changes = Split(TextBox1.Text, vbNewLine)
    For Each aChange In Changes
        aKey = Trim(Split(aChange, "=")(0))
        aValue = Trim(Split(aChange, "=")(1))
        If aKey <> "" Then IniWriteKey ini, cbSections.Text, aKey, aValue
    Next
    If SelectedIniFile = RibbonIni Then Ribbon.InvalidateControl cbSections.Text
End Sub

Private Function SelectedIniFile()
    If cbIniFiles.ListIndex >= 0 Then SelectedIniFile = cbIniFiles.List(cbIniFiles.ListIndex, 1)
End Function

Private Sub cbIniFiles_BeforeUpdate(ByVal Cancel As MSForms.ReturnBoolean)
    Dim index As Long: index = cbIniFiles.ListIndex
    If index > -1 Then Exit Sub
    
    cbIniFiles.Text = Replace(cbIniFiles.Text, """", "")
    Dim ini As String: ini = cbIniFiles.Text: ini = Replace(ini, "/", "\")
    If Not ini Like "*.ini" Then Exit Sub
    If InStr(1, ini, ":") = 0 Then ini = Replace(ThisWorkbook.Path & "\" & ini, "\\", "\")
    If Not FileExists(ini) Then
        Dim folder As String: folder = getFileFolder(ini)
        If MsgBox("Create this?" & vbNewLine & ini, vbYesNo) = vbNo Then GoTo ExitPoint
        If folder <> "" Then FoldersCreate folder
        TxtOverwrite ini, ""
    End If
    If Not ListContains(cbIniFiles, ini, 1, False) Then
        cbIniFiles.AddItem
        cbIniFiles.List(cbIniFiles.ListCount - 1, 0) = getFileName(ini) & getFileExtension(ini)
        cbIniFiles.List(cbIniFiles.ListCount - 1, 1) = ini

        If InStr(1, ini, ThisWorkbook.Path) = 0 Then
            Dim ExtraIniPathsFile As String
                ExtraIniPathsFile = ThisWorkbook.Path & "\ExtraIniPaths.txt"
            Dim fileText As String
                fileText = TxtRead(ExtraIniPathsFile)
            If InStr(1, fileText, "ini") = 0 Then
                TxtOverwrite ExtraIniPathsFile, fileText & vbLf & ini
            End If
        End If
        cbIniFiles.ListIndex = cbIniFiles.ListCount - 1
    End If
    cbIniFiles_Change
    
ExitPoint:

End Sub

Private Sub cbIniFiles_Change()
    If cbIniFiles.ListIndex > -1 Then
        cbSections.Clear
        cbSections.Text = ""
        TextBox1.Text = ""
        Dim arr: arr = IniSections(SelectedIniFile)
        cbSections.List = arr
    End If
End Sub

Private Sub GetInfo_Click()
    uAuthor.Show
End Sub

Private Sub cbSections_KeyUp(ByVal KeyCode As MSForms.ReturnInteger, ByVal Shift As Integer)
    Dim ini As String: ini = SelectedIniFile: If ini = "" Then Exit Sub
    If Not IniSectionExists(ini, cbSections.Text) Then
        TextBox1.Value = ""
    ElseIf cbSections.ListIndex >= 0 Then
        TextBox1.Value = Replace(Join(IniReadSection(ini, cbSections.Text), vbLf), "=", " = ")
    End If
End Sub

Private Sub cbSections_Change()
    Dim ini As String: ini = SelectedIniFile: If ini = "" Then Exit Sub
    If cbSections.ListIndex >= 0 Then
        TextBox1.Value = Replace(Join(IniReadSection(ini, cbSections.Text), vbLf), "=", " = ")
    End If
End Sub

Private Sub Emitter_LabelMouseOut(Label As MSForms.Label)
    If Label.Picture = 0 Then If Label.BackColor <> MyColors.FormBackgroundDarkGray Then Label.BackColor = MyColors.FormBackgroundDarkGray '&H80B91E'&H534848
End Sub

Private Sub Emitter_LabelMouseOver(Label As MSForms.Label)
    If Label.Picture = 0 Then If Label.BackColor <> MyColors.FormSelectedGreen Then Label.BackColor = MyColors.FormSelectedGreen '&H80B91E '&H808080
End Sub

'm_INI	Module

Option Explicit

'____API METHOD______

#If VBA7 Then
    Public Declare PtrSafe Function GetPrivateProfileString Lib "kernel32" Alias "GetPrivateProfileStringA" (ByVal lpApplicationName As String, ByVal lpKeyName As Any, ByVal lpDefault As String, ByVal lpReturnedString As String, ByVal nSize As Long, ByVal lpFileName As String) As Long
    Public Declare PtrSafe Function WritePrivateProfileString Lib "kernel32" Alias "WritePrivateProfileStringA" (ByVal lpApplicationName As String, ByVal lpKeyName As Any, ByVal lpString As Any, ByVal lpFileName As String) As Long
    Public Declare PtrSafe Function GetPrivateProfileSection Lib "kernel32" Alias "GetPrivateProfileSectionA" (ByVal lpAppName As String, ByVal lpReturnedString As String, ByVal nSize As Long, ByVal lpFileName As String) As Long
#Else
    public declare Function GetPrivateProfileString Lib "kernel32" Alias "GetPrivateProfileStringA" (ByVal lpApplicationName As String, ByVal lpKeyName As Any, ByVal lpDefault As String, ByVal lpReturnedString As String, ByVal nSize As Long, ByVal lpFileName As String) As Long
    public declare Function WritePrivateProfileString Lib "kernel32" Alias "WritePrivateProfileStringA" (ByVal lpApplicationName As String, ByVal lpKeyName As Any, ByVal lpString As Any, ByVal lpFileName As String) As Long
    public declare Function GetPrivateProfileSection Lib "kernel32" Alias "GetPrivateProfileSectionA" (ByVal lpAppName As String, ByVal lpReturnedString As String, ByVal nSize As Long, ByVal lpFileName As String) As Long
#End If

Public Function IniReadKey(IniFileName As String, ByVal Sect As String, ByVal Keyname As String) As String
    Dim Worked As Long
    Dim RetStr As String * 128
    Dim StrSize As Long
    Dim iNoOfCharInIni As Long: iNoOfCharInIni = 0
    Dim sIniString As String: sIniString = ""
    If Sect = "" Or Keyname = "" Then
        MsgBox "Section Or Key To Read Not Specified !!!", vbExclamation, "INI"
    Else
        Dim sProfileString As String: sProfileString = ""
        RetStr = Space(128)
        StrSize = Len(RetStr)
        Worked = GetPrivateProfileString(Sect, Keyname, "", RetStr, StrSize, IniFileName)
        If Worked Then
            iNoOfCharInIni = Worked
            sIniString = Left$(RetStr, Worked)
        End If
    End If
    IniReadKey = sIniString
'---- result for reading "settings1", "string1" ----
'aaa
End Function

Public Sub IniWriteKey(IniFileName As String, ByVal Sect As String, ByVal Keyname As String, ByVal Wstr As String)

    Dim Worked As Long
    Dim iNoOfCharInIni As Long

    iNoOfCharInIni = 0
    Dim sIniString As String: sIniString = ""
    If Sect = "" Or Keyname = "" Then
        MsgBox "Section Or Key To Write Not Specified !!!", vbExclamation, "INI"
    Else
        Worked = WritePrivateProfileString(Sect, Keyname, Wstr, IniFileName)
        If Worked Then
            iNoOfCharInIni = Worked
            sIniString = Wstr
        End If
    End If
    
    'This macro also creates the file & section & key if they doesn't exist
    'or overwrites the value for existing key
    
'    Example
'        IniWriteKey iniFile, "settings1", "string1", "newval"
'    Result
'        [settings1]
'            string1 = newval

End Sub

Public Function IniSections(iniFile As String) As Variant
    IniSections = Split(Replace(Replace(Join(Filter(Split(Replace(TxtRead(iniFile), vbLf, vbNewLine), vbNewLine), "[", True), vbNewLine), "[", ""), "]", ""), vbNewLine)

'    file content
'        [settings1]
'            string1 = aaa
'            string2 = bbb
'        [settings2]
'            string1 = ccc
'            string2 = ddd
'    Example
'        var = IniSections(iniFile)
'    Result
'        Array("settings1","settings2")
End Function

Public Function IniSectionExists(iniFile As String, Section As String)
    IniSectionExists = (InStr(1, TxtRead(iniFile), "[" & Section & "]") > 0)
End Function

Public Function IniSectionKeys(FileName As String, Section As String) As Variant
    Dim arr As Variant
    arr = IniReadSection(FileName, Section)
    Dim out As Variant
    ReDim out(UBound(arr))
    Dim i As Long
    For i = LBound(arr) To UBound(arr)
        out(i) = Trim(Split(arr(i), "=")(0))
    Next i
    IniSectionKeys = out
    
'    Example
'        var = IniSectionKeys IniFile, "Settings1"
'    Result
'        Array("string1", "string2"
End Function

Public Function IniReadSection(FileName As String, Section As String) As Variant
    Dim RetVal As String * 255
    Dim v As Long:      v = GetPrivateProfileSection(Section, RetVal, 255, FileName)
    Dim s As String:    s = Left(RetVal, v + 0)
    Dim VL As Variant:  VL = Split(s, Chr$(0))
    VL = ArrayRemoveEmptyElements(VL)
    IniReadSection = VL
    
'    Example
'        var = IniReadSection IniFile, "settings1"
'    Result
'        Array("string1=aaa","string2=bbb")
End Function


'EventListenerEmitter	Class

'------------------------
'userform declararations
'------------------------
'   Private WithEvents Emitter As EventListeneRemitter

'------------------------
'in userform_initialise
'------------------------
'   Set Emitter = New EventListeneRemitter
'   Emitter.AddEventListenerAll Me

Rem Developer Robert Todar

Option Explicit
Option Compare Text

' Array of all the different event listeners for every userform control and the form itself
Private EventList() As New EventListenerItem

' All the current possible events that can be emitted.
' Note, EmittedEvent is sent for all events!
Public Event EmittedEvent(ByRef control As Object, ByVal EventType As EmittedEvent, ByRef EventParameters As Collection)
Public Event Click(ByRef control As Object)
Public Event DblClick(ByRef control As Object, ByRef Cancel As MSForms.ReturnBoolean)
Public Event KeyUp(ByRef control As Object, ByRef KeyCode As MSForms.ReturnInteger, ByRef Shift As Integer)
Public Event KeyDown(ByRef control As Object, ByRef KeyCode As MSForms.ReturnInteger, ByRef Shift As Integer)
Public Event MouseOver(ByRef control As Object)
Public Event MouseOut(ByRef control As Object)
Public Event MouseMove(ByRef control As Object, ByRef Shift As Integer, ByRef X As Single, ByRef Y As Single)
Public Event Focus(ByRef control As Object)
Public Event Blur(ByRef control As Object)
Public Event Change(ByRef control As Object)

' Events to Labels
Public Event LabelMouseOver(ByRef Label As MSForms.Label)
Public Event LabelMouseOut(ByRef Label As MSForms.Label)
Public Event LabelClick(ByRef Label As MSForms.Label)
Public Event LabelDoubleClick(ByRef Label As MSForms.Label, ByRef Cancel As MSForms.ReturnBoolean)
Public Event LabelMouseMove(ByRef Label As MSForms.Label, ByRef Shift As Integer, ByRef X As Single, ByRef Y As Single)

' Events to Textboxes
Public Event TextboxFocus(ByRef Textbox As MSForms.Textbox)
Public Event TextboxBlur(ByRef Textbox As MSForms.Textbox)
Public Event TextboxMouseOver(ByRef Textbox As MSForms.Textbox)
Public Event TextboxMouseOut(ByRef Textbox As MSForms.Textbox)
Public Event TextboxClick(ByRef Textbox As MSForms.Textbox)
Public Event TextboxDoubleClick(ByRef Textbox As MSForms.Textbox, ByRef Cancel As MSForms.ReturnBoolean)
Public Event TextboxMouseMove(ByRef Textbox As MSForms.Textbox, ByRef Shift As Integer, ByRef X As Single, ByRef Y As Single)

' Events to CommandButtons
Public Event CommandButtonMouseOver(ByRef CommandButton As MSForms.CommandButton)
Public Event CommandButtonMouseOut(ByRef CommandButton As MSForms.CommandButton)
Public Event CommandButtonClick(ByRef CommandButton As MSForms.CommandButton)
Public Event CommandButtonDoubleClick(ByRef CommandButton As MSForms.CommandButton, ByRef Cancel As MSForms.ReturnBoolean)
Public Event CommandButtonMouseMove(ByRef CommandButton As MSForms.CommandButton, ByRef Shift As Integer, ByRef X As Single, ByRef Y As Single)

' Types of events that can occur
Public Enum EmittedEvent
    Click
    DoubleClick
    MouseMove
    MouseOut
    MouseOver
    MouseDown
    MouseUp
    KeyUp
    KeyDown
    Focus
    Blur
End Enum

' Called by EventListenerItem class - main entryway of emitting all events
Public Sub EmitEvent(ByRef control As Object, ByVal EventType As EmittedEvent, Optional ByRef EventParameters As Collection)
    ' Event raised for all events. This is a way for the user to collect from a single location.
    RaiseEvent EmittedEvent(control, EventType, EventParameters)

    ' Specific events
    Select Case EventType

    Case Click
        RaiseEvent Click(control)

    Case DoubleClick
        RaiseEvent DblClick(control, EventParameters("Cancel"))

    Case KeyUp
        RaiseEvent KeyUp(control, EventParameters("KeyCode"), EventParameters("Shift"))

    Case KeyDown
        RaiseEvent KeyDown(control, EventParameters("KeyCode"), EventParameters("Shift"))

    Case MouseOver
        RaiseEvent MouseOver(control)

    Case MouseOut
        RaiseEvent MouseOut(control)

    Case Focus
        RaiseEvent Focus(control)

    Case Blur
        RaiseEvent Blur(control)

    Case MouseMove
        RaiseEvent MouseMove(control, EventParameters("Shift"), EventParameters("X"), EventParameters("Y"))

    End Select

    ' Call the specific control type events
    Select Case TypeName(control)
    Case "Label"
        EmitLabelEvent control, EventType, EventParameters

    Case "Textbox"
        EmitTextboxEvent control, EventType, EventParameters

    Case "CommandButton"
        EmitCommandButtonEvent control, EventType, EventParameters

    End Select
End Sub

' Events for Labels
Private Sub EmitLabelEvent(ByRef Label As MSForms.Label, ByVal EventType As String, ByRef EventParameters As Collection)
    Select Case EventType
    Case EmittedEvent.Click
        RaiseEvent LabelClick(Label)

    Case EmittedEvent.DoubleClick
        RaiseEvent LabelDoubleClick(Label, EventParameters("Cancel"))

    Case EmittedEvent.MouseOver
        RaiseEvent LabelMouseOver(Label)

    Case EmittedEvent.MouseOut
        RaiseEvent LabelMouseOut(Label)

    Case EmittedEvent.MouseMove
        RaiseEvent MouseMove(Label, EventParameters("Shift"), EventParameters("X"), EventParameters("Y"))
    End Select
End Sub

' Events for Textboxes
Private Sub EmitTextboxEvent(ByRef Textbox As MSForms.Textbox, ByVal EventType As String, ByRef EventParameters As Collection)
    Select Case EventType
    Case EmittedEvent.Blur
        RaiseEvent TextboxBlur(Textbox)

    Case EmittedEvent.Focus
        RaiseEvent TextboxFocus(Textbox)

    Case EmittedEvent.Click
        RaiseEvent TextboxClick(Textbox)

    Case EmittedEvent.DoubleClick
        RaiseEvent TextboxDoubleClick(Textbox, EventParameters("Cancel"))

    Case EmittedEvent.MouseOver
        RaiseEvent TextboxMouseOver(Textbox)

    Case EmittedEvent.MouseOut
        RaiseEvent TextboxMouseOut(Textbox)

    Case EmittedEvent.MouseMove
        RaiseEvent MouseMove(Textbox, EventParameters("Shift"), EventParameters("X"), EventParameters("Y"))
    End Select
End Sub

' Events for CommandButton
Private Sub EmitCommandButtonEvent(ByRef CommandButton As MSForms.CommandButton, ByVal EventType As String, ByRef EventParameters As Collection)
    Select Case EventType
    Case EmittedEvent.Click
        RaiseEvent CommandButtonClick(CommandButton)

    Case EmittedEvent.DoubleClick
        RaiseEvent CommandButtonDoubleClick(CommandButton, EventParameters("Cancel"))

    Case EmittedEvent.MouseOver
        RaiseEvent CommandButtonMouseOver(CommandButton)

    Case EmittedEvent.MouseOut
        RaiseEvent CommandButtonMouseOut(CommandButton)

    Case EmittedEvent.MouseMove
        RaiseEvent MouseMove(CommandButton, EventParameters("Shift"), EventParameters("X"), EventParameters("Y"))
    End Select
End Sub

' MUST CALL THIS IF YOU WANT TO programmatically SET CONTROL! OTHERWISE, EVENT'S WILL BE OFF!
Public Sub SetFocusToControl(ByRef control As Object)
    'If the user was to set focus through VBA then this code will fall apart considering
    'it is unaware of that event occurring.
    If Not control Is Nothing Then
        control.SetFocus
        EmitEvent control, Focus
    End If
End Sub

' ADD EVENT Listeners ON SPECIFIC CONTROLS - ALSO CALLED BY AddEventListenerAll
Public Sub AddEventListener(ByRef control As Object)
    ' Events are stored in a private EventListenerItem array
    If IsArrayEmpty(EventList) Then
        ReDim EventList(0 To 0)
    Else
        ReDim Preserve EventList(0 To UBound(EventList) + 1)
    End If

    'CALL AddEventListener IN EventListenerItem. THIS IS KEPT IN
    EventList(UBound(EventList)).AddEventListener control, Me
End Sub

'ADD EVENT Listener TO ALL CONTROLS INCLUDING THE FORM
Public Sub AddEventListenerAll(ByRef Form As Object)
    AddEventListener Form

    Dim control As MSForms.control
    For Each control In Form.Controls
        AddEventListener control
    Next control
End Sub

Private Function IsArrayEmpty(arr As Variant) As Boolean
    ''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
    ' CPEARSON: http://www.cpearson.com/excel/VBAArrays.htm
    ' This function tests whether the array is empty (unallocated). Returns TRUE or FALSE.
    '
    ' The VBA IsArray function indicates whether a variable is an array, but it does not
    ' distinguish between allocated and unallocated arrays. It will return TRUE for both
    ' allocated and unallocated arrays. This function tests whether the array has actually
    ' been allocated.
    '''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''

    Err.Clear
    On Error Resume Next
    If IsArray(arr) = False Then
        ' we weren't passed an array, return True
        IsArrayEmpty = True
    End If

    ' Attempt to get the UBound of the array. If the array is
    ' unallocated, an error will occur.
    Dim UB As Long
    UB = UBound(arr, 1)
    If (Err.Number <> 0) Then
        IsArrayEmpty = True
    Else
        ''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
        ' On rare occasion, under circumstances I cannot reliably replicate, Err.Number
        ' will be 0 for an unallocated, empty array. On these occasions, LBound is 0 and
        ' UBound is -1. To accommodate the weird behavior, test to see if LB > UB.
        ' If so, the array is not allocated.
        ''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
        Err.Clear
        Dim LB As Long
        LB = LBound(arr)
        If LB > UB Then
            IsArrayEmpty = True
        Else
            IsArrayEmpty = False
        End If
    End If
End Function


'EventListenerItem	Class

Rem Developer Robert Todar

Option Explicit
Option Compare Text

'SET FROM AddEventListener - NEEDED TO EMIT EVENT BACK TO IT.
Private WithEvents Emitter As EventListenerEmitter

'CONTROLS THAT HAVE THE EVENTS
Private WithEvents Form As MSForms.UserForm
Private WithEvents Textbox As MSForms.Textbox
Private WithEvents Label As MSForms.Label
Private WithEvents CommandButton As MSForms.CommandButton
Private WithEvents ComboBox As MSForms.ComboBox
Private WithEvents Frame As MSForms.Frame

Private Type state
    control As Object
    IsHoveredControl As Boolean
    IsFocusedControl As Boolean
End Type

Private this As state

'TODO
' - ADD EVENTS FOR ALL USERFORM CONTROLS
' - ADD MORE EVENTS THAN THE LIST BELOW
' - DOUBLE CHECK THAT EMITTS ARE ALL CURRENT

'CURRENT EVENTS (WILL ADD MORE AS I HAVE NEED)
' - MouseOver
' - MouseOut
' - MouseMove
' - Click
' - DblClick
' - Focus
' - Blur
' - KeyUp
' - KeyDown

' The Only public method. This will be called from EventListeneRemitter class module
Public Sub AddEventListener(ByRef ControlOrForm As Object, ByRef EmitterReference As EventListenerEmitter)
    ' Capture the emitter class. This will be used to emit events from each control.
    Set Emitter = EmitterReference

    ' This is used to compare and check to see if this is the control triggering the event.
    Set this.control = ControlOrForm

    ' Set control (or form) based on its type
    Select Case TypeName(ControlOrForm)
    Case "CommandButton"
        Set CommandButton = ControlOrForm

    Case "ComboBox"
        Set ComboBox = ControlOrForm

    Case "Frame"
        Set Frame = ControlOrForm

    Case "Label"
        Set Label = ControlOrForm

    Case "TextBox"
        Set Textbox = ControlOrForm

    Case Else

        If TypeOf ControlOrForm Is MSForms.UserForm Then
            Set Form = ControlOrForm
        Else
            Rem todo
            Rem err.Raise 5, TypeName(Me), "Invalid control: Currently unable to listen to events on " & TypeName(ControlOrForm)
        End If

    End Select
End Sub

'***********************************************************************************
' Helper functions
'***********************************************************************************

' Called on mousemove event. This is a way of creating a mouseover and mouseout event.
Private Sub CheckIfHoveredControl()
    If Not this.IsHoveredControl Then
        this.IsHoveredControl = True
        Emitter.EmitEvent this.control, MouseOver
    End If
End Sub

' Called on mousemove event. This is a way of creating a mouseover and mouseout event.
Private Sub CheckIfFocusedControl()
    If Not this.IsFocusedControl Then
        If TypeName(this.control) = "Frame" Then
            Emitter.SetFocusToControl this.control.ActiveControl
        Else
            this.IsFocusedControl = True
            Emitter.EmitEvent this.control, Focus
        End If
    End If
End Sub

' Simple Collection factory — for ease of use.
Private Function ToCollection(ParamArray keyValuePairs() As Variant) As Collection
    ' Check to see if there is even number of parameters
    Dim ArrayLenght As Long
    ArrayLenght = UBound(keyValuePairs) - LBound(keyValuePairs) + 1

    If ArrayLenght Mod 2 <> 0 Then
        Err.Raise 5, TypeName(Me), "Invalid parameters: expecting key/value pairs, but received an odd number of arguments."
    End If

    Set ToCollection = New Collection
    Dim index As Long
    For index = LBound(keyValuePairs) To UBound(keyValuePairs) Step 2
        ToCollection.Add keyValuePairs(index + 1), keyValuePairs(index)
    Next index
End Function

'***********************************************************************************
' EVENTS
'***********************************************************************************

' ONCE AN EVENT HAS EMMITED, EACH EVENTListenerITEM WILL LISTEN FOR THAT EVENT
Private Sub Emitter_EmittedEvent(ByRef control As Object, ByVal EventName As EmittedEvent, ByRef EventParameters As Collection)
    ' CREATE A MOUSEOVER MOUSEOUT EVENTS
    Select Case EventName

    Case MouseOver
        If this.control.Name <> control.Name And this.IsHoveredControl Then
            this.IsHoveredControl = False
            Emitter.EmitEvent this.control, MouseOut
        End If

    Case Focus
        If this.control.Name <> control.Name And this.IsFocusedControl Then
            this.IsFocusedControl = False
            Emitter.EmitEvent this.control, Blur
        ElseIf this.control.Name = control.Name And this.IsFocusedControl = False Then
            this.IsFocusedControl = True
        End If

    End Select
End Sub

'NOTE: Anastasiou Alex (vbArc)
'
'it seems you can attach native events to the emitter (even though it doesn't exist in the class)
'like the following example
'
'Private Sub Emitter_LabelMouseOut(label As MSForms.label)
'End Sub
'
'Private Sub Emitter_LabelMouseOver(label As MSForms.label)
'End Sub

'------------------------------------------------------------------------
' USERFORM
'------------------------------------------------------------------------
Private Sub Form_Click()
    Emitter.EmitEvent this.control, Click
End Sub

Private Sub Form_DblClick(ByVal Cancel As MSForms.ReturnBoolean)
    Emitter.EmitEvent this.control, DoubleClick, ToCollection("Cancel", Cancel)
End Sub

Private Sub Form_MouseMove(ByVal Button As Integer, ByVal Shift As Integer, ByVal X As Single, ByVal Y As Single)
    CheckIfHoveredControl
    Emitter.EmitEvent this.control, MouseMove, ToCollection("Button", Button, "Shift", Shift, "X", X, "Y", Y)
End Sub

Private Sub Form_MouseDown(ByVal Button As Integer, ByVal Shift As Integer, ByVal X As Single, ByVal Y As Single)
    Emitter.EmitEvent this.control, MouseDown, ToCollection("Button", Button, "Shift", Shift, "X", X, "Y", Y)
End Sub

Private Sub Form_MouseUp(ByVal Button As Integer, ByVal Shift As Integer, ByVal X As Single, ByVal Y As Single)
    Emitter.EmitEvent this.control, MouseUp, ToCollection("Button", Button, "Shift", Shift, "X", X, "Y", Y)
End Sub

'------------------------------------------------------------------------
' COMMAND BUTTON
'------------------------------------------------------------------------
Private Sub CommandButton_Click()
    Emitter.EmitEvent this.control, Click
End Sub

Private Sub CommandButton_DblClick(ByVal Cancel As MSForms.ReturnBoolean)
    Emitter.EmitEvent this.control, DoubleClick, ToCollection("Cancel", Cancel)
End Sub

Private Sub CommandButton_MouseMove(ByVal Button As Integer, ByVal Shift As Integer, ByVal X As Single, ByVal Y As Single)
    CheckIfHoveredControl
    Emitter.EmitEvent this.control, MouseMove, ToCollection("Button", Button, "Shift", Shift, "X", X, "Y", Y)
End Sub

Private Sub CommandButton_MouseUp(ByVal Button As Integer, ByVal Shift As Integer, ByVal X As Single, ByVal Y As Single)
    CheckIfFocusedControl
    Emitter.EmitEvent this.control, MouseUp, ToCollection("Button", Button, "Shift", Shift, "X", X, "Y", Y)
End Sub

Private Sub CommandButton_MouseDown(ByVal Button As Integer, ByVal Shift As Integer, ByVal X As Single, ByVal Y As Single)
    Emitter.EmitEvent this.control, MouseDown, ToCollection("Button", Button, "Shift", Shift, "X", X, "Y", Y)
End Sub

Private Sub CommandButton_KeyUp(ByVal KeyCode As MSForms.ReturnInteger, ByVal Shift As Integer)
    CheckIfFocusedControl
    Emitter.EmitEvent this.control, KeyUp, ToCollection("KeyCode", KeyCode, "Shift", Shift)
End Sub

Private Sub CommandButton_KeyDown(ByVal KeyCode As MSForms.ReturnInteger, ByVal Shift As Integer)
    Emitter.EmitEvent this.control, KeyDown, ToCollection("KeyCode", KeyCode, "Shift", Shift)
End Sub

'------------------------------------------------------------------------
' LABEL
'------------------------------------------------------------------------
Private Sub Label_Click()
    Emitter.EmitEvent this.control, Click
End Sub

Private Sub Label_DblClick(ByVal Cancel As MSForms.ReturnBoolean)
    Emitter.EmitEvent this.control, DoubleClick, ToCollection("Cancel", Cancel)
End Sub

Private Sub Label_MouseMove(ByVal Button As Integer, ByVal Shift As Integer, ByVal X As Single, ByVal Y As Single)
    CheckIfHoveredControl
    Emitter.EmitEvent this.control, MouseMove, ToCollection("Button", Button, "Shift", Shift, "X", X, "Y", Y)
End Sub

Private Sub Label_MouseDown(ByVal Button As Integer, ByVal Shift As Integer, ByVal X As Single, ByVal Y As Single)
    Emitter.EmitEvent this.control, MouseDown, ToCollection("Button", Button, "Shift", Shift, "X", X, "Y", Y)
End Sub

Private Sub Label_MouseUp(ByVal Button As Integer, ByVal Shift As Integer, ByVal X As Single, ByVal Y As Single)
    Emitter.EmitEvent this.control, MouseUp, ToCollection("Button", Button, "Shift", Shift, "X", X, "Y", Y)
End Sub

'------------------------------------------------------------------------
' Frame
'------------------------------------------------------------------------
Private Sub Frame_Click()
    Emitter.EmitEvent this.control, Click
End Sub

Private Sub Frame_DblClick(ByVal Cancel As MSForms.ReturnBoolean)
    Emitter.EmitEvent this.control, DoubleClick, ToCollection("Cancel", Cancel)
End Sub

Private Sub Frame_MouseMove(ByVal Button As Integer, ByVal Shift As Integer, ByVal X As Single, ByVal Y As Single)
    CheckIfHoveredControl
    Emitter.EmitEvent this.control, MouseMove, ToCollection("Button", Button, "Shift", Shift, "X", X, "Y", Y)
End Sub

Private Sub Frame_MouseDown(ByVal Button As Integer, ByVal Shift As Integer, ByVal X As Single, ByVal Y As Single)
    CheckIfFocusedControl        'FRAME DOESN'T TAKE FOCUS BUT ACTIVE CONTROL IN FRAME DOES
    Emitter.EmitEvent this.control, MouseDown
End Sub

Private Sub Frame_MouseUp(ByVal Button As Integer, ByVal Shift As Integer, ByVal X As Single, ByVal Y As Single)
    Emitter.EmitEvent this.control, MouseUp, ToCollection("Button", Button, "Shift", Shift, "X", X, "Y", Y)
End Sub

'------------------------------------------------------------------------
' Textbox
'------------------------------------------------------------------------
Private Sub Textbox_Click()
    Emitter.EmitEvent this.control, Click
End Sub

Private Sub Textbox_DblClick(ByVal Cancel As MSForms.ReturnBoolean)
    Emitter.EmitEvent this.control, DoubleClick, ToCollection("Cancel", Cancel)
End Sub

Private Sub Textbox_MouseMove(ByVal Button As Integer, ByVal Shift As Integer, ByVal X As Single, ByVal Y As Single)
    CheckIfHoveredControl
    Emitter.EmitEvent this.control, MouseMove, ToCollection("Button", Button, "Shift", Shift, "X", X, "Y", Y)
End Sub

Private Sub Textbox_MouseUp(ByVal Button As Integer, ByVal Shift As Integer, ByVal X As Single, ByVal Y As Single)
    CheckIfFocusedControl
    Emitter.EmitEvent this.control, MouseUp, ToCollection("Button", Button, "Shift", Shift, "X", X, "Y", Y)
End Sub

Private Sub Textbox_MouseDown(ByVal Button As Integer, ByVal Shift As Integer, ByVal X As Single, ByVal Y As Single)
    Emitter.EmitEvent this.control, MouseDown, ToCollection("Button", Button, "Shift", Shift, "X", X, "Y", Y)
End Sub

Private Sub Textbox_KeyUp(ByVal KeyCode As MSForms.ReturnInteger, ByVal Shift As Integer)
    CheckIfFocusedControl
    Emitter.EmitEvent this.control, KeyUp, ToCollection("KeyCode", KeyCode, "Shift", Shift)
End Sub

Private Sub Textbox_KeyDown(ByVal KeyCode As MSForms.ReturnInteger, ByVal Shift As Integer)
    Emitter.EmitEvent this.control, KeyDown, ToCollection("KeyCode", KeyCode, "Shift", Shift)
End Sub

'------------------------------------------------------------------------
' Combobox
'------------------------------------------------------------------------
Private Sub ComboBox_Click()
    Emitter.EmitEvent this.control, Click
End Sub

Private Sub ComboBox_DblClick(ByVal Cancel As MSForms.ReturnBoolean)
    Emitter.EmitEvent this.control, DoubleClick, ToCollection("Cancel", Cancel)
End Sub

Private Sub ComboBox_MouseMove(ByVal Button As Integer, ByVal Shift As Integer, ByVal X As Single, ByVal Y As Single)
    CheckIfHoveredControl
    Emitter.EmitEvent this.control, MouseMove, ToCollection("Button", Button, "Shift", Shift, "X", X, "Y", Y)
End Sub


'm_General	Module

Option Explicit

Enum MyColors
    FormBackgroundDarkGray = 4208182            ' BACKGROUND DARK GRAY
    FormSidebarMediumGray = 5457992             ' TILE COLORS LIGHTER GRAY
    FormSidebarMouseOverLightGray = &H808080    ' lighter light gray
    FormSelectedGreen = 8435998                 ' green tile
End Enum


Public Sub RotateImage3d()
    Dim img As Shape: Set img = ActiveSheet.Shapes(Application.Caller)
    Dim i As Long
    For i = 0 To 36 Step 1

        img.ThreeD.RotationY = i * 10
        '         img.ThreeD.RotationX = i * 10
         
        ActiveSheet.Shapes("imgGithub").ThreeD.RotationY = i * 10
        ActiveSheet.Shapes("imgBlog").ThreeD.RotationY = i * 10
        ActiveSheet.Shapes("imgYoutube").ThreeD.RotationY = i * 10
        ActiveSheet.Shapes("imgCoffee").ThreeD.RotationY = i * 10
        ActiveSheet.Shapes("imgMail").ThreeD.RotationY = i * 10
    
        DoEvents
    Next i
End Sub

Function FolderContents( _
            FolderOrZipFilePath As Variant, _
            LogFolders As Boolean, _
            LogFiles As Boolean, _
            ScanInSubfolders As Boolean, _
            out As Collection, _
            Optional Filter As String = "*") As Collection
    
    Dim oShell As Object
    Set oShell = CreateObject("Shell.Application")
    Dim oFi As Object
    For Each oFi In oShell.Namespace(FolderOrZipFilePath).Items
        If oFi.IsFolder Then
            If LogFolders Then
                out.Add oFi.Path & "\"
            End If
            If ScanInSubfolders Then FolderContents oFi.Path, LogFolders, LogFiles, ScanInSubfolders, out, Filter
        Else
            If LogFiles Then
                If UCase(oFi.Name) Like UCase(Filter) Then
                    out.Add oFi.Path
                End If
            End If
        End If
    Next
    Set FolderContents = out
    Set oShell = Nothing
End Function

Function getFileName(FilePath As String)
    getFileName = Mid(FilePath, InStrRev(FilePath, "\") + 1)
    getFileName = Left(getFileName, InStr(1, getFileName, ".") - 1)
End Function

Function getFileExtension(FilePath As String)
    Dim pos As Long: pos = InStrRev(FilePath, ".")
    If pos = 0 Then End
    getFileExtension = Mid(FilePath, pos)
End Function

Function getFileFolder(FilePath As String)
    getFileFolder = Left(FilePath, InStrRev(FilePath, "\"))
End Function

Sub FollowLink(FolderPath As String)
    If Right(FolderPath, 1) = "\" Then FolderPath = Left(FolderPath, Len(FolderPath) - 1)
    On Error Resume Next
    Dim oShell As Object
    Dim Wnd As Object
    Set oShell = CreateObject("Shell.Application")
    For Each Wnd In oShell.Windows
        If Wnd.Name = "File Explorer" Then
            If Wnd.document.folder.Self.Path = FolderPath Then Exit Sub
        End If
    Next Wnd
    FollowLink FolderPath
End Sub

Function TxtRead(sPath As Variant) As String
    If sPath = "" Or InStr(1, sPath, ":\") = 0 Then Exit Function
    Dim sTXT As String
    If Dir(sPath) = "" Then
        Debug.Print "File was not found."
        Debug.Print sPath
        Exit Function
    End If
    Open sPath For Input As #1
    Do Until EOF(1)
        Line Input #1, sTXT
        TxtRead = TxtRead & sTXT & vbLf
    Loop
    Close
    If Len(TxtRead) = 0 Then
        TxtRead = ""
    Else
        TxtRead = Left(TxtRead, Len(TxtRead) - 1)
    End If
End Function

Sub TxtOverwrite(sFile As String, sText As String)
    On Error GoTo ERR_HANDLER
    Dim FileNumber As Integer
    FileNumber = FreeFile
    Open sFile For Output As #FileNumber
    Print #FileNumber, sText
    Close #FileNumber
Exit_Err_Handler:
    Exit Sub
ERR_HANDLER:
    MsgBox "The following error has occurred" & vbCrLf & vbCrLf & _
    "Error Number: " & Err.Number & vbCrLf & _
    "Error Source: TxtOverwrite" & vbCrLf & _
    "Error Description: " & Err.Description, vbCritical, "An Error has Occurred!"
    GoTo Exit_Err_Handler
End Sub

Public Function ArrayRemoveEmptyElements(varArray As Variant) As Variant
    If UBound(varArray) = -1 Then ArrayRemoveEmptyElements = varArray: Exit Function
    Dim TempArray() As Variant
    Dim OldIndex As Integer
    Dim NewIndex As Integer
    ReDim TempArray(LBound(varArray) To UBound(varArray))
    For OldIndex = LBound(varArray) To UBound(varArray)
        If Not Trim(varArray(OldIndex) & " ") = "" Then
            TempArray(NewIndex) = varArray(OldIndex)
            NewIndex = NewIndex + 1
        End If
    Next OldIndex
    ReDim Preserve TempArray(LBound(varArray) To NewIndex - 1)
    ArrayRemoveEmptyElements = TempArray
    varArray = TempArray
End Function

Function Transpose2DArray(inputArray As Variant) As Variant
    Dim X As Long, yUbound As Long
    Dim Y As Long, xUbound As Long
    Dim TempArray As Variant
    xUbound = UBound(inputArray, 2)
    yUbound = UBound(inputArray, 1)
    ReDim TempArray(1 To xUbound, 1 To yUbound)
    For X = 1 To xUbound
        For Y = 1 To yUbound
            TempArray(X, Y) = inputArray(Y, X)
        Next Y
    Next X
    Transpose2DArray = TempArray
End Function

Sub FoldersCreate(FolderPath As String)
    On Error Resume Next
    Dim individualFolders() As String
    Dim tempFolderPath As String
    Dim ArrayElement As Variant
    individualFolders = Split(FolderPath, "\")
    For Each ArrayElement In individualFolders
        tempFolderPath = tempFolderPath & ArrayElement & "\"
        If FolderExists(tempFolderPath) = False Then
            MkDir tempFolderPath
        End If
    Next ArrayElement
End Sub

Function FolderExists(ByVal strPath As String) As Boolean
    On Error Resume Next
    FolderExists = ((GetAttr(strPath) And vbDirectory) = vbDirectory)
End Function

Sub KillFile(FilePath As String)
    On Error GoTo ErrorHandler
    Dim fso As New FileSystemObject, aFile As file
    If (fso.FileExists(FilePath)) Then
        SetAttr FilePath, vbNormal
        Set aFile = fso.GetFile(FilePath)
        aFile.Delete
    End If
    Exit Sub
ErrorHandler:
    If MsgBox("Could not delete " & vbNewLine & FilePath & vbNewLine & _
              "Cancel operation?", vbYesNo) = vbYes Then
        End
    Else
        Exit Sub
    End If
End Sub

Public Function FileExists(ByVal FileName As String) As Boolean
    If InStr(1, FileName, "\") = 0 Then Exit Function
    If Right(FileName, 1) = "\" Then FileName = Left(FileName, Len(FileName) - 1)
    FileExists = (Dir(FileName, vbArchive + vbHidden + vbReadOnly + vbSystem) <> "")
End Function


'm_VBE	Module

Option Explicit

Function ProceduresOfProject(TargetProject As vbProject) As Collection
    Dim Module As VBComponent
    Dim ProcKind As VBIDE.vbext_ProcKind
    Dim lineNum As Long
    Dim coll As New Collection
    Dim ProcedureName As String
    For Each Module In TargetProject.VBComponents
        If Module.Type = vbext_ct_StdModule Then
            With Module.CodeModule
                lineNum = .CountOfDeclarationLines + 1
                Do Until lineNum >= .CountOfLines
                    ProcedureName = .ProcOfLine(lineNum, ProcKind)
                    ' _ is used in events. Events may have the same name in different components
                    If InStr(1, ProcedureName, "_") = 0 Then
                        coll.Add ProcedureName
                    End If
                    lineNum = .ProcStartLine(ProcedureName, ProcKind) + .ProcCountLines(ProcedureName, ProcKind) + 1
                Loop
            End With
        End If
    Next Module
    Set ProceduresOfProject = coll
End Function

Function ProcedureExists( _
                        TargetProject As vbProject, _
                        ProcedureName As Variant) As Boolean
    Dim Procedures As Collection
    Set Procedures = ProceduresOfProject(TargetProject)
    Dim Procedure As Variant
    For Each Procedure In Procedures
        If UCase(CStr(Procedure)) = UCase(ProcedureName) Then
            ProcedureExists = True
            Exit Function
        End If
    Next
End Function


Function ListContains(ctrl As control, str As String, _
                        Optional ColumnIndexZeroBased As Long = -1, _
                        Optional CaseSensitive As Boolean = False) As Boolean
    Dim i      As Long
    Dim N      As Long
    Dim sTemp  As String
        If ColumnIndexZeroBased > ctrl.columnCount - 1 Or ColumnIndexZeroBased < 0 Then
            ColumnIndexZeroBased = -1
        End If
        N = ctrl.ListCount
        If ColumnIndexZeroBased <> -1 Then
            For i = N - 1 To 0 Step -1
                If CaseSensitive = True Then
                    sTemp = ctrl.List(i, ColumnIndexZeroBased)
                Else
                    str = LCase(str)
                    sTemp = LCase(ctrl.List(i, ColumnIndexZeroBased))
                End If
                If InStr(1, sTemp, str) > 0 Then
                    ListContains = True
                    Exit Function
                End If
            Next i
        Else
            Dim columnCount As Long
            N = ctrl.ListCount
            For i = N - 1 To 0 Step -1
                For columnCount = 0 To ctrl.columnCount - 1
                    If CaseSensitive = True Then
                        sTemp = ctrl.List(i, columnCount)
                    Else
                        str = LCase(str)
                        sTemp = LCase(ctrl.List(i, columnCount))
                    End If
                    If InStr(1, sTemp, str) > 0 Then
                        ListContains = True
                        Exit Function
                    End If
                Next columnCount
            Next i
        End If
End Function


