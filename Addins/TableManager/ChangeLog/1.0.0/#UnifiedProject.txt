'ThisWorkbook	Document

Option Explicit


'Sheet1	Document

Option Explicit


'uTableManager	UserForm

'* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
'* Author     : Anastasiou Alex
'* Contacts   : AnastasiouAlex@gmail.com
'*
'* GITHUB     : https://github.com/AlexOfRhodes
'* BLOG       : https://alexofrhodes.github.io
'* YOUTUBE    : https://www.youtube.com/channel/UC5QH3fn1zjx0aUjRER_rOjg
'* VK         : https://vk.com/video/playlist/735281600_1
'*
'* Support    : http://paypal.me/alexofrhodes
'*
'* Project    : Table Manager
'* Purpose    : View and Edit Tables
'* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *

Public canceledDatePicker As Boolean

Private WithEvents Emitter As EventListenerEmitter

Private TargetWorkbook      As Workbook
Private TargetWorksheet     As Worksheet
Private TargetTable         As ListObject
Private aLV                 As aListView
Private isDoubleClick       As Boolean
Private isAddingNewRow      As Boolean
Private currentSelection    As String
Private previousInputLength As Long
Private editorIndex         As Long
Private previousEditorIndex As Long
Private targetBanner        As Object

'/////////////////////////////////////////////
'///            INITIALIZE                 ///
'/////////////////////////////////////////////

Private Sub UserForm_Initialize()

    aUserform.Init(Me).MinimizeButton
    Set aLV = aListView.Init(ListViewTable)
    LoadWorkbooks
    LoadOptions
    setControlStyle
    SelectActivePath

End Sub

Sub LoadWorkbooks()
    aListBox.Init(ListboxWorkbook).LoadVBProjects
    Dim i As Long, ws As Worksheet, counter As Long
    'remove workbooks without any table
    For i = ListboxWorkbook.ListCount - 1 To 0 Step -1
        counter = 0
        With Workbooks(ListboxWorkbook.List(i))
            For Each ws In .Worksheets
                If ws.ListObjects.Count > 0 Then GoTo SKIP
            Next
            ListboxWorkbook.RemoveItem i
        End With
SKIP:
    Next
End Sub

Sub LoadOptions()
    'at this moment only if user checked Show Calendar Checkbox
    Dim boo: boo = IniReadKey(ThisWorkbook.Path & "\config.ini", Me.Name, "ShowCalendar")
    If boo <> vbNullString Then CheckBoxDatePicker.Value = boo
    ComboBoxFilterOperator.List = Array("like", "contains", "starts", "ends", "=", "<>", ">", ">=", "<", "<=")
    ComboBoxFilterOperator.ListIndex = 0
End Sub

Private Sub CheckBoxDatePicker_Click()
    'Save checked status
    IniWrite ThisWorkbook.Path & "\config.ini", Me.Name, "ShowCalendar", CheckBoxDatePicker.Value
End Sub

Sub setControlStyle()
    FormatListview
    FormatFrames
    SetImages
    FormatLabels
End Sub

Sub FormatListview()
    With ListViewTable
        .Gridlines = True
        .FullRowSelect = True
        .HideSelection = False
        .Font.Name = "Segoe UI"
    End With
End Sub

Sub FormatFrames()
    FrameTablePath.BorderStyle = fmBorderStyleNone: FrameTablePath.Caption = ""
    With FrameTableView
        .BorderStyle = fmBorderStyleNone
        .Caption = ""
        .Left = FrameTablePath.Left + FrameTablePath.Width
    End With
    With FrameTableEditor
        .BorderStyle = fmBorderStyleNone: FrameTableEditor.Caption = ""
        .ScrollBars = fmScrollBarsVertical
        .ScrollHeight = .InsideHeight * 8.5
        .ScrollWidth = .InsideWidth * 9
        .Left = FrameTableView.Left + FrameTableView.Width
    End With
    With FrameTableEditorBottom
        .BorderStyle = fmBorderStyleNone
        .Caption = ""
        .Left = FrameTableEditor.Left
    End With
End Sub

Sub SetImages()
    LabelResize1.Picture = LoadPicture(ThisWorkbook.Path & "\img\left.jpg"): LabelResize1.Tag = "Left"
    LabelResize2.Picture = LoadPicture(ThisWorkbook.Path & "\img\left.jpg"): LabelResize2.Tag = "Left"
    LabelResize3.Picture = LoadPicture(ThisWorkbook.Path & "\img\left.jpg"): LabelResize3.Tag = "Left"
    LabelNew.Picture = LoadPicture(ThisWorkbook.Path & "\img\add.jpg")
    LabelSave.Picture = LoadPicture(ThisWorkbook.Path & "\img\save.jpg")
    LabelRefresh.Picture = LoadPicture(ThisWorkbook.Path & "\img\refresh.jpg")
    LabelDelete.Picture = LoadPicture(ThisWorkbook.Path & "\img\delete.jpg")
    LabelPrint.Picture = LoadPicture(ThisWorkbook.Path & "\img\printer.jpg")
    LabelAutofit.Picture = LoadPicture(ThisWorkbook.Path & "\img\autofit.jpg")
    LabelGoFirst.Picture = LoadPicture(ThisWorkbook.Path & "\img\top.jpg")
    LabelGoLast.Picture = LoadPicture(ThisWorkbook.Path & "\img\bottom.jpg")
    CheckBoxDatePicker.Picture = LoadPicture(ThisWorkbook.Path & "\img\calendar.jpg")
    CheckBoxDatePicker.PicturePosition = fmPicturePositionLeftCenter
End Sub

Sub FormatLabels()
    Dim Ctrl As MSForms.control
    For Each Ctrl In Me.Controls
        Select Case TypeName(Ctrl)
        Case "Label"
            With Ctrl
                .MouseIcon = LoadPicture(ThisWorkbook.Path & "\img\Hand Cursor Pointer.ico")
                .MousePointer = fmMousePointerCustom
                .SpecialEffect = fmSpecialEffectRaised
                .Width = 28
                .Height = 28
            End With
        'not labels but let's put it here
        Case "ComboBox", "TextBox", "ListBox"
            Ctrl.BorderStyle = fmBorderStyleSingle
        End Select
    Next
End Sub

Sub SelectActivePath()
    'if active cell inside table OR only 1 table in active sheet then
    'choose active workbook > active sheet > that table
    'and create the editors for the first row
    
    Dim lo As ListObject
    On Error Resume Next
    Set lo = ActiveCell.ListObject
    On Error GoTo 0
    If lo Is Nothing And ActiveSheet.ListObjects.Count = 1 Then Set lo = ActiveSheet.ListObjects(1)
    If lo Is Nothing Then Exit Sub
    
    Set TargetTable = lo
    Dim i As Long
    If ListboxWorkbook.ListCount > 0 Then
        For i = 0 To ListboxWorkbook.ListCount - 1
            If ListboxWorkbook.List(i) = ActiveWorkbook.Name Then
                ListboxWorkbook.Selected(i) = True
                Exit For
            End If
        Next
    End If
    If ListboxWorksheet.ListCount > 0 Then
        For i = 0 To ListboxWorksheet.ListCount - 1
            If ListboxWorksheet.List(i) = ActiveSheet.Name Then
                ListboxWorksheet.Selected(i) = True
                Exit For
            End If
        Next
    End If
    If ListboxTable.ListCount > 0 Then
        For i = 0 To ListboxTable.ListCount - 1
            If ListboxTable.List(i) = lo.Name Then
                ListboxTable.Selected(i) = True
                Exit For
            End If
        Next
    End If
End Sub


'/////////////////////////////////////////////
'///     BOOK - SHEET - TABLE CHANGE       ///
'/////////////////////////////////////////////

Private Sub ListboxWorkbook_change()
    'LIST workbook's SHEETS that have tables
    If ListboxWorkbook.ListIndex = -1 Then Exit Sub
    Dim ListboxValue As String
    ListboxValue = ListboxWorkbook.List(ListboxWorkbook.ListIndex)

    Set TargetWorkbook = Workbooks(ListboxValue)
    ListboxWorksheet.Clear
    ListboxTable.Clear
    Set TargetTable = Nothing
    aLV.Clear
    Dim ws As Worksheet
    For Each ws In TargetWorkbook.Worksheets
        If ws.ListObjects.Count > 0 Then
            ListboxWorksheet.AddItem ws.Name
        End If
    Next
    If ListboxWorksheet.ListCount = 1 Then ListboxWorksheet.ListIndex = 0
End Sub

Private Sub ListboxWorksheet_change()
    'LIST worksheet's TABLES
    If ListboxWorksheet.ListIndex = -1 Then Exit Sub
    Dim ListboxValue As String
    ListboxValue = ListboxWorksheet.List(ListboxWorksheet.ListIndex)
    Set TargetWorksheet = TargetWorkbook.Worksheets(ListboxValue)
    ListboxTable.Clear
    Set TargetTable = Nothing
    Dim lo As ListObject
    For Each lo In TargetWorksheet.ListObjects
        ListboxTable.AddItem lo.Name
    Next
    aLV.Clear
    RemoveEditorControls
    'if there's only one table, load it
    If ListboxTable.ListCount = 1 Then ListboxTable.ListIndex = 0
End Sub

Sub RemoveEditorControls()
    Set Emitter = Nothing
    Set Emitter = New EventListenerEmitter
    FrameTableEditor.Clear
End Sub

Private Sub ListboxTable_change()
    'LOAD TABLE
    If ListboxTable.ListIndex = -1 Then Exit Sub
    Set TargetTable = TargetWorksheet.ListObjects(ListboxTable.List(ListboxTable.ListIndex))
    aLV.InitializeFromArray TargetTable.Range.Value
    SetListviewNumberFormat
    ResetFilters
    ListViewTable.SetFocus
    CreateEditorControls
End Sub

Private Sub SetListviewNumberFormat()
    'otherwise the value will be loaded as a weird number
    'this may be slow on large tables, @TODO confirm and modify aListView.InitializeFromArray if needed
    Dim x As ListItem, y As ListSubItem, val
    Dim cell As Range
    For Each x In ListViewTable.ListItems
        Set cell = TargetTable.DataBodyRange(x.index, 1)
        val = cell.Value
        If IsDate(val) Or IsTime(val) Then
            applyFormat val, cell
            x.Text = val
        End If
        For Each y In x.ListSubItems
            Set cell = cell.Offset(0, 1)
            val = cell.Value
            If Not IsEmpty(cell) Then
                If IsDate(val) Or IsTime(val) Then
                    applyFormat val, TargetTable.DataBodyRange(x.index, y.index + 1)
                    y.Text = val
                End If
            End If
        Next
    Next
End Sub

Sub ResetFilters()
    ComboBoxFilterColumn.Clear
    ComboBoxFilterColumn.AddItem "-1"
    Dim i As Long
    For i = 1 To ListViewTable.ColumnHeaders.Count
        ComboBoxFilterColumn.AddItem CStr(i)
    Next
    If ComboBoxFilterColumn.ListCount > 0 Then ComboBoxFilterColumn.ListIndex = 0
    If ComboBoxFilterOperator.ListCount > 0 Then ComboBoxFilterOperator.ListIndex = 0
End Sub

'* Modified   : Date and Time       Author              Description
'* Updated    : 10-10-2023 21:07    Alex                (uTableManager.frm > CreateEditorControls) disabled editors if cell value calculated by formula

Private Sub CreateEditorControls()
'@LastModified 2310102107
    RemoveEditorControls
    Dim i As Long, lbl As MSForms.Label, txt As MSForms.Textbox, cbx As MSForms.ComboBox
    Dim cell As Range
    Dim validationArray
    
    '@bug fixed
    Dim controlWidth As Long
    controlWidth = WorksheetFunction.Max(354, FrameTableEditor.Width) - 32
    For i = 1 To ListViewTable.ColumnHeaders.Count
        On Error Resume Next
        If isAddingNewRow Then
            Set cell = TargetTable.DataBodyRange(1, i)
        Else
            Set cell = TargetTable.DataBodyRange(ListViewTable.selectedItem.index, i)
        End If
        On Error GoTo 0
        If cell Is Nothing Then Exit Sub
        Set lbl = FrameTableEditor.Controls.Add("Forms.Label.1")
        lbl.Width = controlWidth
        lbl.Height = 10
        lbl.Left = 6
        lbl.Top = AvailableFormOrFrameRow(FrameTableEditor, , , 3)
        lbl.Caption = i & "/" & ListViewTable.ColumnHeaders.Count & " - " & ListViewTable.ColumnHeaders(i).Text
        lbl.Font.Size = 6
        lbl.Font.Bold = True
        lbl.Name = "lbl-" & i
        Dim dvType As Integer
        On Error Resume Next
        dvType = cell.Validation.Type
        On Error GoTo 0
        'create a combobox if it has datavalidation list, for example = Cat, Dog, Horse
        If isValidationList(cell) Then
            Set cbx = FrameTableEditor.Controls.Add("Forms.ComboBox.1")
            cbx.columnCount = 1
            If InStr(1, cell.Validation.Formula1, "=") > 0 Then
                Dim rng As Range
                Set rng = Nothing
                On Error Resume Next
                Set rng = TargetWorksheet.Range(Replace(cell.Validation.Formula1, "=", ""))
                On Error GoTo 0
                If Not rng Is Nothing Then
                    validationArray = rng.Value
                End If
            Else
                validationArray = Split(cell.Validation.Formula1, ",")
            End If
            cbx.List = validationArray
            cbx.Top = lbl.Top + lbl.Height
            cbx.Left = lbl.Left
            cbx.Width = controlWidth
            cbx.Height = 16
            cbx.Name = "Editor-" & i
            cbx.Font.Name = "Segoe UI"
            cbx.BorderStyle = fmBorderStyleSingle
'            cbx.Style=fmStyleDropDownList
            ShowDatavalidationBanner cbx, lbl
        Else 'create a textbox
            Set txt = FrameTableEditor.Controls.Add("Forms.Textbox.1")
            txt.Top = lbl.Top + lbl.Height
            txt.Left = lbl.Left
            txt.Width = controlWidth
            txt.Height = 16
            txt.Name = "Editor-" & i
            txt.Font.Name = "Segoe UI"
            txt.EnterKeyBehavior = True
            txt.MultiLine = True
            txt.WordWrap = False
            txt.BorderStyle = fmBorderStyleSingle
        End If
        
        '@MODIFIED
        If cell.HasFormula Then
            Me.Controls("Editor-" & i).Enabled = False
            lbl.Caption = lbl.Caption & " => " & cell.Formula
        End If
    Next
    
    If isAddingNewRow Then
        'if adding new row then editors would be empty, do nothing
    Else 'load the values properly formated
        Dim val As Variant
        For i = 1 To ListViewTable.ColumnHeaders.Count
            Set cell = TargetTable.DataBodyRange(ListViewTable.selectedItem.index, i)
            val = cell.Value
            If Not IsEmpty(cell) Then
                If IsDate(val) Or IsTime(val) Then
                    applyFormat val, cell
                End If
            End If
            Me.Controls("Editor-" & i).Value = val
        Next
    End If
    'add event handling to the dynamically created Editor Controls
    Emitter.AddEventListenerAll FrameTableEditor
    FrameTableEditor.ScrollTop = 0
    SetScrollHeight
End Sub

Function isValidationList(cell As Range) As Boolean
    Dim dvType As Integer
    On Error Resume Next
    dvType = cell.Validation.Type
    On Error GoTo 0
    If dvType = 3 Then isValidationList = True
End Function

Sub SetScrollHeight()
    Dim ctr As MSForms.control
    Set ctr = FrameTableEditor.Controls("Editor-" & FrameTableEditor.Controls.Count / 2)
    'making it able to go a bit further down below last control
    'because if it is a multiline textbox it will be able to expand its height (see Emitter_Focus)
    FrameTableEditor.ScrollHeight = ctr.Top + ctr.Height + 100
End Sub


'/////////////////////////////////////////////
'///            TABLE EVENTS               ///
'/////////////////////////////////////////////

Private Sub ListViewTable_BeforeLabelEdit(Cancel As Integer)
    Cancel = True
End Sub

Private Sub ListViewTable_DblClick()
    'we actually need to info from the _MouseUp parameters which are not available in _DblClick
    'so we put our DblClick code in the _MouseUp event, but allow it to run only if we Double Clicked
    isDoubleClick = True
End Sub

Private Sub ListViewTable_MouseUp(ByVal Button As Integer, ByVal Shift As Integer, _
                                  ByVal x As stdole.OLE_XPOS_PIXELS, ByVal y As stdole.OLE_YPOS_PIXELS)
    'we clicked a table row, so recreate the editors
    isAddingNewRow = False
    CreateEditorControls
    If Not isDoubleClick Then Exit Sub
    'detect in which column the cell we clicked belongs
    Dim targetColumn As Long:   targetColumn = aLV.ClickedColumn(x, y)
    Dim targetRow As Long:      targetRow = ListViewTable.selectedItem.index
    'set focus to the corresponding Editor
    Dim Editor As MSForms.control
    Set Editor = FrameTableEditor.Controls("Editor-" & IIf(targetColumn = -1, 1, targetColumn + 1))
    Editor.SetFocus
    Editor.SelStart = 0
    Emitter_Focus Editor
    isDoubleClick = False
End Sub

Private Sub ListViewTable_KeyUp(KeyCode As Integer, ByVal Shift As Integer)
    'if we're moving with up/down keys so changed row, recreate the editors
    isAddingNewRow = False
    Dim selectedItem As MSComctlLib.ListItem
    On Error Resume Next
    Set selectedItem = ListViewTable.selectedItem
    On Error GoTo 0
    If selectedItem Is Nothing Then Exit Sub
    Dim newSelection As String
    newSelection = ListboxWorkbook.List(ListboxWorkbook.ListIndex) & _
                    ListboxWorksheet.List(ListboxWorksheet.ListIndex) & _
                    ListboxTable.List(ListboxTable.ListIndex) & _
                    ListViewTable.selectedItem.index
    If Not newSelection = currentSelection Then
        currentSelection = newSelection
        CreateEditorControls
    End If
End Sub

Private Sub LabelAutoFit_Click()
    aLV.AutofitColumns
End Sub

Private Sub LabelRefresh_Click()
    'refresh if for example we have the form loaded and created a new table or opened a new workbook
    aLV.Clear
    RemoveEditorControls
    ListboxTable.Clear
    Set TargetTable = Nothing
    ListboxWorksheet.Clear
    aListBox.Init(ListboxWorkbook).LoadVBProjects
    SelectActivePath
End Sub

Private Sub LabelPrint_Click()
    TargetTable.Range.PrintOut , , , True
End Sub

Private Sub TextBoxFilterValue_Change()
    '@TODO consider actually filtering the table on the worksheet and loading its filtered data
    '      but the current method allows filtering if value matches any cell in the row
    Dim op As operator
    op = Choose(ComboBoxFilterOperator.ListIndex + 1, _
                operator.IS_LIKE, _
                operator.IS_EQUAL, _
                operator.NOT_EQUAL, _
                operator.IS_CONTAINS, _
                operator.NOT_CONTAINS, _
                operator.STARTS_WITH, _
                operator.ENDS_WITH, _
                operator.GREATER_THAN, _
                operator.GREATER_OR_EQUAL, _
                operator.LESS_THAN, _
                operator.LESS_OR_EQUAL)
    
    Select Case Len(TextBoxFilterValue.Value)
    Case Is = 0 'show whole range.value
        aLV.InitializeFromArray TargetTable.Range.Value
    Case Is = 1 'filter the whole array
        previousInputLength = 1
        aLV.InitializeFromArray FilterArray2d(TargetTable.Range.Value, True, _
                                              TextBoxFilterValue.Value, _
                                              operator.IS_LIKE, _
                                              ComboBoxFilterColumn.Value)
    Case Is > 1
        If Len(TextBoxFilterValue.Value) > previousInputLength Then 'filter the current table's array for better speed
            aLV.InitializeFromArray FilterArray2d(aLV.Value, True, _
                                                  TextBoxFilterValue.Value, _
                                                  operator.IS_LIKE, _
                                                  ComboBoxFilterColumn.Value)
        Else
            previousInputLength = Len(TextBoxFilterValue.Value)     'filter the original table array
            aLV.InitializeFromArray FilterArray2d(TargetTable.Range.Value, True, _
                                                  TextBoxFilterValue.Value, _
                                                  operator.IS_LIKE, _
                                                  ComboBoxFilterColumn.Value)
        End If
    End Select
    CreateEditorControls
End Sub

'/////////////////////////////////////////////
'///            EDITORS EVENTS             ///
'/////////////////////////////////////////////

'* Modified   : Date and Time       Author              Description
'* Updated    : 10-10-2023 21:08    Alex                (uTableManager.frm > Emitter_Focus) when focused on combobox open listcombo userform

Private Sub Emitter_Focus(control As Object)
'@LastModified 2310102108
    If Not control.Enabled Then Exit Sub
    If Not control.Name Like "Editor-*" Then Exit Sub
    editorIndex = Split(control.Name, "-")(1)
    'format editor's label to indicate focus
    Dim Label As MSForms.Label
    Set Label = Me.Controls("lbl-" & editorIndex)
    Label.BackColor = RGB(80, 200, 120)
    'if multiline textbox then make it taller
    ResizeIfNeeded control
    'if we focused by tabbing, remove the tab (we're using _KeyDown event, read that)
    If TypeName(control) = "TextBox" Then control.Value = Replace(control.Value, vbTab, "")
    
    '@MODIFIED
    If TypeName(control) = "ComboBox" Then
        control.Text = Replace(control.Text, vbTab, "")
        Load UserForm1
        UserForm1.LoadedList = control.List
        UserForm1.ListBox1.List = UserForm1.LoadedList
        Set UserForm1.TargetControl = control
        UserForm1.Show False
    End If
    
    'if it is a date field then optionally show date picker
    If CheckBoxDatePicker.Value = True And UCase(Label.Caption) Like "*DATE*" Then
        canceledDatePicker = False
        Dim retVal As String: retVal = uCalendar.Datepicker
        If retVal <> "" And canceledDatePicker = False Then control.Value = Format(Replace(retVal, ".", "/"), TableCell(control).NumberFormat)
    End If
    'display datavalidation information for respective cell
    ShowDatavalidationBanner control, Label
End Sub

Sub ResizeIfNeeded(control As Object)
    If Not TypeName(control) = "TextBox" Then Exit Sub
    If Not control.Enabled Then Exit Sub
    control.ZOrder (fmTop)
    Dim dif As Long
    dif = CountOfCharacters(control.Text, vbNewLine) + 1
    Dim targetHeight
    targetHeight = WorksheetFunction.Min(FrameTableEditor.Height - control.Top - 12, 12 * dif)
    targetHeight = WorksheetFunction.Max(targetHeight, 18)
    If control.Height = targetHeight Then Exit Sub
    control.Height = targetHeight
    control.ScrollBars = fmScrollBarsBoth
    'change backcolor to make it more distinguishable as it is over other controls
    control.BackColor = RGB(255, 255, 204)
End Sub

Function TableCell(control As Object) As Range
    Set TableCell = TargetTable.DataBodyRange(ListViewTable.selectedItem.index, CInt(Split(control.Name, "-")(1)))
End Function

Sub ShowDatavalidationBanner(control As Object, Banner As Object)
    On Error Resume Next
    Dim cell As Range
    Set cell = TableCell(control)
    Dim dataValidation As Validation:   Set dataValidation = cell.Validation
    Dim ValidationType As XlDVType:     ValidationType = dataValidation.Type
    On Error GoTo 0
    If ValidationType = 0 Then Exit Sub
    Dim validationFormula1  As String:                      validationFormula1 = dataValidation.Formula1
    Dim validationFormula2  As String:                      validationFormula2 = dataValidation.Formula2
    Dim operator            As XlFormatConditionOperator:   operator = dataValidation.operator
    Dim msg As String
    If DatavalidationTypeToString(ValidationType) = "List" Then
        If InStr(1, validationFormula1, "$") > 0 Then
            Dim rng As Range
            Set rng = cell.Parent.Range(Mid(validationFormula1, 2))
            For Each cell In rng
                msg = msg & IIf(msg <> "", ", ", "") & cell.Value
            Next
        Else
            msg = validationFormula1
        End If
    Else
        Select Case dataValidation.operator
            Case xlBetween
                msg = validationFormula1 & " < VALUE < " & validationFormula2
            Case xlNotBetween
                msg = validationFormula1 & " > VALUE < " & validationFormula2
            Case xlEqual, xlNotEqual, xlGreater, xlLess, xlGreaterEqual, xlLessEqual
                msg = "VALUE " & OperatorToString(operator) & " " & validationFormula1
         End Select
     End If
     msg = "(" & msg & ")"
     Set targetBanner = Banner
     If InStr(1, Banner.Caption, "[List]") > 0 Then Exit Sub
     Banner.Caption = Banner.Caption & Space(4) & "-" & Space(4) & _
                      "[" & DatavalidationTypeToString(ValidationType) & "]" & Space(4) & "-" & Space(4) & msg
     Banner.Tag = Banner.Caption
End Sub

Public Function OperatorToString(operator As XlFormatConditionOperator) As String
    OperatorToString = CStr(aSwitch(operator, _
                            xlBetween, "<<", _
                            xlNotBetween, "><", _
                            xlEqual, "=", _
                            xlNotEqual, "<>", _
                            xlGreater, ">", _
                            xlLess, "<", _
                            xlGreaterEqual, ">=", _
                            xlLessEqual, "<="))
End Function

Public Function DatavalidationTypeToString(dvType As XlDVType)
    Select Case dvType
    Case xlValidateInputOnly:   DatavalidationTypeToString = "Value Change"
    Case xlValidateWholeNumber: DatavalidationTypeToString = "Whole Number"
    Case xlValidateDecimal:     DatavalidationTypeToString = "Decimal"
    Case xlValidateList:        DatavalidationTypeToString = "List"
    Case xlValidateDate:        DatavalidationTypeToString = "Date"
    Case xlValidateTime:        DatavalidationTypeToString = "Time"
    Case xlValidateCustom:      DatavalidationTypeToString = "Custom"
    Case xlValidateTextLength:  DatavalidationTypeToString = "Text Length"
    End Select
End Function

Private Sub Emitter_Blur(control As Object)
    If Not control.Name Like "Editor-*" Then Exit Sub
    If Not control.Enabled Then Exit Sub
    
    'restore original view for the Editor and its Label
    Me.Controls("lbl-" & Split(control.Name, "-")(1)).BackColor = Me.BackColor
    If Not TypeName(control) = "TextBox" Then Exit Sub
'    If InStr(1, control.Text, vbNewLine) > 0 Then
    If control.Enabled Then
        control.Height = 16
        control.ScrollBars = fmScrollBarsNone
        control.BackColor = vbWhite
    End If
    If Not targetBanner Is Nothing Then targetBanner.Caption = targetBanner.Tag
End Sub

'* Modified   : Date and Time       Author              Description
'* Updated    : 10-10-2023 21:09    Alex                (uTableManager.frm > Emitter_Keydown) skip editor if disabled

Private Sub Emitter_Keydown(control As Object, KeyCode As MSForms.ReturnInteger, Shift As Integer)
'@LastModified 2310102109
    If Not control.Name Like "Editor-*" Then Exit Sub
    ' we're using _KeyDown event to
    ' 1. capture the use tab or shift + tab and allow cycling from last to first or first to last ...
    editorIndex = -1
    If KeyCode = vbKeyTab Then
        editorIndex = Split(control.Name, "-")(1)
        If Shift = 1 Then
            editorIndex = IIf(editorIndex = 1, FrameTableEditor.Controls.Count / 2, editorIndex - 1)
        Else
            editorIndex = IIf(editorIndex = FrameTableEditor.Controls.Count / 2, 1, editorIndex + 1)
        End If
    Else
        Exit Sub
    End If
    If editorIndex = -1 Then Exit Sub
    ' 2. restore the view of the editor we're moving out from and it's label
    Emitter_Blur control
    ' ... continuing (1)
    Dim Editor As MSForms.control
    Set Editor = FrameTableEditor.Controls("Editor-" & editorIndex)
    ' if going backwards then scroll if needed to have the correct view
    If editorIndex < previousEditorIndex _
    Or (previousEditorIndex = 1 And editorIndex = FrameTableEditor.Controls.Count / 2) Then
        FrameTableEditor.ScrollTop = IIf(editorIndex = 1, 0, Controls("lbl-" & editorIndex).Top)
    End If
    
    '@MODIFIED
    If Editor.Visible And Editor.Enabled Then Editor.SetFocus
    previousEditorIndex = editorIndex
End Sub

Private Sub Emitter_Change(control As Object)
    ' format for data validation pass/fail
    If InStr(1, control.Value, vbTab) > 0 Then Exit Sub
    If Not control.Enabled Then Exit Sub
    ResizeIfNeeded control
    Dim cell As Range: Set cell = TableCell(control)
    If IsValueValidForCell(cell, control.Value) Then
        control.ForeColor = RGB(0, 100, 0)
        control.BorderColor = RGB(0, 100, 0)
    Else
        control.ForeColor = RGB(178, 34, 34)
        control.BorderColor = RGB(178, 34, 34)
    End If
End Sub

Public Function IsValueValidForCell(ByVal inputRange As Range, ByVal inputValue As Variant) As Boolean
    On Error Resume Next
    Dim dataValidation As Validation:   Set dataValidation = inputRange.Validation
    Dim ValidationType As XlDVType:     ValidationType = dataValidation.Type
    On Error GoTo 0
    If ValidationType = 0 Then
        IsValueValidForCell = True
    Else
        ' Check if the cell has data validation
        Dim validationFormula1 As String, validationFormula2 As String
        validationFormula1 = dataValidation.Formula1
        validationFormula2 = dataValidation.Formula2
        Dim operator As XlFormatConditionOperator:  operator = dataValidation.operator
        
        If inputValue = "" And Not dataValidation.IgnoreBlank Then
            IsValueValidForCell = False
        ElseIf ValidationType = xlValidateInputOnly Then
            ' Allow input only or custom validation types
            IsValueValidForCell = True
        ElseIf ValidationType = xlValidateCustom Then
            '@TODO consider whate this custom may be and try to evaluate it
            IsValueValidForCell = True
        Else
            ' Evaluate the validation formula with the input value
            Dim validationResult As Boolean
            Select Case ValidationType
                Case xlValidateWholeNumber
                    If IsNumeric(inputValue) Then
                        If CLng(inputValue) - Round(inputValue, 0) = 0 Then
                            validationResult = EvaluateComparison(CInt(inputValue), operator, _
                                                                  CInt(validationFormula1), _
                                                                  CInt(validationFormula2))
                        End If
                    End If
                Case xlValidateDecimal
                    If IsNumeric(inputValue) Then
                        validationResult = EvaluateComparison(CLng(inputValue), operator, _
                                                              CLng(validationFormula1), _
                                                              CLng(validationFormula2))
                    End If
                Case xlValidateTextLength
                    If validationFormula2 = "" Then
                        validationResult = EvaluateComparison(CInt(Len(inputValue)), operator, _
                                                              CInt(validationFormula1), _
                                                              0)
                    Else
                        validationResult = EvaluateComparison(CInt(Len(inputValue)), operator, _
                                                              CInt(validationFormula1), _
                                                              CInt(validationFormula2))
                    End If
                Case xlValidateDate
                    If IsDate(inputValue) Then
                        If validationFormula2 = "" Then
                            validationResult = EvaluateComparison(CDate(inputValue), operator, _
                                                                  CDate(validationFormula1), _
                                                                  "")
                        Else
                            validationResult = EvaluateComparison(CDate(inputValue), operator, _
                                                                  CDate(validationFormula1), _
                                                                  CDate(validationFormula2))
                        End If
                    End If
                Case xlValidateTime
                    If IsDate(inputValue) Then
                        If IsTime(CDate(inputValue)) Then
                            If validationFormula2 = "" Then
                                validationResult = EvaluateComparison(CDate(inputValue), operator, _
                                                                      CDate(validationFormula1), _
                                                                      "")
                            Else
                                validationResult = EvaluateComparison(CDate(inputValue), operator, _
                                                                      CDate(validationFormula1), _
                                                                      CDate(validationFormula2))
                            End If
                        End If
                    End If
                Case xlValidateList
                    Dim i As Long
                    Dim listValues As Variant
                    If InStr(1, validationFormula1, "$") > 0 Then
                        Dim rng As Range
                        Set rng = inputRange.Parent.Range(Mid(validationFormula1, 2))
                        Dim cell As Range
                        ReDim listValues(1 To rng.Cells.Count)
                        For i = 1 To rng.Cells.Count
                            listValues(i) = rng.Cells(i)
                        Next
                    Else
                        listValues = Split(Replace(validationFormula1, "=", ""), ",")
                    End If
                    
                    For i = LBound(listValues) To UBound(listValues)
                        listValues(i) = Trim(listValues(i))
                    Next i

                    validationResult = Not IsError(Application.match(inputValue, listValues, 0))
                Case xlValidateCustom
                    validationResult = True
                Case Else
                    validationResult = Application.Evaluate(inputValue & validationFormula1)
            End Select
            IsValueValidForCell = CBool(validationResult)
        End If
    End If
End Function

Public Function EvaluateComparison(ByVal inputValue As Variant, ByVal operator As XlFormatConditionOperator, _
                                   ByVal validationFormula1 As Variant, ByVal validationFormula2 As Variant) As Boolean
    If TypeName(inputValue) <> TypeName(validationFormula1) Then Exit Function
    Select Case operator
        Case xlBetween
            If TypeName(inputValue) <> TypeName(validationFormula2) Then Exit Function
            EvaluateComparison = (validationFormula1 <= inputValue And inputValue <= validationFormula2)
        Case xlNotBetween
            If TypeName(inputValue) <> TypeName(validationFormula2) Then Exit Function
            EvaluateComparison = (validationFormula1 > inputValue And inputValue < validationFormula2)
        Case xlEqual
            EvaluateComparison = (inputValue = validationFormula1)
        Case xlNotEqual
            EvaluateComparison = (inputValue <> validationFormula1)
        Case xlGreater
            EvaluateComparison = (inputValue > validationFormula1)
        Case xlLess
            EvaluateComparison = (inputValue < validationFormula1)
        Case xlGreaterEqual
            EvaluateComparison = (inputValue >= validationFormula1)
        Case xlLessEqual
            EvaluateComparison = (inputValue <= validationFormula1)
    End Select
End Function


'/////////////////////////////////////////////
'///            EDITORS COMMANDS           ///
'/////////////////////////////////////////////

Private Sub LabelSave_Click()
    ' check for data validation
    If Not PassValidation Then
        MsgBox "Failed to pass Data Validation"
        Exit Sub
    End If
    ' save depending on if we are new row or editing existing
    If isAddingNewRow Then
        SaveNewRow
    Else
        SaveChanges
    End If
End Sub

'* Modified   : Date and Time       Author              Description
'* Updated    : 10-10-2023 21:12    Alex                (uTableManager.frm > SaveChanges) skip disabled editor, load value from cell calculated by formula

Private Sub SaveChanges()
'@LastModified 2310102112
    Application.ScreenUpdating = False
    Dim targetRow As Long: targetRow = ListViewTable.selectedItem.index
    Dim i As Long
    For i = 1 To TargetTable.ListColumns.Count
        If FrameTableEditor.Controls("Editor-" & i).Enabled Then
            TargetTable.DataBodyRange(ListViewTable.selectedItem.index, i).Value = FrameTableEditor.Controls("Editor-" & i).Value
        End If
    Next
    Dim EditorCount As Long: EditorCount = FrameTableEditor.Controls.Count / 2
    Dim out
    ReDim out(1 To EditorCount)
    Dim val
    For i = 1 To EditorCount
        val = TargetTable.DataBodyRange(targetRow, i).Value
        FrameTableEditor.Controls("Editor-" & i).Value = val
        out(i) = val
    Next
    UpdateListviewRow targetRow, out
    Application.ScreenUpdating = True
End Sub

'* Modified   : Date and Time       Author              Description
'* Updated    : 10-10-2023 21:13    Alex                (uTableManager.frm > SaveNewRow) skip disabled editor, load value from cell calculated by formula

Private Sub SaveNewRow()
'@LastModified 2310102113
    Application.ScreenUpdating = False
    TargetTable.ListRows.Add
    Dim EditorCount As Long: EditorCount = FrameTableEditor.Controls.Count / 2
    Dim targetRow As Long: targetRow = ListViewTable.selectedItem.index
    Dim out()
    ReDim out(1 To 1, 1 To EditorCount)
    Dim i As Long
    For i = 1 To EditorCount
        If FrameTableEditor.Controls("Editor-" & i).Enabled Then
            TargetTable.DataBodyRange(targetRow, i).Value = FrameTableEditor.Controls("Editor-" & i).Value
        End If
    Next
    For i = 1 To EditorCount
        out(1, i) = TargetTable.DataBodyRange(targetRow, i).Value
    Next
    aLV.AppendArray out
    Application.ScreenUpdating = True
End Sub

Sub UpdateListviewRow(index As Long, newValues)
    Dim i As Long
    For i = 1 To ListViewTable.ColumnHeaders.Count
        If i = 1 Then
            ListViewTable.ListItems(index).Text = newValues(i)
        Else
            ListViewTable.ListItems(index).ListSubItems(i - 1).Text = newValues(i)
        End If
    Next
End Sub

Function PassValidation() As Boolean
    PassValidation = True
    Dim val
    Dim cell As Range
    Dim i As Long
    For i = 1 To FrameTableEditor.Controls.Count / 2
        Set cell = TargetTable.DataBodyRange(ListViewTable.selectedItem.index, i)
        val = Controls("Editor-" & i).Value
        If Not IsValueValidForCell(cell, val) Then
            PassValidation = False
            Exit Function
        End If
    Next
End Function

Private Sub LabelNew_Click()
    isAddingNewRow = True
    CreateEditorControls
End Sub

Private Sub LabelDelete_Click()
    'delete selected row
    If isAddingNewRow Then Exit Sub
    If MsgBox("Permanently delete this row?", vbYesNo + vbExclamation) = vbNo Then Exit Sub
    TargetTable.ListRows(ListViewTable.selectedItem.index).Delete
    ListViewTable.ListItems.Remove ListViewTable.selectedItem.index
End Sub

Private Sub LabelGoFirst_Click()
    FrameTableEditor.ScrollTop = 0
    FrameTableEditor.Controls("Editor-1").SetFocus
    If editorIndex > 0 Then Emitter_Blur Controls("Editor-" & editorIndex)
    Emitter_Focus Controls("Editor-1")
End Sub

Private Sub LabelGoLast_Click()
    FrameTableEditor.ScrollTop = Controls("lbl-" & (FrameTableEditor.Controls.Count / 2)).Top
    FrameTableEditor.Controls("Editor-" & (FrameTableEditor.Controls.Count / 2)).SetFocus
    If editorIndex > 0 Then Emitter_Blur Controls("Editor-" & editorIndex)
    Emitter_Focus Controls("Editor-" & (FrameTableEditor.Controls.Count / 2))
End Sub


'/////////////////////////////////////////////
'///            RESIZING                   ///
'/////////////////////////////////////////////

' @TODO consider resizing by dragging

Private Sub LabelResize1_Click()
    Select Case LabelResize1.Tag
    Dim i As Long
    Case "Left"
        LabelResize1.Picture = LoadPicture(ThisWorkbook.Path & "\img\right.jpg")
        LabelResize1.Tag = "Right"
        FrameTablePath.Width = LabelResize1.Width + 6
    Case "Right"
        LabelResize1.Picture = LoadPicture(ThisWorkbook.Path & "\img\left.jpg")
        LabelResize1.Tag = "Left"
        FrameTablePath.Width = ListboxWorkbook.Left + ListboxWorkbook.Width + 6
    End Select
    FrameTableView.Left = FrameTablePath.Left + FrameTablePath.Width
    FrameTableEditor.Left = FrameTableView.Left + FrameTableView.Width
    FrameTableEditorBottom.Left = FrameTableEditor.Left
    
    aUserform.Init(Me).ResizeToFitControls (6)
End Sub

Private Sub LabelResize2_Click()
    Select Case LabelResize2.Tag
    Dim i As Long
    Case "Left"
        LabelResize2.Picture = LoadPicture(ThisWorkbook.Path & "\img\right.jpg")
        LabelResize2.Tag = "Right"
        FrameTableView.Width = LabelResize2.Width + 6
    Case "Right"
        LabelResize2.Picture = LoadPicture(ThisWorkbook.Path & "\img\left.jpg")
        LabelResize2.Tag = "Left"
        FrameTableView.Width = 540
    End Select
    FrameTableEditor.Left = FrameTableView.Left + FrameTableView.Width
    FrameTableEditorBottom.Left = FrameTableEditor.Left
    
    aUserform.Init(Me).ResizeToFitControls (6)
End Sub

Private Sub LabelResize3_Click()
    Select Case LabelResize3.Tag
    Dim i As Long
    Case "Left"
        LabelResize3.Picture = LoadPicture(ThisWorkbook.Path & "\img\right.jpg")
        LabelResize3.Tag = "Right"
        FrameTableEditor.Width = LabelResize3.Width + 6
    Case "Right"
        LabelResize3.Picture = LoadPicture(ThisWorkbook.Path & "\img\left.jpg")
        LabelResize3.Tag = "Left"
        FrameTableEditor.Width = 354
    End Select
    FrameTableEditorBottom.Width = FrameTableEditor.Width
    aUserform.Init(Me).ResizeToFitControls (6)
End Sub


'/////////////////////////////////////////////
'///            OTHER                      ///
'/////////////////////////////////////////////

Private Sub GetInfo_Click()
    uAuthor.Show
End Sub


Private Sub UserForm_QueryClose(Cancel As Integer, CloseMode As Integer)
    On Error Resume Next
    Unload UserForm1
    On Error GoTo 0
End Sub

'aListView	Class


'* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
'* Class      : aListView
'* Author     : Anastasiou Alex
'* Contacts   : AnastasiouAlex@gmail.com
'*
'* BLOG       : https://alexofrhodes.github.io/
'* GITHUB     : https://github.com/alexofrhodes/
'* YOUTUBE    : https://www.youtube.com/channel/UC5QH3fn1zjx0aUjRER_rOjg
'* VK         : https://vk.com/video/playlist/735281600_1
'*
'* Modified   : Date and Time       Author              Description
'* Created    : 29-06-2023 13:45    Alex
'* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *

Option Explicit

Public WithEvents EventDragSort As ListView
Public WithEvents EventDropFiles As ListView
Public WithEvents aListViewEvent As ListView

#If VBA7 Then
    Private Declare PtrSafe Function SendMessage Lib "user32" Alias "SendMessageA" (ByVal hwnd As Long, ByVal wMsg As Long, ByVal wParam As Long, lParam As Any) As Long
    Private Declare PtrSafe Function SendMessageLong Lib "user32" Alias "SendMessageA" (ByVal hwnd As Long, ByVal wMsg As Long, ByVal wParam As Long, ByVal lParam As Long) As Long
    Private Declare PtrSafe Function GetDC Lib "user32" (ByVal hwnd As Long) As Long
    Private Declare PtrSafe Function GetDeviceCaps Lib "gdi32" (ByVal hDC As Long, ByVal nIndex As Long) As Long
    Private Declare PtrSafe Function ReleaseDC Lib "user32" (ByVal hwnd As Long, ByVal hDC As Long) As Long
    
    Private Declare PtrSafe Sub mouse_event Lib "user32" (ByVal dwFlags As Long, ByVal dx As Long, ByVal dy As Long, ByVal cButtons As Long, ByVal dwExtraInfo As Long)
    Private Declare PtrSafe Sub Sleep Lib "kernel32" (ByVal dwMilliseconds As Long)
#Else
    Private Declare Function SendMessage Lib "user32" Alias "SendMessageA" (ByVal hwnd As Long, ByVal wMsg As Long, ByVal wParam As Long, lParam As Any) As Long
    Private Declare Function SendMessageLong Lib "user32" Alias "SendMessageA" (ByVal hwnd As Long, ByVal wMsg As Long, ByVal wParam As Long, ByVal lParam As Long) As Long
    private Declare Function GetDC Lib "user32" (ByVal hWnd As Long) As Long
    private Declare Function GetDeviceCaps Lib "gdi32" (ByVal hDC As Long, ByVal nIndex As Long) As Long
    private Declare Function ReleaseDC Lib "user32" (ByVal hWnd As Long, ByVal hDC As Long) As Long
    
    Private Declare Sub mouse_event Lib "user32" (ByVal dwFlags As Long, ByVal dx As Long, ByVal dy As Long, ByVal cButtons As Long, ByVal dwExtraInfo As Long)
    Private Declare Sub Sleep Lib "kernel32" (ByVal dwMilliseconds As Long)
#End If

Private Const MOUSEEVENTF_LEFTDOWN = &H2
Private Const MOUSEEVENTF_LEFTUP = &H4

'Windows API Constants
Private Const LOGPIXELSX = 88
Private Const LOGPIXELSY = 90

Private Const LVM_FIRST = &H1000
Private Const LVM_SETCOLUMNWIDTH = (LVM_FIRST + 30)
 
Private Const LVSCW_AUTOSIZE = -1
Private Const LVSCW_AUTOSIZE_USEHEADER = -2
 
Private Const LVM_GETNEXTITEM = (LVM_FIRST + 12)
Private Const LVNI_SELECTED = &H2
Private Const LVM_GETSELECTEDCOUNT = (LVM_FIRST + 50)

Public oListView As ListView

Private option_DD_LogFiles As Boolean
Private option_DD_LogFolders As Boolean
Private option_DD_IncludeSubfolders As Boolean
Private option_DD_CommaSeparatedFilters As String


Public LstItmObj As ListItem
Public swapNeeded As Boolean 'swap mode

Private Type POINTAPI
    x As Long
    y As Long
End Type
Private Type LVHITTESTINFO
    pt As POINTAPI
    Flags As Long
    iItem As Long
    iSubItem  As Long
End Type

Private Const LVM_HITTEST As Long = &H418
Private Const LVM_SUBITEMHITTEST As Long = &H1039
Private Const LVHT_ONITEM As Long = &H2

Public Function Value()
    Dim headCount As Long: headCount = oListView.ColumnHeaders.Count
    Dim rowCount As Long: rowCount = oListView.ListItems.Count
    
    Dim arr()
    ReDim arr(1 To rowCount, 1 To headCount)
    Dim x As Long
    Dim y As Long
    For y = 1 To oListView.ColumnHeaders.Count
        arr(1, y) = oListView.ColumnHeaders(y)
    Next
    If oListView.ListItems.Count > 0 Then
        For x = 1 To oListView.ListItems.Count
            For y = 1 To headCount
                If y = 1 Then
                    arr(x, y) = oListView.ListItems(x)
                Else
                    arr(x, y) = oListView.ListItems(x).ListSubItems(y - 1)
                End If
            Next
        Next
        Value = arr
    Else
        Value = Array()
    End If
End Function

Public Function RowArray(targetRow As Long)
    Dim headCount As Long: headCount = oListView.ColumnHeaders.Count
    Dim arr
    ReDim arr(1 To headCount)
    Dim i As Long
    For i = 1 To headCount
        If i = 1 Then
            arr(i) = oListView.ListItems(targetRow)
        Else
            arr(i) = oListView.ListItems(targetRow).ListSubItems(i - 1)
        End If
    Next
    RowArray = arr
End Function

Public Function SelectionArray()
    Dim item As MSComctlLib.ListItem
    Dim targetRow As Long
    Dim arr()
    Dim i As Long, counter As Long
    Dim headCount As Long: headCount = oListView.ColumnHeaders.Count
    If oListView.MultiSelect = False Then
        targetRow = oListView.selectedItem.index
        arr = RowArray(targetRow)
    Else
        ReDim arr(1 To 1, 1 To headCount)
        For Each item In oListView.ListItems
            If item.Selected Then
                targetRow = item.index
                counter = counter + 1
                ReDim Preserve arr(1 To counter, 1 To headCount)
                For i = 1 To headCount
                    If i = 1 Then
                        arr(counter, i) = oListView.ListItems(targetRow)
                    Else
                        arr(counter, i) = oListView.ListItems(targetRow).ListSubItems(i - 1)
                    End If
                Next
                
            End If
        Next
    End If
    SelectionArray = arr
End Function

Public Function ClickedColumn(x, y)
    'call from userform ListView1_MouseDown event and such
    ClickedColumn = -1
    
    Dim hitTestInfo As LVHITTESTINFO
    hitTestInfo.pt.x = x
    hitTestInfo.pt.y = y
    SendMessage oListView.hwnd, LVM_SUBITEMHITTEST, 0, hitTestInfo
    
    If hitTestInfo.Flags = 4 Then
        Dim ColumnIndex As Long
        ColumnIndex = hitTestInfo.iSubItem
        ClickedColumn = ColumnIndex
    End If

End Function

Sub DeselectAll()
    Dim sThisItem As Long, lLvHwnd As Long, lSelectedItems As Long, lItemIndex As Long

    On Error GoTo ErrFailed

    With oListView
        lLvHwnd = .hwnd
        lSelectedItems = SendMessage(lLvHwnd, LVM_GETSELECTEDCOUNT, 0, ByVal 0&)
        lItemIndex = -1
        For sThisItem = 1 To lSelectedItems
            lItemIndex = SendMessage(lLvHwnd, LVM_GETNEXTITEM, lItemIndex, ByVal LVNI_SELECTED)
            .ListItems(lItemIndex + 1).Selected = False
        Next
    End With
    Exit Sub

ErrFailed:
    Debug.Print Err.Description
    Debug.Assert False
End Sub

Public Sub EnableDragSort()
    Set EventDragSort = oListView
    swapNeeded = False
End Sub

Private Sub EventDragSort_OLEStartDrag(Data As MSComctlLib.DataObject, AllowedEffects As Long)
    Set LstItmObj = EventDragSort.selectedItem
End Sub

'when drop occurs we make mouseclick to select next item and then set swap mode on
Private Sub EventDragSort_OLEDragDrop(Data As MSComctlLib.DataObject, Effect As Long, Button As Integer, Shift As Integer, x As Single, y As Single)
    'that click will occur only after end of this Sub, that's why we can't make rows swaping here
    Call SingleClick
    swapNeeded = True
End Sub

'this Sub starts after OLEDragDrop ends so new row is already selected and old row is already saved to LstItmObj so here we just need to swap those two rows
Private Sub eventdragsort_MouseUp(ByVal Button As Integer, ByVal Shift As Integer, ByVal x As stdole.OLE_XPOS_PIXELS, ByVal y As stdole.OLE_YPOS_PIXELS)
    If LstItmObj Is Nothing Then Exit Sub
    If (swapNeeded) Then
        Sleep 30
        Dim insertedList As ListItem
        Dim SelectedIndex As Integer
        Dim newListSubItemObj As ListSubItem

        SelectedIndex = EventDragSort.selectedItem.index
        EventDragSort.ListItems.Remove LstItmObj.index

        Set insertedList = EventDragSort.ListItems.Add(SelectedIndex, LstItmObj.key, LstItmObj.Text, LstItmObj.Icon, LstItmObj.SmallIcon)
        For Each newListSubItemObj In LstItmObj.ListSubItems
                insertedList.ListSubItems.Add newListSubItemObj.index, newListSubItemObj.key, newListSubItemObj.Text, newListSubItemObj.ReportIcon, newListSubItemObj.TooltipText
        Next newListSubItemObj 'swap mode off again
        swapNeeded = False
        Set EventDragSort.selectedItem = EventDragSort.ListItems.item(SelectedIndex)
    End If
End Sub

Private Sub SingleClick()
  mouse_event MOUSEEVENTF_LEFTDOWN, 0, 0, 0, 0
  mouse_event MOUSEEVENTF_LEFTUP, 0, 0, 0, 0
End Sub

Public Sub EnableDropFilesFolders(LogFiles As Boolean, LogFolders As Boolean, IncludeSubFolders As Boolean, Optional CommaSeparatedFilters As String = "*")
    option_DD_LogFiles = LogFiles
    option_DD_LogFolders = LogFolders
    option_DD_IncludeSubfolders = IncludeSubFolders
    option_DD_CommaSeparatedFilters = CommaSeparatedFilters
    oListView.OLEDragMode = ccOLEDragAutomatic ' ccOLEDragManual
    oListView.OLEDropMode = ccOLEDropManual
    Set EventDropFiles = oListView
End Sub

Private Sub EventDropFiles_OLEDragDrop(Data As MSComctlLib.DataObject, Effect As Long, Button As Integer, Shift As Integer, x As Single, y As Single)
    '''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
    'The Format numbers used in the OLE DragDrop data structure, are:
    '''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
    'Text = 1 (vbCFText)
    'Bitmap = 2 (vbCFBitmap)
    'Metafile = 3
    'Emetafile = 14
    'DIB = 8
    'Palette = 9
    'Files = 15 (vbCFFiles)
    'RTF = -16639
    
    Select Case True
    Case Data.GetFormat(15) 'Format 15 is an array of names from WinExplorer
        If Not option_DD_LogFiles And Not option_DD_LogFiles Then Exit Sub
        Dim FileFullPath As String
        Dim fileItem As Long
        For fileItem = 1 To Data.Files.Count
            FileFullPath = Data.Files(fileItem)
            DD_AddToListview FileFullPath
        Next fileItem
    Case Else
    End Select
End Sub

Private Sub DD_AddToListview(FileFullPath As String)
    Dim objFSO As Scripting.FileSystemObject
    Dim objTopFolder As Scripting.Folder
    Set objFSO = CreateObject("Scripting.FileSystemObject")
    Dim FileName As String
    Dim var As Variant, element As Variant
        var = Split(option_DD_CommaSeparatedFilters, ",")
    If option_DD_LogFiles = True Then
        If LCase(IsFileFolderURL(FileFullPath)) = "f" Then
            FileName = Mid(FileFullPath, InStrRev(FileFullPath, "\") + 1)
            On Error Resume Next
            If Left(FileName, 1) <> "~" And (var(0) = "*" Or var(0) = "") Then GoTo PASS
            For Each element In var
                If InStr(1, FileFullPath, element, vbTextCompare) > 0 And Left(FileName, 1) <> "~" Then
PASS:

'where:
'Constant    Value   Description
'lvwText     0       (Default) Matches the string with a ListItem object's Text property.
'lvwSubitem  1       Matches the string with any string in a ListItem object's SubItems property.
'lvwTag      2       Matches the string with any ListItem object's Tag property.

'match:
'The settings for match are:
'Constant        Value   Description
'lvwWholeWord    0       (Default) An integer or constant specifying that a match will occur if the item's Text property begins with the whole word being searched. Ignored if the criteria is not text.
'lvwPartial      1       An integer or constant specifying that a match will occur if the item's Text property begins with the string being searched. Ignored if the criteria is not text.

'                    For full match:
'                        Dim itm As MSComctlLib.listItem
'                        Set itm = oListView.FindItem(sz:="subitemtext", where:=lvwSubItem, index:=2, fPartial:=lvwPartial)
'                    for partial search use this:
                    'Set itm = oListView.FindItem(sz:="partial", where:=lvwSubItem, index:=2, fPartial:lvwPartial)
                    
                    If Not oListView.FindItem(sz:=FileFullPath, where:=0, fPartial:=lvwWhole) Then
                        oListView.ListItems.Add , , FileFullPath
                    End If
                End If
            Next
        Else        'if drag dropped folder
            Set objTopFolder = objFSO.GetFolder(FileFullPath)
            DD_AddToListview objTopFolder.Path
        End If
    End If

    If option_DD_LogFolders = True Then
        If UCase(IsFileFolderURL(FileFullPath)) = "D" Then
            Set objTopFolder = objFSO.GetFolder(FileFullPath)
            If Not oListView.FindItem(sz:=objTopFolder.Path & "\", where:=0, fPartial:=lvwWhole) Then
                oListView.ListItems.Add , , objTopFolder.Path
            End If
            DD_AddToListview objTopFolder.Path
        End If
    End If
    Set objFSO = Nothing
    Set objTopFolder = Nothing
End Sub

Sub AutofitColumns()
    Dim counter As Long
    For counter = 1 To oListView.ColumnHeaders.Count
        Call SendMessageLong(oListView.hwnd, LVM_SETCOLUMNWIDTH, counter - 1, LVSCW_AUTOSIZE_USEHEADER)
    Next
End Sub

Public Function Init(LV As ListView) As aListView
'REFERENCE: Microsoft Windows Common Controls 6.0 (SP6)

    Set oListView = LV
    Set Init = Me

End Function

Public Sub Clear()
    oListView.ListItems.Clear
    oListView.ColumnHeaders.Clear
End Sub

Public Sub InitializeFromArray(inputArray As Variant)
    Clear
    Dim vListItem As ListItem
    Dim vChildItem As ListSubItem
    Dim vHeader As Variant
    Dim iRows As Long, iColumns As Long
    For iColumns = LBound(inputArray, 2) To UBound(inputArray, 2)
        Set vHeader = oListView.ColumnHeaders.Add(, , inputArray(LBound(inputArray, 1), iColumns))
    Next
    For iRows = LBound(inputArray, 1) + 1 To UBound(inputArray, 1)
        Set vListItem = oListView.ListItems.Add(, , inputArray(iRows, 1))
        For iColumns = LBound(inputArray, 2) + 1 To UBound(inputArray, 2)
            Set vChildItem = vListItem.ListSubItems.Add(, , inputArray(iRows, iColumns))
        Next
    Next
    oListView.View = lvwReport
End Sub

Public Sub AppendArray(inputArray As Variant)
    If ArrayDimensions(inputArray) <> 2 Then Exit Sub
    If UBound(inputArray, 2) + IIf(LBound(inputArray, 2) = 0, 1, 0) <> oListView.ColumnHeaders.Count Then Exit Sub
    Dim vListItem As ListItem
    Dim vChildItem As ListSubItem
    Dim iRows As Long, iColumns As Long
    For iRows = LBound(inputArray, 1) To UBound(inputArray, 1)
        Set vListItem = oListView.ListItems.Add(, , inputArray(iRows, 1))
        For iColumns = LBound(inputArray, 2) + 1 To UBound(inputArray, 2)
            Set vChildItem = vListItem.ListSubItems.Add(, , inputArray(iRows, iColumns))
        Next
    Next
'    oListView.View = lvwReport
End Sub

Public Sub EventListener()
    Set aListViewEvent = oListView
End Sub

'Private Sub aListViewEvent_Click()
'    MsgBox "Clicked the listview"
'End Sub

Private Sub aListViewEvent_MouseDown(ByVal Button As Integer, ByVal Shift As Integer, _
                                    ByVal x As stdole.OLE_XPOS_PIXELS, ByVal y As stdole.OLE_YPOS_PIXELS)

   'https://markdagosta.wordpress.com/2010/08/08/listview-hittest-right-click/
   Dim item As MSComctlLib.ListItem
   Dim lngXPixelsPerInch As Long, lngYPixelsPerInch As Long
   Dim lngDeviceHandle As Long
   'Only capture the standard right-clicks; otherwise get out.
   If (Button <> xlSecondaryButton) Or (Shift <> 0) Then Exit Sub
  'We must determine the Pixels per Inch for the display device.
   lngDeviceHandle = GetDC(0)
   lngXPixelsPerInch = GetDeviceCaps(lngDeviceHandle, LOGPIXELSX)
   lngYPixelsPerInch = GetDeviceCaps(lngDeviceHandle, LOGPIXELSY)
   ReleaseDC 0, lngDeviceHandle
  'Convert the event's x and y arguments from Pixels to Twips
  Set item = oListView.hitTest(x * 1440 / lngXPixelsPerInch, y * 1440 / lngYPixelsPerInch)
   MsgBox "List ID #" & item.index & ": " & item.Text & " has been right-clicked!"
End Sub

Public Sub RowsFormatOddEven()
    Dim i As Long, y As Long
    For i = 1 To oListView.ListItems.Count
        If i Mod 2 Then
            oListView.ListItems(i).ForeColor = vbBlue
            For y = 1 To oListView.ColumnHeaders.Count - 1
                oListView.ListItems(i).ListSubItems(y).ForeColor = vbBlue
            Next
        Else
            oListView.ListItems(i).ForeColor = vbRed
            For y = 1 To oListView.ColumnHeaders.Count - 1
                oListView.ListItems(i).ListSubItems(y).ForeColor = vbRed
            Next
        End If
    Next
    oListView.Refresh
End Sub

'Various	Module

Option Explicit

Public Type tCursor
    Left As Long
    Top As Long
End Type

Public Const LOGPIXELSX = 88
Public Const LOGPIXELSY = 90

#If VBA7 Then
    Public Declare PtrSafe Sub Sleep Lib "kernel32" (ByVal dwMilliseconds As Long)
    Public Declare PtrSafe Function InternetGetConnectedState Lib "wininet.dll" (ByRef dwFlags As Long, ByVal dwReserved As Long) As Long
    Public Declare PtrSafe Sub mouse_event Lib "user32" (ByVal dwFlags As Long, ByVal dx As Long, ByVal dy As Long, ByVal cButtons As Long, ByVal dwExtraInfo As Long)
    Public Declare PtrSafe Function GetAsyncKeyState Lib "user32" (ByVal vKey As Long) As Integer
    Public Declare PtrSafe Function ClientToScreen Lib "user32" (ByVal hwnd As Long, lpPoint As tCursor) As Long
    Public Declare PtrSafe Function GetCursorPos Lib "user32" (p As tCursor) As Long
    Public Declare PtrSafe Function FindWindow Lib "user32" Alias "FindWindowA" (ByVal lpClassName As String, ByVal lpWindowName As String) As Long
    Public Declare PtrSafe Function GetDC Lib "user32" (ByVal hwnd As Long) As Long
    Public Declare PtrSafe Function GetDeviceCaps Lib "gdi32" (ByVal hDC As Long, ByVal nIndex As Long) As Long
    Public Declare PtrSafe Function ReleaseDC Lib "user32" (ByVal hwnd As Long, ByVal hDC As Long) As Long
    Public Declare PtrSafe Function SetCursorPos Lib "user32" (ByVal x As Long, ByVal y As Long) As Long
#Else
    Public Declare Sub Sleep Lib "kernel32" (ByVal dwMilliseconds As Long)
    public Declare Function InternetGetConnectedState Lib "wininet.dll" (ByRef dwFlags As Long, ByVal dwReserved As Long) As Long
    Public Declare Sub mouse_event Lib "user32" (ByVal dwFlags As Long, ByVal dX As Long, ByVal dy As Long, ByVal cbuttons As Long, ByVal dwExtraInfo As Long)
    Public Declare Function GetAsyncKeyState Lib "user32" (ByVal vKey As Long) As Integer
    public Declare Function ClientToScreen Lib "user32" (ByVal hWnd As Long, lpPoint As tCursor) As Long
    public Declare Function GetCursorPos Lib "user32" (p As tCursor) As Long
    Public Declare Function FindWindow Lib "user32" Alias "FindWindowA" (ByVal lpClassName As String, ByVal lpWindowName As String) As Long
    Public Declare Function GetDC Lib "user32" (ByVal hWnd As Long) As Long
    Public Declare Function GetDeviceCaps Lib "gdi32" (ByVal hdc As Long, ByVal nIndex As Long) As Long
    Public Declare Function ReleaseDC Lib "user32" (ByVal hWnd As Long, ByVal hdc As Long) As Long
    Public Declare Function SetCursorPos Lib "user32" (ByVal X As Long, ByVal Y As Long) As Long
#End If

Enum MyColors
    FormBackgroundDarkGray = 4208182            ' BACKGROUND DARK GRAY
    FormSidebarMediumGray = 5457992             ' TILE COLORS LIGHTER GRAY
    FormSidebarMouseOverLightGray = &H808080    ' lighter light gray
    FormSelectedGreen = 8435998                 ' green tile
End Enum

Sub TableManagerButtonClicked(control As IRibbonControl)
'@AssignedModule Various
'@INCLUDE USERFORM uTableManager
    uTableManager.Show
End Sub

Function IsTime(ByVal inputValue As Variant) As Boolean
'@AssignedModule Various
    If IsNumeric(inputValue) Or IsDate(inputValue) Then
        ' Check if the numeric value is within the valid time range (0 to 1)
        If inputValue >= 0 And inputValue <= 1 Then
            ' Convert to total seconds in a day
            Dim totalSeconds As Double
            totalSeconds = inputValue * 24 * 60 * 60
            ' Validate the individual components of the time
            Dim hours As Long, minutes As Long, seconds As Long
            hours = Int(totalSeconds / 3600)
            totalSeconds = totalSeconds Mod 3600
            minutes = Int(totalSeconds / 60)
            seconds = totalSeconds Mod 60
            If hours >= 0 And hours < 24 And minutes >= 0 And minutes < 60 And seconds >= 0 And seconds < 60 Then
                IsTime = True
                Exit Function
            End If
        End If
    End If
    IsTime = False
End Function

Sub applyFormat(ByRef inputValue As Variant, ByVal inputRange As Range)
'@AssignedModule Various
    If IsEmpty(inputValue) Then Exit Sub
    On Error Resume Next
    Dim cellFormat As String
    cellFormat = inputRange.NumberFormat
    On Error GoTo 0
    If cellFormat = "General" Then Exit Sub
    On Error Resume Next
    Dim formattedValue As Variant
    formattedValue = Format(inputValue, cellFormat)
    On Error GoTo 0
    If Not IsError(formattedValue) Then inputValue = formattedValue
End Sub

Function ArrayFilterLike(inputArray As Variant, MatchThis As String, MatchCase As Boolean)
'@AssignedModule Various
    Dim OutputArray As Variant
    ReDim OutputArray(1 To 1)
    Dim counter As Long
    counter = 0
    Dim element
    Dim doesMatch As Boolean
    For Each element In inputArray
        doesMatch = IIf(MatchCase, _
                    element Like MatchThis, _
                    UCase(element) Like UCase(MatchThis))
        If doesMatch Then
            counter = counter + 1
            ReDim Preserve OutputArray(1 To counter)
            OutputArray(UBound(OutputArray)) = element
        End If
    Next
    ArrayFilterLike = OutputArray
End Function

Public Function IsValueFormattedCorrectly(ByVal inputRange As Range, ByVal inputValue As Variant) As Boolean
'@AssignedModule Various
    If IsEmpty(inputValue) Then
        IsValueFormattedCorrectly = True ' Empty values are always valid
        Exit Function
    End If

    On Error Resume Next
    Dim cellFormat As String
    cellFormat = inputRange.NumberFormat
    On Error GoTo 0

    If cellFormat = "General" Then
        IsValueFormattedCorrectly = True ' General format allows any value
        Exit Function
    End If

    On Error Resume Next
    Dim formattedValue As Variant
    formattedValue = Format(inputValue, cellFormat)
    On Error GoTo 0

    If Not IsError(formattedValue) Then
        ' The value can be formatted correctly, now compare the formatted value with the original input value
        IsValueFormattedCorrectly = (formattedValue = inputValue)
    Else
        ' The value cannot be formatted correctly
        IsValueFormattedCorrectly = False
    End If
End Function

Public Function aSwitch(CheckThis, ParamArray OptionPairs() As Variant)
'@AssignedModule Various
    Dim i As Long
    For i = LBound(OptionPairs) To UBound(OptionPairs) Step 2
        If UCase(CheckThis) = UCase(OptionPairs(i)) Then
            aSwitch = OptionPairs(i + 1)
            Exit Function
        End If
    Next
End Function

Function CountOfCharacters(SearchHere As String, FindThis As String)
'@AssignedModule Various
    CountOfCharacters = (Len(SearchHere) - Len(Replace(SearchHere, FindThis, ""))) / Len(FindThis)
End Function
Function AvailableFormOrFrameRow(FormOrFrame As Object, Optional AfterWidth As Long = 0, Optional AfterHeight As Long = 0, Optional AddMargin As Long = 0) As Long
'@AssignedModule Various
    Dim ctr As MSForms.control
    Dim myHeight
    For Each ctr In FormOrFrame.Controls
        If ctr.Visible = True Then
            If ctr.Left >= AfterWidth And ctr.Top >= AfterHeight Then
                If ctr.Top + ctr.Height > myHeight Then myHeight = ctr.Top + ctr.Height
            End If
        End If
    Next
    AvailableFormOrFrameRow = myHeight + AddMargin '6
End Function

Function AvailableFormOrFrameColumn(FormOrFrame As Object, Optional AfterWidth As Long = 0, Optional AfterHeight As Long = 0, Optional AddMargin As Long = 0) As Long
'@AssignedModule Various

    Dim ctr As MSForms.control
    Dim myWidth
    For Each ctr In FormOrFrame.Controls
        If ctr.Visible = True Then
            If ctr.Left >= AfterWidth And ctr.Top >= AfterHeight Then
                If ctr.Left + ctr.Width > myWidth Then myWidth = ctr.Left + ctr.Width
            End If
        End If
    Next
    AvailableFormOrFrameColumn = myWidth + AddMargin '6
End Function

Function Transpose2DArray(inputArray As Variant) As Variant
'@AssignedModule Various

    Dim x As Long, yUbound As Long
    Dim y As Long, xUbound As Long
    Dim tempArray As Variant
    xUbound = UBound(inputArray, 2)
    yUbound = UBound(inputArray, 1)
    ReDim tempArray(1 To xUbound, 1 To yUbound)
    For x = 1 To xUbound
        For y = 1 To yUbound
            tempArray(x, y) = inputArray(y, x)
        Next y
    Next x
    Transpose2DArray = tempArray
End Function


Function IsFileFolderURL(Path) As String
'@AssignedModule Various
'@INCLUDE PROCEDURE URLExists
'@INCLUDE PROCEDURE FolderExists
'@INCLUDE PROCEDURE FileExists
    Dim retVal As String
    retVal = "I"
    If (retVal = "I") And FileExists(Path) Then retVal = "F"
    If (retVal = "I") And FolderExists(Path) Then retVal = "D"
    If (retVal = "I") And URLExists(Path) Then retVal = "U"
    ' I => Invalid | F => File | D => Directory | U => Valid Url
    IsFileFolderURL = retVal
End Function

Function URLExists(url) As Boolean
'@AssignedModule Various
    Dim Request As Object
    Dim FF As Integer
    Dim rc As Variant

    On Error GoTo EndNow
    Set Request = CreateObject("WinHttp.WinHttpRequest.5.1")

    With Request
      .Open "GET", url, False
      .send
      rc = .statusText
    End With
    Set Request = Nothing
    If rc = "OK" Then URLExists = True

    Exit Function
EndNow:
End Function

Function FolderExists(ByVal strPath As String) As Boolean
'@AssignedModule Various

    On Error Resume Next
    FolderExists = ((GetAttr(strPath) And vbDirectory) = vbDirectory)
End Function

Public Function FileExists(ByVal FileName As String) As Boolean
'@AssignedModule Various

    If InStr(1, FileName, "\") = 0 Then Exit Function
    If Right(FileName, 1) = "\" Then FileName = Left(FileName, Len(FileName) - 1)
    On Error Resume Next
    FileExists = (Dir(FileName, vbArchive + vbHidden + vbReadOnly + vbSystem) <> "")
End Function


Function WorkbookProjectProtected(ByVal TargetWorkbook As Workbook) As Boolean
'@AssignedModule Various
'@INCLUDE DECLARATION TargetWorkbook
        WorkbookProjectProtected = (TargetWorkbook.VBProject.Protection = 1)
End Function

Function TxtRead(sPath As Variant) As String
'@AssignedModule Various
    Dim sTXT As String
    If Dir(sPath) = "" Then
        Debug.Print "File was not found."
        Debug.Print sPath
        Exit Function
    End If
    Open sPath For Input As #1
    Do Until EOF(1)
        Line Input #1, sTXT
        TxtRead = TxtRead & sTXT & vbLf
    Loop
    Close
    If Len(TxtRead) = 0 Then
        TxtRead = ""
    Else
        TxtRead = Left(TxtRead, Len(TxtRead) - 1)
    End If
End Function

Function GetInputRange(ByRef rInput As Excel.Range, _
                    sPrompt As String, _
                    sTitle As String, _
                    Optional ByVal sDefault As String, _
                    Optional ByVal bActivate As Boolean, _
                    Optional x, _
                    Optional y) As Boolean
'@AssignedModule Various

'assigns range to variable passed
'GetInputRange(rng, "Range picker", "Select range to output listbox' list") = False Then Exit Sub
    Dim bGotRng As Boolean
    Dim bEvents As Boolean
    Dim nAttempt As Long
    Dim sAddr As String
    Dim vReturn
    On Error Resume Next
    If Len(sDefault) = 0 Then
        If TypeName(Application.Selection) = "Range" Then
            sDefault = "=" & Application.Selection.Address
            If Len(sDefault) > 240 Then
                sDefault = "=" & Application.ActiveCell.Address
            End If
        ElseIf TypeName(Application.ActiveSheet) = "Chart" Then
            sDefault = " first select a Worksheet"
        Else
            sDefault = " Select Cell(s) or type address"
        End If
    End If
    Set rInput = Nothing
    For nAttempt = 1 To 3
        vReturn = False
        vReturn = Application.InputBox(sPrompt, sTitle, sDefault, x, y, Type:=0)
        If False = vReturn Or Len(vReturn) = 0 Then
            Exit For
        Else
            sAddr = vReturn
            If Left$(sAddr, 1) = "=" Then sAddr = Mid$(sAddr, 2, 256)
            If Left$(sAddr, 1) = Chr(34) Then sAddr = Mid$(sAddr, 2, 255)
            If Right$(sAddr, 1) = Chr(34) Then sAddr = Left$(sAddr, Len(sAddr) - 1)
            Set rInput = Application.Range(sAddr)
            If rInput Is Nothing Then
                sAddr = Application.ConvertFormula(sAddr, xlR1C1, xlA1)
                Set rInput = Application.Range(sAddr)
                bGotRng = Not rInput Is Nothing
            Else
                bGotRng = True
            End If
        End If
        If bGotRng Then
            If bActivate Then
                On Error GoTo ErrH
                bEvents = Application.EnableEvents
                Application.EnableEvents = False
                If Not Application.ActiveWorkbook Is rInput.Parent.Parent Then
                    rInput.Parent.Parent.Activate
                End If
                If Not Application.ActiveSheet Is rInput.Parent Then
                    rInput.Parent.Activate
                End If
                rInput.Select
            End If
            Exit For
        ElseIf nAttempt < 3 Then
            If MsgBox("Invalid reference, do you want to try again ?", _
                vbOKCancel, sTitle) <> vbOK Then
                Exit For
            End If
        End If
    Next
cleanup:
    On Error Resume Next
    If bEvents Then
        Application.EnableEvents = True
    End If
    GetInputRange = bGotRng
    Exit Function
ErrH:
    Set rInput = Nothing
    bGotRng = False
    Resume cleanup
End Function

Sub TxtOverwrite(sFile As String, sText As String)
'@AssignedModule Various

    On Error GoTo ERR_HANDLER
    Dim FileNumber As Integer
    FileNumber = FreeFile
    Open sFile For Output As #FileNumber
    Print #FileNumber, sText
    Close #FileNumber
Exit_Err_Handler:
    Exit Sub
ERR_HANDLER:
    MsgBox "The following error has occurred" & vbCrLf & vbCrLf & _
    "Error Number: " & Err.Number & vbCrLf & _
    "Error Source: TxtOverwrite" & vbCrLf & _
    "Error Description: " & Err.Description, vbCritical, "An Error has Occurred!"
    GoTo Exit_Err_Handler
End Sub

Public Function ArrayContains( _
    ByVal value1 As Variant, _
    ByVal array1 As Variant, _
    Optional CaseSensitive As Boolean) _
    As Boolean
   '@Description: This function checks if a value is in an array
    '@Author: Anthony Mancini
    '@Version: 1.0.0
    '@License: MIT
    '@Param: value1 is the value that will be checked if its in the array
    '@Param: array1 is the array
    '@Returns: Returns boolean True if the value is in the array, and false otherwise
    '@Example: =IsInArray("hello", {"one", 2, "hello"}) -> True
    '@Example: =IsInArray("hello", {1, "two", "three"}) -> False
'@AssignedModule Various

    Dim individualElement As Variant
    If CaseSensitive = True Then value1 = UCase(value1)
    For Each individualElement In array1
        If CaseSensitive = True Then individualElement = UCase(individualElement)
        If individualElement = value1 Then
            ArrayContains = True
            Exit Function
        End If
    Next
    ArrayContains = False
End Function

Public Function ArrayAllocated(ByVal arr As Variant) As Boolean
'@AssignedModule Various

    On Error Resume Next
    ArrayAllocated = IsArray(arr) And (Not IsError(LBound(arr, 1))) And LBound(arr, 1) <= UBound(arr, 1)
End Function

Sub FoldersCreate(FolderPath As String)
'@AssignedModule Various
'@INCLUDE PROCEDURE FolderExists
    On Error Resume Next
    Dim individualFolders() As String
    Dim tempFolderPath As String
    Dim ArrayElement As Variant
    individualFolders = Split(FolderPath, "\")
    For Each ArrayElement In individualFolders
        tempFolderPath = tempFolderPath & ArrayElement & "\"
        If FolderExists(tempFolderPath) = False Then
            MkDir tempFolderPath
        End If
    Next ArrayElement
End Sub

Sub FollowLink(FolderPath As String)
'@AssignedModule Various
    If Right(FolderPath, 1) = "\" Then FolderPath = Left(FolderPath, Len(FolderPath) - 1)
    On Error Resume Next
    Dim oShell As Object
    Dim Wnd As Object
    Set oShell = CreateObject("Shell.Application")
    For Each Wnd In oShell.Windows
        If Wnd.Name = "File Explorer" Then
            If Wnd.document.Folder.Self.Path = FolderPath Then Exit Sub
        End If
    Next Wnd
    Application.ThisWorkbook.FollowHyperlink Address:=FolderPath, NewWindow:=True
End Sub

Function ArrayTrim(ByVal arr As Variant)
'@AssignedModule Various
        Dim i As Long
        For i = LBound(arr) To UBound(arr)
            arr(i) = Trim(arr(i))
        Next
        ArrayTrim = arr
End Function


'aListBox	Class



'@TODO C:\Users\acer\Dropbox\SOFTWARE\EXCEL\Manfred van den Noort\      '-> listbox_drag_drop_advanced

'* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
'* Class      : aListBox
'* Author     : Anastasiou Alex
'* Contacts   : AnastasiouAlex@gmail.com
'*
'* BLOG       : https://alexofrhodes.github.io/
'* GITHUB     : https://github.com/alexofrhodes/
'* YOUTUBE    : https://www.youtube.com/channel/UC5QH3fn1zjx0aUjRER_rOjg
'* VK         : https://vk.com/video/playlist/735281600_1
'*
'* Modified   : Date and Time       Author              Description
'* Created    : 04-05-2023 12:18    Alex
'* Modified   : 27-06-2023 12:38    Alex                figured how to get the control's hWnd
'* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *

Option Explicit


Public WithEvents ListboxEvents As MSForms.ListBox

Private oListBox As MSForms.ListBox
Private oForm As Object

#If VBA7 Then
    Private Declare PtrSafe Function GetSystemMetrics Lib "user32" (ByVal nIndex As Long) As Long
#Else
    Private Declare Function GetSystemMetrics Lib "user32" (ByVal nIndex As Long) As Long
#End If

Private Type POINTAPI
    x               As Long
    y               As Long
End Type

#If VBA7 Then
    
    Private Type msg
        hwnd            As LongPtr
        message         As Long
        wParam          As LongPtr
        lParam          As LongPtr
        time            As Long
        pt              As POINTAPI
    End Type
    
    #If Win64 Then
    Private Declare PtrSafe Function WindowFromPoint Lib "user32" (ByVal POINT As LongPtr) As LongPtr
    #Else
    Private Declare PtrSafe Function WindowFromPoint Lib "user32" (ByVal xPoint As Long, ByVal yPoint As Long) As LongPtr
    #End If
    
    Private Declare PtrSafe Function GetMessage Lib "user32" Alias "GetMessageA" (lpMsg As msg, ByVal hwnd As LongPtr, ByVal wMsgFilterMin As Long, ByVal wMsgFilterMax As Long) As Long
    Private Declare PtrSafe Function DispatchMessage Lib "user32" Alias "DispatchMessageA" (lpMsg As msg) As LongPtr
    Private Declare PtrSafe Function TranslateMessage Lib "user32" (lpMsg As msg) As Long
    Private Declare PtrSafe Function WindowFromAccessibleObject Lib "oleacc" (ByVal pacc As IAccessible, phwnd As LongPtr) As Long
    Private Declare PtrSafe Function IsWindow Lib "user32" (ByVal hwnd As LongPtr) As Long
    Private Declare PtrSafe Sub DragAcceptFiles Lib "shell32.dll" (ByVal hwnd As LongPtr, ByVal fAccept As Long)
    Private Declare PtrSafe Sub DragFinish Lib "shell32.dll" (ByVal HDROP As LongPtr)
    Private Declare PtrSafe Function DragQueryFile Lib "shell32.dll" Alias "DragQueryFileA" (ByVal HDROP As LongPtr, ByVal UINT As Long, ByVal lpStr As String, ByVal ch As Long) As Long
    Private Declare PtrSafe Sub CopyMemory Lib "kernel32" Alias "RtlMoveMemory" (Destination As Any, Source As Any, ByVal Length As LongPtr)

#Else
    
    Private Type msg
        hwnd            As Long
        message         As Long
        wParam          As Long
        lParam          As Long
        time            As Long
        pt              As POINTAPI
    End Type
    
    Private Declare Function WindowFromPoint Lib "user32" (ByVal xPoint As Long, ByVal yPoint As Long) As Long
    Private Declare Function GetMessage Lib "user32" Alias "GetMessageA" (lpMsg As MSG, ByVal hwnd As Long, ByVal wMsgFilterMin As Long, ByVal wMsgFilterMax As Long) As Long
    Private Declare Function DispatchMessage Lib "user32" Alias "DispatchMessageA" (lpMsg As MSG) As Long
    Private Declare Function TranslateMessage Lib "user32" (lpMsg As MSG) As Long
    Private Declare Function WindowFromAccessibleObject Lib "oleacc" (ByVal pacc As IAccessible, phwnd As Long) As Long
    Private Declare Function IsWindow Lib "user32" (ByVal hwnd As Long) As Long
    Private Declare Sub DragAcceptFiles Lib "shell32.dll" (ByVal hwnd As Long, ByVal fAccept As Long)
    Private Declare Sub DragFinish Lib "shell32.dll" (ByVal HDROP As Long)
    Private Declare Function DragQueryFile Lib "shell32.dll" Alias "DragQueryFileA" (ByVal HDROP As Long, ByVal UINT As Long, ByVal lpStr As String, ByVal ch As Long) As Long
    Private Declare Sub CopyMemory Lib "kernel32" Alias "RtlMoveMemory" (Destination As Any, Source As Any, ByVal Length As Long)
#End If

Public Function Init(ByRef TargetListBox As MSForms.ListBox) As aListBox
    Set oListBox = TargetListBox
    Set oForm = oListBox.Parent
    Set Init = Me
    
'    Me.AutofitColumns  '@TODO where to put this? needed for TargetColumn, in case they were not manually set
End Function

Public Function Parent()
    Set Parent = oForm
End Function

Public Sub EnableEvents1()
        Set ListboxEvents = oListBox
End Sub

Public Sub listboxevents_MouseUp(ByVal Button As Integer, ByVal Shift As Integer, ByVal x As Single, ByVal y As Single)
    MsgBox SelectedRowsText
End Sub


Public Function targetColumn(x, y)
        ' Calculate the column index based on the X-coordinate
        Dim ColumnIndex As Long
        Dim columnWidth As Long
        Dim totalWidth As Long
        Dim totalColumns As Long
        
        totalColumns = oListBox.columnCount
        
        ' Calculate the total width of all columns
        For ColumnIndex = 1 To totalColumns
            columnWidth = Replace(Split(oListBox.ColumnWidths, ";")(ColumnIndex - 1), " pt", "")
            totalWidth = totalWidth + columnWidth
        Next ColumnIndex
        
        ' Calculate the approximate column index based on the X-coordinate
        Dim clickedX As Long
        clickedX = x
        
        If clickedX > 0 Then
            Dim cumulativeWidth As Long
            Dim lastColumnIndex As Long
            
            For ColumnIndex = 1 To totalColumns
                columnWidth = Replace(Split(oListBox.ColumnWidths, ";")(ColumnIndex - 1), " pt", "")
                cumulativeWidth = cumulativeWidth + columnWidth
                
                If clickedX < cumulativeWidth Then
                    ' The mouse click is within this column
                    lastColumnIndex = ColumnIndex
                    Exit For
                End If
            Next ColumnIndex
            
            ' Display the clicked column index
            targetColumn = lastColumnIndex
        End If
End Function

Sub AutofitColumns(Optional ResizeListbox As Boolean)
 Application.ScreenUpdating = False
    Dim ws As Worksheet
    If sheetExists("ListboxColumnWidth", ThisWorkbook) = False Then
        Set ws = ThisWorkbook.Worksheets.Add
        ws.Name = "ListboxColumnwidth"
    Else
        Set ws = ThisWorkbook.Worksheets("ListboxColumnwidth")
        ws.Cells.Clear
    End If
    
    '---Listbox to range-----
    Dim rng As Range
    Set rng = ThisWorkbook.Sheets("ListboxColumnwidth").Range("A1")
    Set rng = rng.Resize(UBound(oListBox.List) + 1, oListBox.columnCount)
    rng = oListBox.List
    
    '---Get ColumnWidths------
    rng.Columns.AutoFit
    Dim sWidth As String
    Dim vR() As Variant
    Dim N As Integer
    Dim cell As Range
    For Each cell In rng.Resize(1)
        N = N + 1
        ReDim Preserve vR(1 To N)
        vR(N) = cell.EntireColumn.Width
    Next cell
    sWidth = Join(vR, ";")
    'Debug.Print sWidth

    '---assign ColumnWidths----
    With oListBox
        .ColumnWidths = sWidth
        '.RowSource = "A1:A3"
        .BorderStyle = fmBorderStyleSingle
    End With
        
    'remove worksheet
    Application.DisplayAlerts = False
    ws.Delete
    Application.DisplayAlerts = True
    
    Application.ScreenUpdating = True
    
    '----Resize Listbox--------
    If ResizeListbox = False Then Exit Sub
    Dim w As Long
    Dim i As Long
    For i = LBound(vR) To UBound(vR)
        w = w + vR(i)
    Next
    DoEvents
    oListBox.Width = w + 10
End Sub

Private Function sheetExists(sheetToFind As String, Optional InWorkbook As Workbook) As Boolean
    If InWorkbook Is Nothing Then Set InWorkbook = ThisWorkbook
    On Error Resume Next
    sheetExists = Not InWorkbook.Sheets(sheetToFind) Is Nothing
End Function

Function LoadCSV(TargetFile As String, Clear As Boolean)
    If Clear Then oListBox.Clear
    Dim ArrayOfElements
    Dim s: s = TxtRead(TargetFile)
    Dim arr
    arr = Split(s, vbLf)
    Dim iCols, iRows
    iRows = UBound(arr) + 1
    iCols = UBound(Split(arr(0), ",")) + 1
    ReDim ArrayOfElements(1 To iRows, 1 To iCols)
    Dim x, y
    For x = 1 To iRows
        For y = 1 To iCols
            ArrayOfElements(x, y) = Split(arr(x - 1), ",")(y - 1)
        Next
    Next
    oListBox.columnCount = iCols
    oListBox.List = ArrayOfElements
End Function

Sub ToRange(cell As Range)
    cell.Resize(oListBox.ListCount, oListBox.columnCount) = oListBox.List
End Sub

Sub ClearSelection(lBox As MSForms.ListBox)
    On Error Resume Next
    Dim i As Long
    For i = 0 To lBox.ListCount
        lBox.Selected(i) = False
    Next i
End Sub

Public Sub AcceptFiles(Optional sExpansion As String = "*.xlsm;*.xlsb;*.xlsx", Optional iDeepSubPath As Integer = 999)

'eg on userform_activate:
'aListBox.Init(ListBox1).AcceptFiles sExpansion:="*.xlsm;*.xlsb;*.xlsx",iDeepSubPath:=999
    
    'you can use .[_GethWnd] with controls which support it
    Dim ctr As control: Set ctr = oListBox
    Dim lGethWnd As LongPtr: lGethWnd = ctr.[_GethWnd]

#If VBA7 Then
    Dim hwnd As LongPtr, HDROP As LongPtr
#Else
    Dim hwnd As Long, HDROP As Long
#End If

    Const WM_DROPFILES = &H233
    Dim tMsg As msg, sFileName As String * 1000
    Dim lFilesCount As Long, i As Long
    hwnd = aUserform.Init(oForm).hwnd
    Call DragAcceptFiles(lGethWnd, True)

    Dim sPathFile   As String

    Do While GetMessage(tMsg, 0, 0, 0) And IsWindow(hwnd)
        If tMsg.message = WM_DROPFILES Then
            HDROP = tMsg.wParam
            lFilesCount = DragQueryFile(HDROP, &HFFFFFFFF, 0, 0)
            If lFilesCount Then
                For i = 0 To lFilesCount - 1
                    sPathFile = VBA.Trim$(VBA.Left(sFileName, DragQueryFile(HDROP, i, sFileName, VBA.Len(sFileName))))
                    Call getFilesFromPath(sPathFile, sExpansion, iDeepSubPath)
                Next i
            End If
            Call DragFinish(HDROP)
        End If
        Call TranslateMessage(tMsg)
        Call DispatchMessage(tMsg)
    Loop
End Sub

Private Sub getFilesFromPath(ByVal sPathFile As String, ByVal sExpansion As String, ByVal iDeepSubPath As Integer)
    Dim sExp        As String
    sExp = sGetExtensionName(sPathFile)
    If sExp = vbNullString Then
        Dim oCol    As Collection
        Set oCol = GetFiles(sPathFile, iDeepSubPath)
        Dim i       As Integer
        Dim iCount  As Integer
        iCount = oCol.Count
        Dim sFile   As String
        For i = 1 To iCount
            sFile = oCol.item(i).Path
            sExp = sGetExtensionName(sFile)
            Call filterFiles(sFile, sExpansion, sExp)
        Next i
    Else
        Call filterFiles(sPathFile, sExpansion, sExp)
    End If
End Sub

Private Sub filterFiles(ByVal sPathFile As String, ByVal sExpansion As String, ByVal sExp As String)
    sExp = "*." & sExp
    If sExpansion = vbNullString Or sExpansion = "*.*" Then
    ElseIf Not likeExp(sExpansion, sExp) Then
        Exit Sub
    End If
    oListBox.AddItem sPathFile
End Sub

Private Function likeExp(ByVal sExpansion As String, ByVal sExp As String) As Boolean
    Dim arr         As Variant
    arr = VBA.Split(sExpansion, ";")
    Dim i           As Long
    Dim iCount      As Long
    iCount = UBound(arr, 1)
    For i = 0 To iCount
        If sExp Like arr(i) Then
            likeExp = True
            Exit For
        End If
    Next i
End Function

Private Function sGetExtensionName(ByVal sPathFile As String) As String
    Dim FSO         As Object
    Set FSO = CreateObject("Scripting.FileSystemObject")
    sGetExtensionName = FSO.GetExtensionName(sPathFile)
    Set FSO = Nothing
End Function

Private Function GetFiles(ByVal Path As String, ByVal iDeepSubPath As Integer) As Collection
    Dim MainFolder  As Object
    Dim iFolder     As Object
    Dim iFile       As Object
    Dim FSO         As Object
    Dim MainColl    As New Collection
    Dim iColl       As Collection
    Dim i           As Long

    Set FSO = CreateObject("Scripting.FileSystemObject")
    Set MainFolder = FSO.GetFolder(Path)
    If MainFolder Is Nothing Then Exit Function

    For Each iFile In MainFolder.Files
        If VBA.InStr(1, iFile.Name, "~") = 0 Then
            MainColl.Add iFile, iFile.Path
        End If
    Next

    If iDeepSubPath > 0 Then
        For Each iFolder In MainFolder.SubFolders
            Set iColl = GetFiles(iFolder.Path, iDeepSubPath - 1)
            For i = 1 To iColl.Count
                MainColl.Add iColl(i)
            Next
        Next
    End If
    Set GetFiles = MainColl
End Function

Public Sub HeightToEntries(ByVal NumberOfEntries As Long)
    Const SM_CYEDGE = 46&
    With oListBox
        NumberOfEntries = IIf(NumberOfEntries > .ListCount, .ListCount, NumberOfEntries)
        .Height = ((9.75 * NumberOfEntries) + IIf(.SpecialEffect = fmSpecialEffectFlat, 0, GetSystemMetrics(SM_CYEDGE)))
    End With
End Sub

Public Sub LoadVBProjects()
    oListBox.Clear
    ListWorkbooks
    ListAddins
    Exit Sub

End Sub
Sub ListAddins()
    On Error GoTo ErrorHandler
    Dim vbProj As VBProject
    Dim wbPath As String
    For Each vbProj In Application.VBE.VBProjects
        On Error GoTo ErrorHandler
        wbPath = vbProj.FileName
        If Right(wbPath, 4) = "xlam" Or Right(wbPath, 3) = "xla" Then
            Dim wbName As String
            wbName = Mid(wbPath, InStrRev(wbPath, "\") + 1)
            If WorkbookProjectProtected(Workbooks(wbName)) = False Then
                oListBox.AddItem wbName
            End If
        End If
SKIP:
    Next vbProj
    Exit Sub
ErrorHandler:
    If Err.Number = 76 Then GoTo SKIP
End Sub
Sub ListWorkbooks()
    Dim WB As Workbook
    For Each WB In Workbooks
        If Len(WB.Path) > 0 Then
            If WorkbookProjectProtected(WB) = False Then
                oListBox.AddItem WB.Name
            End If
        End If
    Next
End Sub

Public Sub SelectItems(This As Variant, Optional ByIndex As Boolean)
    Dim i As Long
    Select Case TypeName(This)
    Case Is = "String", "Long", "Integer"
        For i = 0 To oListBox.ListCount - 1
            If oListBox.List(i) = CStr(This) Then
                oListBox.Selected(i) = True
                DoEvents
                If oListBox.MultiSelect = fmMultiSelectSingle Then Exit Sub
            End If
        Next
    Case Else
        Dim el As Variant
        If ByIndex Then
            For Each el In This
                oListBox.Selected(el) = True
            Next
        Else
            For Each el In This
                For i = 0 To oListBox.ListCount - 1
                    If oListBox.List(i) = el Then
                        oListBox.Selected(i) = True
                        DoEvents
                    End If
                Next
            Next
        End If
    End Select
End Sub

Public Sub AddHeader(Header As MSForms.ListBox, arrHeaders)
    Header.Width = oListBox.Width
    Dim i As Long
    Header.columnCount = oListBox.columnCount
    Header.ColumnWidths = oListBox.ColumnWidths
    Header.Clear
    Header.AddItem
    If ArrayDimensions(arrHeaders) = 1 Then
        For i = 0 To UBound(arrHeaders)
            Header.List(0, i) = arrHeaders(i)
        Next i
    Else
        For i = 1 To UBound(arrHeaders, 2)
            Header.List(0, i - 1) = arrHeaders(1, i)
        Next i
    End If
'    oListBox.ZOrder (1)
'    Header.ZOrder (0)
    Header.SpecialEffect = fmSpecialEffectFlat
    Header.BackColor = RGB(200, 200, 200)
    Header.Height = 15
    Header.Width = oListBox.Width
    Header.Left = oListBox.Left
    Header.Top = oListBox.Top - Header.Height - 1
    Header.Font.Bold = True
    Header.Font.Name = "Comic Sans MS"
    Header.Font.Size = oListBox.Font.Size
End Sub

Public Function Contains(This As String, _
                Optional ColumnIndexZeroBased As Long = -1, _
                Optional CaseSensitive As Boolean = False) As Boolean
    Dim i      As Long
    Dim N      As Long
    Dim sTemp  As String
    If ColumnIndexZeroBased > oListBox.columnCount - 1 Or ColumnIndexZeroBased < 0 Then
        ColumnIndexZeroBased = -1
    End If
    N = oListBox.ListCount
    If ColumnIndexZeroBased <> -1 Then
        For i = N - 1 To 0 Step -1
            If CaseSensitive = True Then
                sTemp = oListBox.List(i, ColumnIndexZeroBased)
            Else
                This = LCase(This)
                sTemp = LCase(oListBox.List(i, ColumnIndexZeroBased))
            End If
            If InStr(1, sTemp, This) > 0 Then
                Contains = True
                Exit Function
            End If
        Next i
    Else
        Dim columnCount As Long
        N = oListBox.ListCount
        For i = N - 1 To 0 Step -1
            For columnCount = 0 To oListBox.columnCount - 1
                If CaseSensitive = True Then
                    sTemp = oListBox.List(i, columnCount)
                Else
                    This = LCase(This)
                    sTemp = LCase(oListBox.List(i, columnCount))
                End If
                If InStr(1, sTemp, This) > 0 Then
                    Contains = True
                    Exit Function
                End If
            Next columnCount
        Next i
    End If
End Function

Public Sub FilterByColumn(This As String, _
                            Optional ColumnIndexZeroBased As Long = -1, _
                            Optional CaseSensitive As Boolean = False)
    Dim i               As Long
    Dim N               As Long
    Dim sTemp           As String
    If ColumnIndexZeroBased > oListBox.columnCount - 1 Or ColumnIndexZeroBased < 0 Then
        ColumnIndexZeroBased = -1
    End If
    N = oListBox.ListCount
    If ColumnIndexZeroBased <> -1 Then
        For i = N - 1 To 0 Step -1
            If CaseSensitive = True Then
                sTemp = oListBox.List(i, ColumnIndexZeroBased)
            Else
                This = LCase(This)
                sTemp = LCase(oListBox.List(i, ColumnIndexZeroBased))
            End If
            If InStr(1, sTemp, This) = 0 Then
                oListBox.RemoveItem (i)
            End If
        Next i
    Else
        Dim columnCount As Long
        N = oListBox.ListCount
        For i = N - 1 To 0 Step -1
            For columnCount = 0 To oListBox.columnCount - 1
                If CaseSensitive = True Then
                    sTemp = oListBox.List(i, columnCount)
                Else
                    This = LCase(This)
                    sTemp = LCase(oListBox.List(i, columnCount))
                End If
                If InStr(1, sTemp, This) > 0 Then
                Else
                    If columnCount = oListBox.columnCount - 1 Then
                        oListBox.RemoveItem (i)
                    End If
                End If
            Next columnCount
        Next i
    End If
End Sub

Public Sub SortOnColumn(OnColumn As Long)
    Dim vntData As Variant
    Dim vntTempItem As Variant
    Dim lngOuterIndex As Long
    Dim lngInnerIndex As Long
    Dim lngSubItemIndex As Long
    vntData = oListBox.List
    For lngOuterIndex = LBound(vntData, 1) To UBound(vntData, 1) - 1
        For lngInnerIndex = lngOuterIndex + 1 To UBound(vntData, 1)
            If vntData(lngOuterIndex, OnColumn) > vntData(lngInnerIndex, OnColumn) Then
                For lngSubItemIndex = 0 To oListBox.columnCount - 1
                    vntTempItem = vntData(lngOuterIndex, lngSubItemIndex)
                    vntData(lngOuterIndex, lngSubItemIndex) = vntData(lngInnerIndex, lngSubItemIndex)
                    vntData(lngInnerIndex, lngSubItemIndex) = vntTempItem
                Next
            End If
        Next lngInnerIndex
    Next lngOuterIndex
    oListBox.Clear
    oListBox.List = vntData
End Sub

Public Function selectedIndexes() As Collection
    Dim i As Long
    Dim coll As New Collection
    If oListBox.ListCount > 0 Then
        For i = 0 To oListBox.ListCount - 1
            If oListBox.Selected(i) Then coll.Add i
        Next i
    End If
    Set selectedIndexes = coll
End Function

Public Function SelectedValues() As Collection 'single column
    Dim i As Long
    Dim ListItem As Long
    Dim selectedCollection As Collection
    Set selectedCollection = New Collection
    Dim listboxCount As Long

        If oListBox.ListCount > 0 Then
            For i = 0 To oListBox.ListCount - 1
                If oListBox.Selected(i) Then
                    selectedCollection.Add oListBox.List(i, oListBox.BoundColumn - 1)
                End If
            Next i
        End If

    Set SelectedValues = selectedCollection
End Function


Public Function SelectedRowsText() As String
    Dim x As Long, y As Long
    Dim ListItem As Long
    Dim out As String
    Dim indexColl As New Collection
    Set indexColl = selectedIndexes
    For x = 1 To indexColl.Count
        For y = 0 To oListBox.columnCount - 1
            out = out & IIf(y > 0, "|", "") & oListBox.List(indexColl(x), y)
        Next
        If x < indexColl.Count Then out = out & vbNewLine
    Next
    SelectedRowsText = out
End Function


Public Function SelectedCount() As Long
    Dim i As Long
    Dim ListItem As Long
    Dim selectedCollection As Collection
    Set selectedCollection = New Collection
    Dim listboxCount As Long
    Dim counter As Long

        If oListBox.ListCount > 0 Then
            For i = 0 To oListBox.ListCount - 1
                If oListBox.Selected(i) = True Then
                    counter = counter + 1
                End If
            Next i
        End If
    SelectedCount = counter
End Function

Public Sub DeselectAll()
    If oListBox.ListCount <> 0 Then
        Dim i As Long
        For i = 0 To oListBox.ListCount - 1
            oListBox.Selected(i) = False
        Next i
    End If
End Sub

Public Sub SelectAll()
    If oListBox.ListCount <> 0 Then
        Dim i As Long
        For i = 0 To oListBox.ListCount - 1
            oListBox.Selected(i) = True
        Next i
    End If
End Sub

Public Sub SelectLike(This As String)
    DeselectAll
    If This = "" Then Exit Sub
    Dim i As Long
    For i = 0 To oListBox.ListCount - 1
        If UCase(oListBox.List(i, 1)) Like "*" & UCase(This) & "*" Then
            oListBox.Selected(i) = True
        End If
    Next i
End Sub

Public Sub SortAZ()
    Dim j As Long
    Dim i As Long
    Dim Temp As Variant
    With oListBox
        For j = 0 To .ListCount - 2
            For i = 0 To .ListCount - 2
                If LCase(.List(i)) > LCase(.List(i + 1)) Then
                    Temp = .List(i)
                    .List(i) = .List(i + 1)
                    .List(i + 1) = Temp
                End If
            Next i
        Next j
    End With
End Sub

Public Sub SortZA()
    Dim j As Long
    Dim i As Long
    Dim Temp As Variant
    With oListBox
        For j = 0 To .ListCount - 2
            For i = 0 To .ListCount - 2
                If LCase(.List(i)) < LCase(.List(i + 1)) Then
                    Temp = .List(i)
                    .List(i) = .List(i + 1)
                    .List(i + 1) = Temp
                End If
            Next i
        Next j
    End With
End Sub

'Sub ToRangeSelect(cell As Range)
'    cell.RESIZE(lBox.ListCount, oListBox.ColumnCount) = CollectionsToArray2D(SelectedValues)
'End Sub

Public Sub SelectedToRange()
   Dim rng As Range
    If GetInputRange(rng, "Range picker", "Select range to output listbox' list") = False Then Exit Sub
    Dim var: var = aCollection.CollectionsToArray2D(SelectedValues)
    rng.Resize(UBound(var, 1), oListBox.columnCount) = var
End Sub

Public Sub RemoveSelected()
    Dim coll As New Collection: Set coll = selectedIndexes
    If coll.Count = 0 Then Exit Sub
    Dim i As Long
    For i = coll.Count To 1 Step -1
        oListBox.RemoveItem coll(i)
    Next
End Sub



'aUserform	Class


'* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
'* Class      : aUserform
'* Author     : Anastasiou Alex
'* Contacts   : AnastasiouAlex@gmail.com
'*
'* BLOG       : https://alexofrhodes.github.io/
'* GITHUB     : https://github.com/alexofrhodes/
'* YOUTUBE    : https://www.youtube.com/channel/UC5QH3fn1zjx0aUjRER_rOjg
'* VK         : https://vk.com/video/playlist/735281600_1
'*
'* Modified   : Date and Time       Author              Description
'* Created    : 04-05-2023 12:19    Alex
'* Modified   : 30-06-2023 10:01    Alex                modified save/load options/position to use ini
'* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *

Option Explicit

Option Compare Text

Public WithEvents DockResize As UserForm
Private oForm As Object

Public TrackHeight As New Collection
Public TrackWidth As New Collection
Public TrackTop As New Collection
Public TrackLeft As New Collection

Private Const WS_MINIMIZEBOX = &H20000
Private Const WS_MAXIMIZEBOX = &H10000


Rem Transparent Declarations
Rem Private Declare PtrSafe Function GetWindowLong Lib "user32" Alias "GetWindowLongA" (ByVal hwnd As Long, ByVal nIndex As Long) As Long
#If VBA7 Then
    Private Declare PtrSafe Function DrawMenuBar Lib "user32" (ByVal hwnd As Long) As Long
    Private Declare PtrSafe Function SetLayeredWindowAttributes Lib "user32" (ByVal hwnd As Long, ByVal crKey As Long, ByVal bAlpha As Byte, ByVal dwFlags As Long) As Long
#Else
    Private Declare Function DrawMenuBar Lib "user32" (ByVal hWnd As Long) As Long
    Private Declare Function SetLayeredWindowAttributes Lib "user32" (ByVal hWnd As Long, ByVal crKey As Long, ByVal bAlpha As Byte, ByVal dwFlags As Long) As Long
#End If

Private Const GWL_STYLE As Long = (-16)
Private Const GWL_EXSTYLE As Long = (-20)
Private Const WS_CAPTION As Long = &HC00000
Private Const WS_EX_DLGMODALFRAME As Long = &H1

Private Const WS_EX_LAYERED = &H80000
Private Const LWA_COLORKEY = &H1
Private Const LWA_ALPHA = &H2

Private m_sngDownX As Single
Private m_sngDownY As Single

Rem Private Declare PtrSafe Function FindWindow Lib "user32" Alias "FindWindowA" (ByVal lpClassName As String, ByVal lpWindowName As String) As Long
Rem Private Declare PtrSafe Function SetWindowLong Lib "user32" Alias "SetWindowLongA" (ByVal hwnd As Long, ByVal nIndex As Long, ByVal dwNewLong As Long) As Long

Rem Parent Declarations
Private Const FORMAT_MESSAGE_ALLOCATE_BUFFER = &H100
Private Const FORMAT_MESSAGE_ARGUMENT_ARRAY = &H2000
Private Const FORMAT_MESSAGE_FROM_HMODULE = &H800
Private Const FORMAT_MESSAGE_FROM_STRING = &H400
Private Const FORMAT_MESSAGE_FROM_SYSTEM = &H1000
Private Const FORMAT_MESSAGE_IGNORE_INSERTS = &H200
Private Const FORMAT_MESSAGE_MAX_WIDTH_MASK = &HFF
Private Const FORMAT_MESSAGE_TEXT_LEN = 160
Private Const MAX_PATH = 260
Private Const GWL_HWNDPARENT As Long = -8
Private Const GW_OWNER = 4

#If VBA7 Then
    Private Declare PtrSafe Function GetWindowText Lib "user32.dll" Alias "GetWindowTextA" (ByVal hwnd As Long, ByVal lpString As String, ByVal cch As Long) As Long
    Private Declare PtrSafe Function GetClassName Lib "user32" Alias "GetClassNameA" (ByVal hwnd As Long, ByVal lpClassName As String, ByVal nMaxCount As Long) As Long
    Private Declare PtrSafe Function GetWindowLong Lib "user32.dll" Alias "GetWindowLongA" (ByVal hwnd As Long, ByVal nIndex As Long) As Long
#Else
    Private Declare Function GetWindowText Lib "user32.dll" Alias "GetWindowTextA" (ByVal hWnd As Long, ByVal lpString As String, ByVal cch As Long) As Long
    Private Declare Function GetClassName Lib "user32" Alias "GetClassNameA" (ByVal hWnd As Long, ByVal lpClassName As String, ByVal nMaxCount As Long) As Long
    Private Declare Function GetWindowLong Lib "user32.dll" Alias "GetWindowLongA" (ByVal hWnd As Long, ByVal nIndex As Long) As Long
#End If

Private VBEditorHWnd As Long
Private ApplicationHWnd As Long
Private ExcelDeskHWnd As Long
Private ActiveWindowHWnd As Long
Private UserFormHWnd As Long
Private WindowsDesktopHWnd As Long
Private Const GA_ROOT As Long = 2
Private Const GA_ROOTOWNER As Long = 3
Private Const GA_PARENT As Long = 1

#If VBA7 Then
    Private Declare PtrSafe Function GetWindow Lib "user32" (ByVal hwnd As Long, ByVal wCmd As Long) As Long
    Private Declare PtrSafe Function FindWindowEx Lib "user32" Alias "FindWindowExA" (ByVal hWnd1 As Long, ByVal hWnd2 As Long, ByVal lpsz1 As String, ByVal lpsz2 As String) As Long
    Private Declare PtrSafe Function GetAncestor Lib "user32.dll" (ByVal hwnd As Long, ByVal gaFlags As Long) As Long
    Private Declare PtrSafe Function GetDesktopWindow Lib "user32" () As Long
    Private Declare PtrSafe Function GetParent Lib "user32.dll" (ByVal hwnd As Long) As Long
    Private Declare PtrSafe Function SetWindowLong Lib "user32" Alias "SetWindowLongA" (ByVal hwnd As Long, ByVal nIndex As Long, ByVal dwNewLong As Long) As Long
#Else
    Private Declare Function GetWindow Lib "user32" (ByVal hWnd As Long, ByVal wCmd As Long) As Long
    Private Declare Function FindWindowEx Lib "user32" Alias "FindWindowExA" (ByVal hWnd1 As Long, ByVal hWnd2 As Long, ByVal lpsz1 As String, ByVal lpsz2 As String) As Long
    Private Declare Function GetAncestor Lib "user32.dll" (ByVal hWnd As Long, ByVal gaFlags As Long) As Long
    Private Declare Function GetDesktopWindow Lib "user32" () As Long
    Private Declare Function GetParent Lib "user32.dll" (ByVal hWnd As Long) As Long
    Private Declare Function SetWindowLong Lib "user32" Alias "SetWindowLongA" (ByVal hWnd As Long, ByVal nIndex As Long, ByVal dwNewLong As Long) As Long
#End If

Private Const C_EXCEL_APP_WINDOWCLASS = "XLMAIN"
Private Const C_EXCEL_DESK_WINDOWCLASS = "XLDESK"
Private Const C_EXCEL_WINDOW_WINDOWCLASS = "EXCEL7"
Private Const USERFORM_WINDOW_CLASS = "ThunderDFrame"
Private Const C_VBA_USERFORM_WINDOWCLASS = "ThunderDFrame"

Rem Window position and more
Private Const SWP_NOMOVE = &H2
Private Const SWP_NOSIZE = &H1
Private Const HWND_TOP = 0
Private Const HWND_BOTTOM = 1
Private Const HWND_TOPMOST = -1
Private Const HWND_NOTOPMOST = -2

#If VBA7 Then
    Private Declare PtrSafe Function SetWindowPos Lib "user32" (ByVal hwnd As LongPtr, ByVal hWndInsertAfter As LongPtr, ByVal x As LongPtr, ByVal y As LongPtr, ByVal cx As LongPtr, ByVal cy As LongPtr, ByVal uFlags As LongPtr) As Long
#Else
    Private Declare Function SetWindowPos Lib "user32" (ByVal hWnd As LongPtr, ByVal hWndInsertAfter As LongPtr, ByVal X As LongPtr, ByVal Y As LongPtr, ByVal cx As LongPtr, ByVal cy As LongPtr, ByVal uFlags As LongPtr) As Long
#End If

Rem ---
#If VBA7 Then
    Private Declare PtrSafe Function SetParent Lib "user32" (ByVal hwndChild As LongPtr, ByVal hWndNewParent As LongPtr) As LongPtr
    Private Declare PtrSafe Function SetForegroundWindow Lib "user32" (ByVal hwnd As LongPtr) As Long
    Private Declare PtrSafe Function FormatMessage Lib "kernel32" Alias "FormatMessageA" (ByVal dwFlags As Long, lpSource As Any, ByVal dwMessageId As Long, ByVal dwLanguageId As Long, ByVal lpBuffer As String, ByVal nSize As Long, Arguments As LongPtr) As Long
#Else
    Private Declare Function SetParent Lib "user32" (ByVal hWndChild As Long, ByVal hWndNewParent As Long) As Long
    Private Declare Function SetForegroundWindow Lib "user32" (ByVal hwnd As Long) As Long
    Private Declare Function FormatMessage Lib "kernel32.dll" Alias "FormatMessageA" (ByVal dwFlags As Long, ByRef lpSource As Any, ByVal dwMessageId As Long, ByVal dwLanguageId As Long, ByVal lpBuffer As String, ByVal nSize As Long, ByRef Arguments As Long) As Long
#End If

Rem Closeby
Private Enum CloseBy
    User = 0
    code = 1
    WindowsOS = 2
    TaskManager = 3
End Enum


Private Const black As Long = &H80000012
Private Const red As Long = &HFF&

Rem other
#If VBA7 Then
    Private Declare PtrSafe Function FindWindow Lib "user32" Alias "FindWindowA" (ByVal lpClassName As String, ByVal lpWindowName As String) As Long
    Private Declare PtrSafe Function FindWindowA Lib "user32" (ByVal lpClassName As String, ByVal lpWindowName As String) As Long
    Private Declare PtrSafe Function GetWindowLongA Lib "user32" (ByVal hwnd As Long, ByVal nIndex As Long) As Long
    Private Declare PtrSafe Function SetWindowLongA Lib "user32" (ByVal hwnd As Long, ByVal nIndex As Long, ByVal dwNewLong As Long) As Long
#Else
    Private Declare Function FindWindow Lib "user32" Alias "FindWindowA" (ByVal lpClassName As String, ByVal lpWindowName As String) As Long
    Private Declare Function FindWindowA Lib "user32" (ByVal lpClassName As String, ByVal lpWindowName As String) As Long
    Private Declare Function GetWindowLongA Lib "user32" (ByVal hWnd As Long, ByVal nIndex As Long) As Long
    Private Declare Function SetWindowLongA Lib "user32" (ByVal hWnd As Long, ByVal nIndex As Long, ByVal dwNewLong As Long) As Long
#End If

Rem userform hwnd
#If Win64 Then
    Private Declare PtrSafe Function IUnknown_GetWindow Lib "shlwapi" Alias "#172" (ByVal pIUnk As IUnknown, ByVal hwnd As LongPtr) As Long
#Else
    Private Declare Function IUnknown_GetWindow Lib "shlwapi" Alias "#172" (ByVal pIUnk As IUnknown, ByVal hwnd As Long) As Long
#End If

Rem ___Resizeable___

Public isResizable As Boolean
#If VBA7 Then
    Private Declare PtrSafe Function SetFocus Lib "user32" (ByVal hwnd As Long) As Long
    Private Declare PtrSafe Function ShowWindow Lib "user32" (ByVal hwnd As Long, ByVal nCmdShow As Long) As Long
#Else
    Private Declare Function SetFocus Lib "user32" (ByVal hwnd As Long) As Long
    Private Declare Function ShowWindow Lib "user32" (ByVal hwnd As Long, ByVal nCmdShow As Long) As Long
#End If
Private mdWidth As Double
Private mdHeight As Double
Private Const WS_THICKFRAME As Long = &H40000
Private Const SW_SHOW As Long = 5



#If VBA7 Then
    Private Declare PtrSafe Function getFrequency Lib "kernel32" Alias "QueryPerformanceFrequency" (cyFrequency As Currency) As Long
    Private Declare PtrSafe Function getTickCount Lib "kernel32" Alias "QueryPerformanceCounter" (cyTickCount As Currency) As Long
#Else
    Private Declare Function getFrequency Lib "kernel32" Alias "QueryPerformanceFrequency" (cyFrequency As Currency) As Long
    Private Declare Function getTickCount Lib "kernel32" Alias "QueryPerformanceCounter" (cyTickCount As Currency) As Long
#End If



Rem get hwnd
#If VBA7 Then
    Private Declare PtrSafe Function WindowFromAccessibleObject Lib "oleacc" (ByVal pacc As IAccessible, phwnd As LongPtr) As Long
#Else
    Private Declare Function WindowFromAccessibleObject Lib "oleacc" (ByVal pacc As IAccessible, phwnd As Long) As Long
#End If

#If VBA7 Then
    Function hwnd() As LongPtr
#Else
    Function hwnd() As Long
#End If
    WindowFromAccessibleObject oForm, hwnd
End Function

Public Sub Transition(ParamArray Elements() As Variant)
Rem By Robert Todaer
'__usage__
'with aUserform
'    .Transition .Effect(Box, "Top", Me.InsideHeight - Box.Height, 1000) _
                , .Effect(box2, "Top", 0, 100) _
                , .Effect(GoButton, "fontsize", 12, 1000) _
                , .Effect(Me, "Top", 20, 2000)
'End with

    If IsArray(Elements(LBound(Elements, 1))) Then
        Dim Temp As Variant
        Temp = Elements(LBound(Elements, 1))
        Elements = Temp
    End If
    Dim Form As MSForms.UserForm
    Set Form = Elements(LBound(Elements, 1))("form")
    MicroTimer True
    Do
        Dim index As Integer
        For index = LBound(Elements, 1) To UBound(Elements, 1)
            IncRementElement Elements(index), MicroTimer
        Next index
        Sleep 40
        Form.Repaint
    Loop Until AllTransitionsComplete(CVar(Elements))
End Sub

Public Function Effect(obj As Object, Property As String, Destination As Double, MilSecs As Double) As Scripting.Dictionary
    Dim Temp As New Scripting.Dictionary
    Set Temp("obj") = obj
    Temp("property") = Property
    Temp("startValue") = CallByName(obj, Property, VbGet)
    Temp("destination") = Destination
    Temp("travel") = Destination - Temp("startValue")
    Temp("milSec") = MilSecs
    Temp("complete") = False
    On Error GoTo Catch:
    Set Temp("form") = obj.Parent
    Set Effect = Temp
    Exit Function
Catch:
    Set Temp("form") = obj
    Resume Next
End Function

Private Function MicroTimer(Optional startTime As Boolean = False) As Double
    Static dTime As Double
    Dim cyTicks1 As Currency
    Dim cyTicks2 As Currency
    Static cyFrequency As Currency
    MicroTimer = 0
    If cyFrequency = 0 Then getFrequency cyFrequency
    getTickCount cyTicks1
    getTickCount cyTicks2
    If cyTicks2 < cyTicks1 Then cyTicks2 = cyTicks1
    If cyFrequency Then MicroTimer = cyTicks2 / cyFrequency
    If startTime = True Then
        dTime = MicroTimer
        MicroTimer = 0
    Else
        MicroTimer = (MicroTimer - dTime) * 1000
    End If
End Function

Private Function AllTransitionsComplete(Elements As Variant) As Boolean
    '@INCLUDE TransitionComplete
    Dim el As Object
    Dim index As Integer
    For index = LBound(Elements, 1) To UBound(Elements, 1)
        Set el = Elements(index)
        If Not TransitionComplete(el) Then
            AllTransitionsComplete = False
            Exit Function
        End If
    Next index
    AllTransitionsComplete = True
End Function

Private Function TransitionComplete(ByVal el As Scripting.Dictionary) As Boolean
    If Math.Round(el("destination")) = Math.Round(CallByName(el("obj"), el("property"), VbGet)) Then
        TransitionComplete = True
    End If
End Function

Private Function IncRementElement(ByVal el As Scripting.Dictionary, CurrentTime As Double) As Boolean
    '@INCLUDE TransitionComplete
    '@INCLUDE easeInAndOut
    Dim IncRementValue As Double
    Dim CurrentValue As Double
    If TransitionComplete(el) Then
        Exit Function
    End If
    Dim o As Object
    Dim p As String
    Dim Value As Double
    Dim d As Double
    IncRementValue = easeInAndOut(CurrentTime, el("startValue"), el("travel"), el("milSec"))
    If el("travel") < 0 Then
        If Math.Round(IncRementValue, 4) < el("destination") Then
            CallByName el("obj"), el("property"), VbLet, el("destination")
        Else
            CallByName el("obj"), el("property"), VbLet, IncRementValue
        End If
    Else
        If Math.Round(IncRementValue, 4) > el("destination") Then
            CallByName el("obj"), el("property"), VbLet, el("destination")
        Else
            CallByName el("obj"), el("property"), VbLet, IncRementValue
        End If
    End If
End Function

Private Function easeInAndOut(ByVal t As Double, ByVal b As Double, ByVal c As Double, ByVal d As Double) As Double
    d = d / 2
    t = t / d
    If (t < 1) Then
        easeInAndOut = c / 2 * t * t * t + b
    Else
        t = t - 2
        easeInAndOut = c / 2 * (t * t * t + 2) + b
    End If
End Function

Public Sub Resizable()
    Dim hWndForm As Long, iStyle As Long
    If val(Application.Version) < 9 Then
        hWndForm = FindWindow("ThunderXFrame", oForm.Caption)
    Else
        hWndForm = FindWindow("ThunderDFrame", oForm.Caption)
    End If
    iStyle = GetWindowLong(hWndForm, GWL_STYLE)
    iStyle = iStyle Or WS_THICKFRAME
    SetWindowLong hWndForm, GWL_STYLE, iStyle
    ShowWindow hWndForm, SW_SHOW
    DrawMenuBar hWndForm
    SetFocus hWndForm
    
    mdWidth = oForm.Width
    mdHeight = oForm.Height
    
    Set DockResize = oForm
    isResizable = True
End Sub

Public Sub DockResize_Layout()
    DockControls
End Sub

Public Sub DockControls()
'
'from STEPHEN BULLEN's USERFORM RESIZER CLASS
'
' _______IN USERFORM_________________
'
' Private Sub UserForm_Resize()
'     auserform.init(me).DockControls
' End Sub
'
' _______Instructions_______
'
' To specify which control(s) to resize (and how), you set the control's .Tag property at design time to
' indicate that the control's top, left, width and height should be adjusted as the form's size changes.
'
' Use the letters t, l, w and h in any order (or not at all) to state that the property should change as the form
' is resized.  Follow the property by a decimal to indicate that the control should change by a percentage of the
' form's change.
'
' For example:
'   hw           Sets the control's height and width to change with the form (e.g. if there's a single list box on the form)
'   tl           Sets the contol's top and left to change in line with the form (e.g. to keep it in the bottom-right corner)
'   w0.5         Sets the control's width to change by 0.5 that of the form's width change
'   w0.5l0.5     Sets the control's width and position to change by 0.5 that of the form's width change

    Dim dWidthAdj As Double, dHeightAdj As Double, sTag As String
    Dim oCtl As MSForms.control
    If mdWidth = 0 Then mdWidth = oForm.Width
    If mdHeight = 0 Then mdHeight = oForm.Height
    dWidthAdj = oForm.Width - mdWidth
    dHeightAdj = oForm.Height - mdHeight
    For Each oCtl In oForm.Controls
'        If TypeName(oCtl) = "ListView" Then Stop
        With oCtl
            sTag = UCase(.Tag)
            If InStr(1, sTag, "L", vbBinaryCompare) Then
                If .Left + dWidthAdj <= 0 Then oForm.Width = mdWidth
            End If
            If InStr(1, sTag, "W", vbBinaryCompare) Then
                If .Width + dWidthAdj <= 0 Then oForm.Width = mdWidth
            End If
            If InStr(1, sTag, "T", vbBinaryCompare) Then
                If .Top + dHeightAdj <= 0 Then oForm.Height = mdHeight
            End If
            If InStr(1, sTag, "H", vbBinaryCompare) Then
                If .Height + dHeightAdj <= 0 Then oForm.Height = mdHeight
            End If
        End With
    Next
    dWidthAdj = oForm.Width - mdWidth
    dHeightAdj = oForm.Height - mdHeight
    For Each oCtl In oForm.Controls
        With oCtl
            sTag = UCase(.Tag)
            If InStr(1, sTag, "L", vbBinaryCompare) Then .Left = .Left + dWidthAdj * ResizeFactor(sTag, "L")
            If InStr(1, sTag, "T", vbBinaryCompare) Then .Top = .Top + dHeightAdj * ResizeFactor(sTag, "T")
            If InStr(1, sTag, "W", vbBinaryCompare) Then .Width = .Width + dWidthAdj * ResizeFactor(sTag, "W")
            If InStr(1, sTag, "H", vbBinaryCompare) Then .Height = .Height + dHeightAdj * ResizeFactor(sTag, "H")

        End With
    Next
    mdWidth = oForm.Width
    mdHeight = oForm.Height
End Sub

Private Function ResizeFactor(sTag As String, sChange As String)
    Dim i As Integer, d As Double
    i = InStr(1, sTag, sChange, vbBinaryCompare)
    If i > 0 Then
        d = val(Mid$(sTag, i + 1))
        If d = 0 Then d = 1
    End If
    ResizeFactor = d
End Function

Public Function Init(Form As Object) As aUserform
    Set oForm = Form
    Set Init = Me
End Function

Private Sub Class_Initialize()
'do something
End Sub

'Function GetUserformHwnd(Form As Object)
'    GetUserformHwnd = FindWindow(vbNullString, Form.Caption)
'End Function

Public Sub TRANSPARENT(Optional color As Variant)
    '@INCLUDE MakeFormBorderless
    Dim formhandle As Long
    Dim bytOpacity As Byte
    formhandle = CLng(FindWindow(vbNullString, oForm.Caption))
    If IsMissing(color) Then color = vbWhite
    bytOpacity = 100
    SetWindowLong formhandle, GWL_EXSTYLE, GetWindowLong(formhandle, GWL_EXSTYLE) Or WS_EX_LAYERED
    oForm.BackColor = color
    SetLayeredWindowAttributes formhandle, color, bytOpacity, LWA_COLORKEY
End Sub

Public Sub Borderless()
    Dim lngWindow As Long
    Dim lFrmHdl As Long
    lFrmHdl = CLng(FindWindow(vbNullString, oForm.Caption))
    lngWindow = GetWindowLong(lFrmHdl, GWL_STYLE)
    lngWindow = lngWindow And (Not WS_CAPTION)
    SetWindowLong lFrmHdl, GWL_STYLE, lngWindow
    lngWindow = GetWindowLong(lFrmHdl, GWL_EXSTYLE)
    lngWindow = lngWindow And Not WS_EX_DLGMODALFRAME
    SetWindowLong lFrmHdl, GWL_EXSTYLE, lngWindow
    DrawMenuBar lFrmHdl
End Sub

Public Sub OnTop()
    Const C_VBA6_USERFORM_CLASSNAME = "ThunderDFrame"
    Dim Ret As Long
    Dim formHWnd As Long
    formHWnd = CLng(FindWindow(C_VBA6_USERFORM_CLASSNAME, oForm.Caption))
    If formHWnd = 0 Then
        Debug.Print Err.LastDllError
    End If
    Ret = SetWindowPos(formHWnd, HWND_TOPMOST, 0, 0, 0, 0, SWP_NOMOVE Or SWP_NOSIZE)
    If Ret = 0 Then
        Debug.Print Err.LastDllError
    End If
End Sub

Sub ParentIsVBE()
    '@INCLUDE DisplayErrorText
    Dim GivenFormCaption As String
        GivenFormCaption = oForm.Caption
    #If VBA7 Then
        Dim VBEWindowPointer As LongPtr
        Dim UserFormWindowPointer As LongPtr
        Dim ReturnOfSetParentAPI As LongPtr
    #Else
        Dim VBEWindowPointer As Long
        Dim UserFormWindowPointer As Long
        Dim ReturnOfSetParentAPI As Long
    #End If
    Dim ErrorNumber As Long
    VBEWindowPointer = Application.VBE.MainWindow.hwnd
    UserFormWindowPointer = FindWindow(lpClassName:=USERFORM_WINDOW_CLASS, lpWindowName:=GivenFormCaption)
    Const ERROR_NUMBER_FOR_SETPARENT_API = 0
    ReturnOfSetParentAPI = SetParent(hwndChild:=UserFormWindowPointer, hWndNewParent:=VBEWindowPointer)
    If ReturnOfSetParentAPI = ERROR_NUMBER_FOR_SETPARENT_API Then
        ErrorNumber = Err.LastDllError
        DisplayErrorText "Error With SetParent", ErrorNumber
    Else
'        Debug.Print GivenFormCaption & " is child of VBE Window."
    End If
    SetForegroundWindow UserFormWindowPointer
    Application.VBE.MainWindow.SetFocus
End Sub

Private Sub DisplayErrorText(Context As String, ErrNum As Long)
    Rem  Displays a standard error message box. For this
    Rem  procedure, ErrNum should be the number returned
    Rem  by the GetLastError API function or the value
    Rem  of Err.LastDllError. It is NOT the number
    Rem  returned by Err.Number.
    '@INCLUDE GetSystemErrorMessageText
    Dim ErrText As String
    ErrText = GetSystemErrorMessageText(ErrNum)
    Debug.Print Context & vbCrLf & _
    "Error Number: " & CStr(ErrNum) & vbCrLf & _
    "Error Text:   " & ErrText, vbOKOnly
End Sub

Private Function GetSystemErrorMessageText(ErrorNumber As Long) As String
    Rem  This function gets the system error message text that corresponds to the error code returned by the
    Rem  GetLastError API function or the Err.LastDllError property. It may be used ONLY for these error codes.
    Rem  These are NOT the error numbers returned by Err.Number (for these errors, use Err.Description to get the description of the message).
    Rem  The error number MUST be the value returned by GetLastError or Err.LastDLLError.
    Rem
    Rem  In general, you should use Err.LastDllError rather than GetLastError because under some circumstances the value of
    Rem  GetLastError will be reset to 0 before the value is returned to VB. Err.LastDllError will always reliably return
    Rem  the last error number raised in a DLL.
    Dim ErrorText As String
    Dim ErrorTextLength As Long
    Dim FormatMessageResult As Long
    Dim LanguageID As Long
    LanguageID = 0&
    ErrorText = String$(FORMAT_MESSAGE_TEXT_LEN, " ")
    ErrorTextLength = Len(ErrorText)
    FormatMessageResult = 0&
    #If VBA7 Then
        Dim FormatMessageAPILastArgument As LongPtr
        FormatMessageAPILastArgument = 0
    #Else
        Dim FormatMessageAPILastArgument As Long
        FormatMessageAPILastArgument = 0
    #End If
    FormatMessageResult = FormatMessage( _
    dwFlags:=FORMAT_MESSAGE_FROM_SYSTEM Or FORMAT_MESSAGE_IGNORE_INSERTS, _
    lpSource:=0&, _
    dwMessageId:=ErrorNumber, _
    dwLanguageId:=0&, _
    lpBuffer:=ErrorText, _
    nSize:=ErrorTextLength, _
    Arguments:=FormatMessageAPILastArgument)
    If FormatMessageResult > 0 Then
        ErrorText = TrimToNull(ErrorText)
        GetSystemErrorMessageText = ErrorText
    Else
        GetSystemErrorMessageText = "NO ERROR DESCRIPTION AVAILABLE"
    End If
End Function

Private Function TrimToNull(Text As String) As String
    Rem  Returns all the text in Text to the left of the vbNullChar
    Dim NullCharIndex As Integer
    NullCharIndex = InStr(1, Text, vbNullChar, vbTextCompare)
    If NullCharIndex > 0 Then
        TrimToNull = Left(Text, NullCharIndex - 1)
    Else
        TrimToNull = Text
    End If
End Function

Public Sub MinimizeButton()
    Dim cap     As String:  cap = oForm.Caption
    Dim hwnd    As Long:    hwnd = FindWindowA(vbNullString, cap)
    Dim exLong  As Long:    exLong = GetWindowLongA(hwnd, -16)
    
    If (exLong And &H20000) = 0 Then SetWindowLongA hwnd, -16, exLong Or &H20000
End Sub
Public Sub MaximizeButton()
    Dim cap     As String:  cap = oForm.Caption
    Dim hwnd    As Long:    hwnd = FindWindowA(vbNullString, cap)
    Dim exLong  As Long:    exLong = GetWindowLongA(hwnd, GWL_STYLE)
    
    If (exLong And WS_MAXIMIZEBOX) = 0 Then
        exLong = exLong Or WS_MAXIMIZEBOX
        SetWindowLongA hwnd, GWL_STYLE, exLong
    End If
End Sub

Public Sub SaveOptions( _
    Optional includeCheckbox As Boolean = True, _
    Optional includeOptionButton As Boolean = True, _
    Optional includeTextBox As Boolean = True, _
    Optional includeListbox As Boolean = True, _
    Optional includeToggleButton As Boolean = True, _
    Optional includeCombobox As Boolean = True)
                
    Dim configFolder As String: configFolder = ThisWorkbook.Path & "\configurations\"
    FoldersCreate configFolder
    Dim iniFile As String: iniFile = configFolder & "UserformSettings.ini"
    Dim key, Value
    Dim c As MSForms.control
    For Each c In oForm.Controls
        If TypeName(c) Like "CheckBox" Then
         If Not includeCheckbox Then GoTo SKIP
        ElseIf TypeName(c) Like "OptionButton" Then
            If Not includeOptionButton Then GoTo SKIP
        ElseIf TypeName(c) Like "TextBox" Then
            If Not includeTextBox Then GoTo SKIP
        ElseIf TypeName(c) = "ListBox" Then
            If Not includeListbox Then GoTo SKIP
        ElseIf TypeName(c) Like "ToggleButton" Then
            If Not includeToggleButton Then GoTo SKIP
        ElseIf TypeName(c) Like "ComboBox" Then
            If Not includeCombobox Then GoTo SKIP
        Else
            GoTo SKIP
        End If
        key = c.Name
        Dim coll As New Collection
        Select Case TypeName(c)
        Case "TextBox", "CheckBox", "OptionButton", "ToggleButton", "ComboBox"
            IniWrite iniFile, oForm.Name, key, c.Value
        Case "ListBox"
            Set coll = aListBox.Init(c).selectedIndexes
            If coll.Count > 0 Then
                IniWrite iniFile, oForm.Name, key, aCollection.Init(coll).ToString(",")
            Else
                IniWrite iniFile, oForm.Name, key, -1
            End If
        End Select
SKIP:
    Next
    
'=== OBSOLETE
'
'    Dim ws As Worksheet
'    Set ws = CreateOrSetSheet(oForm.Name & "_Settings", ThisWorkbook)
'    ws.Cells.Clear
'    Dim coll As New Collection
'    Dim cell As Range
'    Set cell = ws.Cells(1, 1)
'    Dim c As MSForms.control
'    For Each c In oForm.Controls
'        If TypeName(c) Like "CheckBox" Then
'            If Not includeCheckbox Then GoTo SKIP
'        ElseIf TypeName(c) Like "OptionButton" Then
'            If Not includeOptionButton Then GoTo SKIP
'        ElseIf TypeName(c) Like "TextBox" Then
'            If Not includeTextBox Then GoTo SKIP
'        ElseIf TypeName(c) = "ListBox" Then
'            If Not includeListbox Then GoTo SKIP
'        ElseIf TypeName(c) Like "ToggleButton" Then
'            If Not includeToggleButton Then GoTo SKIP
'        ElseIf TypeName(c) Like "ComboBox" Then
'            If Not includeCombobox Then GoTo SKIP
'        Else
'            GoTo SKIP
'        End If
'        cell = c.Name
'        Select Case TypeName(c)
'        Case "TextBox", "CheckBox", "OptionButton", "ToggleButton", "ComboBox"
'            cell.OFFSET(0, 1) = c.Value
'        Case "ListBox"
'            Set coll = aListBox.Init(c).selectedIndexes
'            If coll.count > 0 Then
'                cell.OFFSET(0, 1) = aCollection.Init(coll).ToString(",")
'            Else
'                cell.OFFSET(0, 1) = -1
'            End If
'        End Select
'        Set cell = cell.OFFSET(1, 0)
'SKIP:
'    Next
End Sub

Public Sub LoadOptions(ParamArray ExcludeThese() As Variant)
'ExcludeThese:="ListBox1","TextBox1"     '<--control names
    Dim configFolder As String: configFolder = ThisWorkbook.Path & "\configurations\"
    FoldersCreate configFolder
    Dim iniFile As String: iniFile = configFolder & "UserformSettings.ini"
    If Not FileExists(iniFile) Then TxtOverwrite iniFile, ""
    Dim key, Value
    Dim c As MSForms.control
    Dim keyArray: keyArray = IniSectionKeys(iniFile, oForm.Name)
    If Not ArrayAllocated(keyArray) Then Exit Sub
    For Each key In keyArray
    On Error Resume Next
        Set c = oForm.Controls(key)
        Value = IniReadKey(iniFile, oForm.Name, key)
        If Not TypeName(c) = "Nothing " Then
            If Not ArrayContains(key, ExcludeThese) Then
                Select Case TypeName(c)
                Case "TextBox", "CheckBox", "OptionButton", "ToggleButton", "ComboBox"
                    c.Value = Value
                Case "ListBox"
                    If InStr(1, Value, ",") > 0 Then
                        aListBox.Init(c).SelectItems Split(Value, ","), True
                    Else
                        c.Selected(CInt(Value)) = True
                    End If
                End Select
            End If
        End If
    Next

'=== OBSOLETE
'
'    Dim ws As Worksheet
'    Set ws = CreateOrSetSheet(oForm.Name & "_Settings", ThisWorkbook)
'    If ws.Range("A1") = "" Then Exit Sub
'    Dim cell As Range
'    Set cell = ws.Cells(1, 1)
'    Dim c As MSForms.control
'    Dim v
'    On Error Resume Next
'    Do While cell <> ""
'        Set c = oForm.Controls(cell.TEXT)
'        If Not TypeName(c) = "Nothing " Then
'            If Not ArrayContains(cell, ExcludeThese) Then
'                Select Case TypeName(c)
'                Case "TextBox", "CheckBox", "OptionButton", "ToggleButton", "ComboBox"
'                    c.Value = cell.OFFSET(0, 1)
'                Case "ListBox"
'                    If InStr(1, cell.OFFSET(0, 1), ",") > 0 Then
'                        aListBox.Init(c).SelectItems Split(cell.OFFSET(0, 1), ","), True
'                    Else
'                        c.Selected(CInt(cell.OFFSET(0, 1))) = True
'                    End If
'                End Select
'            End If
'        End If
'        Set cell = cell.OFFSET(1, 0)
'    Loop
End Sub

Public Sub SavePosition()
    Dim configFolder As String: configFolder = ThisWorkbook.Path & "\configurations\"
    FoldersCreate configFolder
    Dim iniFile As String: iniFile = configFolder & "UserformSettings.ini"
    IniWrite iniFile, oForm.Name, "StartupTop", oForm.Top
    IniWrite iniFile, oForm.Name, "StartupLeft", oForm.Left
        
'=== OBSOLETE
'    SaveSetting "My Settings Folder", oForm.Name, "Left Position", oForm.Left
'    SaveSetting "My Settings Folder", oForm.Name, "Top Position", oForm.Top
End Sub

Public Sub LoadPosition()
    Dim configFolder As String: configFolder = ThisWorkbook.Path & "\configurations\"
    FoldersCreate configFolder
    Dim iniFile As String: iniFile = configFolder & "UserformSettings.ini"
    If Not FileExists(iniFile) Then TxtOverwrite iniFile, ""
    Dim myLeft, myTop
    myLeft = IniReadKey(iniFile, oForm.Name, "StartupLeft")
    myTop = IniReadKey(iniFile, oForm.Name, "StartupTop")
    If myLeft = "" Or myTop = "" Then
        oForm.startupposition = 1
    Else
        oForm.startupposition = 0
        oForm.Top = myTop
        oForm.Left = myLeft
    End If
    
'Setting         Value   Description
'Manual          0       No initial setting specified.
'CenterOwner     1       Center on the item to which the UserForm belongs.
'CenterScreen    2       Center on the whole screen.
'WindowsDefault  3       Position in upper-left corner of screen.

'=== OBSOLETE
'    If GetSetting("My Settings Folder", oForm.Name, "Left Position") = "" _
'        And GetSetting("My Settings Folder", oForm.Name, "Top Position") = "" Then
'        oForm.startupposition = 1
'    Else
'        oForm.Left = GetSetting("My Settings Folder", oForm.Name, "Left Position")
'        oForm.Top = GetSetting("My Settings Folder", oForm.Name, "Top Position")
'    End If
End Sub

Public Sub ResizeToFitControls(Optional marginRight = 0, Optional marginBottom = 0)
    oForm.Width = 0
    oForm.Height = 0
    Dim ctr As MSForms.control
    Dim myWidth:    myWidth = 0 'oForm.InsideWidth
    Dim myHeight:   myHeight = 0 'oForm.InsideHeight
    For Each ctr In oForm.Controls
        If ctr.Parent.Name = oForm.Name Then
            If ctr.Visible = True Then
                If ctr.Left + ctr.Width > myWidth Then myWidth = ctr.Left + ctr.Width
                If ctr.Top + ctr.Height > myHeight Then myHeight = ctr.Top + ctr.Height
            End If
        End If
    Next
    oForm.Width = myWidth + (oForm.Width - oForm.InsideWidth) + marginRight
    oForm.Height = myHeight + (oForm.Height - oForm.InsideHeight) + marginBottom
End Sub


Public Sub MouseOnControl(Ctrl As Object)
'modified to take into account if ctrl is inside frame
    Dim p As tCursor
    Dim lngHwnd As Long
    lngHwnd = CLng(FindWindow(vbNullString, oForm.Caption))
    
    Dim difX As Long, difY As Long, par As Object
    Set par = Ctrl
    Do While par.Parent.Name <> oForm.Name
        difX = difX + par.Parent.Left
        difY = difY + par.Parent.Top
        Set par = par.Parent
    Loop
    p.Left = (difX + Ctrl.Left + (Ctrl.Width \ 2)) / PointsPerPixelX
    p.Top = (difY + Ctrl.Top + (Ctrl.Height \ 2)) / PointsPerPixelY
    ClientToScreen lngHwnd, p
    SetCursorPos p.Left, p.Top
End Sub

Private Function PointsPerPixelX() As Double
    Dim hDC As Long
    hDC = GetDC(0)
    PointsPerPixelX = 72 / GetDeviceCaps(hDC, LOGPIXELSX)
    ReleaseDC 0, hDC
End Function

Private Function PointsPerPixelY() As Double
    Dim hDC As Long
    hDC = GetDC(0)
    PointsPerPixelY = 72 / GetDeviceCaps(hDC, LOGPIXELSY)
    ReleaseDC 0, hDC
End Function

Private Function WhereIsTheMouseAt() As tCursor
    Dim mPos As tCursor
    GetCursorPos mPos
    WhereIsTheMouseAt = mPos
End Function

Private Function convertMouseToForm() As tCursor
    Dim mPos As tCursor
    mPos = WhereIsTheMouseAt
    mPos.Left = PointsPerPixelY * mPos.Left
    mPos.Top = PointsPerPixelX * mPos.Top
    convertMouseToForm = mPos
End Function

Public Sub ShowAtCursor()
    oForm.Left = convertMouseToForm.Left
    oForm.Top = convertMouseToForm.Top
End Sub

'aCollection	Class


'* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
'* Class      : aCollection
'* Author     : Anastasiou Alex
'* Contacts   : AnastasiouAlex@gmail.com
'*
'* BLOG       : https://alexofrhodes.github.io/
'* GITHUB     : https://github.com/alexofrhodes/
'* YOUTUBE    : https://www.youtube.com/channel/UC5QH3fn1zjx0aUjRER_rOjg
'* VK         : https://vk.com/video/playlist/735281600_1
'*
'* Modified   : Date and Time       Author              Description
'* Created    : 04-05-2023 12:17    Alex
'* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *

Option Explicit

Public RestrictionTypename As String
Private myCollection As Collection

Public Property Get Items() As Collection
    Set Items = myCollection
End Property

Public Function Init(coll As Collection) As aCollection
    Select Case RestrictionTypename
    Case ""
        Set myCollection = coll
 
    Case Else
        Set myCollection = New Collection
        Dim element
        For Each element In coll
            Add element
        Next
    End Select
    Set Init = Me
End Function

Public Property Get item(NameOrNumber As Variant) As Variant
  Set item = myCollection(IndexOf(NameOrNumber))
End Property

Public Function IndexOf(ByVal item As Variant, _
                        Optional ByVal StartIndex As Long = 1) As Long
    Dim collindex As Long
    Dim collitemtype As Integer
    Dim itemtype As Integer

    itemtype = VarType(item)
    For collindex = StartIndex To myCollection.Count
        collitemtype = VarType(myCollection(collindex))
        If collitemtype = itemtype Then
            Select Case collitemtype
                Case 0 To 1: IndexOf = collindex: Exit Function
                Case 2 To 8, 11, 14, 17: If myCollection(collindex) = item Then IndexOf = collindex: Exit Function
                Case 9: If myCollection(collindex) Is item Then IndexOf = collindex: Exit Function
                Case Else
                    Debug.Print "Unsupported type for CollectionIndexOf."
                    Debug.Assert False
            End Select
        End If
    Next
    IndexOf = 0
End Function

Public Sub RestrictionSet(ByVal Value As String)
    RestrictionTypename = Value
End Sub

Public Sub RestrictionRemove()
    RestrictionTypename = ""
End Sub

Public Sub Add(NewItem As Variant, Optional Description As String)
    Err.Clear
    Select Case RestrictionTypename
    Case ""
        myCollection.Add NewItem
    Case Else
        If TypeName(NewItem) <> RestrictionTypename Then
            MsgBox "TypeName does not match restriction: " & RestrictionTypename
        Else
            On Error Resume Next
            myCollection.Add NewItem, Description
            On Error GoTo 0
            If Err.Number <> 0 Then Debug.Print "Item " & Description & " already contained"
        End If
    End Select
End Sub

Public Property Get Count() As Long
    Count = myCollection.Count
End Property

Public Sub Remove(NameOrNumber As Variant)
  myCollection.Remove NameOrNumber
End Sub

Public Function ToString(delim As String) As String
    Dim element
    Dim out As String
    For Each element In myCollection
        out = IIf(out = "", element, out & delim & element)
    Next
    ToString = out
End Function

Public Function Distinct() As aCollection
    Dim result As Collection: Set result = New Collection
    Dim eachItem As Variant
    On Error Resume Next
    For Each eachItem In myCollection
        result.Add eachItem, CStr(eachItem)
    Next
    On Error GoTo 0
    Set myCollection = result
    Set Distinct = Me
End Function

Public Function Unique() As aCollection
    '-----------------------------------------------------------------------------------------------------------
    ' CollectionUnique     - Returns a collection of unique values from a full collection
    '                                   - In : myCollection As Collection
    '                                   - Out: Result of values, or error
    '                                   - Last Updated: 8/7/11 by AJS
    '-----------------------------------------------------------------------------------------------------------
    Dim result As New Collection
    Dim eachItem As Variant, eachUnique As Variant
    Dim MatchFound As Boolean
'    On Error GoTo IsError:
        For Each eachItem In myCollection
            MatchFound = False
            For Each eachUnique In result
                If eachItem = eachUnique Then
                    MatchFound = True
                    Exit For
                End If
            Next
            If MatchFound = False Then result.Add eachItem
        Next
        Set myCollection = result
        Set Unique = Me
'    Exit Function
'IsError:
'    Unique = CVErr(xlErrNA)
'    Debug.Print "Error in aCollection.Unique: " & Err.Number & ": " & Err.Description
End Function

Public Function Sort() As aCollection
    Dim iCounter As Integer
    Dim iCounter2 As Integer
    Dim Temp As Variant
    For iCounter = 1 To myCollection.Count - 1
        For iCounter2 = iCounter + 1 To myCollection.Count
            If myCollection(iCounter) > myCollection(iCounter2) Then
                Temp = myCollection(iCounter2)
                myCollection.Remove iCounter2
                myCollection.Add Temp, , iCounter
            End If
        Next iCounter2
    Next iCounter
    Set Sort = Me
End Function

Public Function Contains( _
                        Optional key As Variant, _
                        Optional item As Variant) As Boolean
    Dim strKey As String
    Dim var As Variant
    If Not IsMissing(key) Then
        strKey = CStr(key)
        On Error Resume Next
        Contains = True
        var = myCollection(strKey)
        If Err.Number = 91 Then GoTo CheckForObject
        If Err.Number = 5 Then GoTo NotFound
        On Error GoTo 0
        Exit Function
CheckForObject:
        If IsObject(myCollection(strKey)) Then
            Contains = True
            On Error GoTo 0
            Exit Function
        End If
NotFound:
        Contains = False
        On Error GoTo 0
        Exit Function
    ElseIf Not IsMissing(item) Then
        Contains = False
        For Each var In myCollection
            If var = item Then
                Contains = True
                Exit Function
            End If
        Next var
    Else
        Contains = False
    End If
End Function

Function ToArray() As Variant
    Dim a() As Variant: ReDim a(0 To myCollection.Count - 1)
    Dim i As Long
    For i = 1 To myCollection.Count
        a(i - 1) = myCollection.item(i)
    Next
    ToArray = a
End Function

Public Function Reverse() As aCollection
    Dim Output As New Collection
    Dim i As Long
    For i = myCollection.Count To 1 Step -1
        Output.Add myCollection.item(i)
    Next i
    Set myCollection = Output
    Set Reverse = Me
End Function

'------------------------------------------------------------------------'
'Function Mid : returns subset of a collection                       '
'  Similar to Mid$() on strings.                                         '
'See also: FromToColl, LeftColl, RightColl, ButLastColl                  '
'------------------------------------------------------------------------'
Public Function Mid(ByVal Start As Long, Optional ByVal Length As Variant) As aCollection
    Dim result As New Collection
    Dim Count As Long
    Dim c As Long

    Count = myCollection.Count
    If Start < 1 Then Error 5
    If Start > Count Then
        Set result = result
    Else
        If IsMissing(Length) Then
            For c = Start To Count
                result.Add myCollection(c)
            Next
        ElseIf (Length >= (Count + 1 - Start)) Then
            For c = Start To Count
                result.Add myCollection(c)
            Next
        ElseIf Length < 0 Then
            Error 5
        Else
            For c = Start To Start + Count - 1
                result.Add myCollection(c)
            Next
        End If
    End If
    Set myCollection = result
    Set Mid = Me
End Function

'------------------------------------------------------------------------'
'Function FromTo : returns subset of a collection                    '
'See also: MidColl, LeftColl, RightColl                                  '
'------------------------------------------------------------------------'
Public Function FromTo(ByVal FromOffset As Long, ByVal ToOffset As Long) As aCollection
    Dim result As New Collection
    Dim c As Long

    If FromOffset > myCollection.Count Then
        Set FromTo = result
        Exit Function
    ElseIf FromOffset < 1 Then
        Error 5
    End If

    If ToOffset > myCollection.Count Then
        ToOffset = myCollection.Count
    ElseIf ToOffset < 1 Then
        Error 5
    End If

    If ToOffset < FromOffset Then
        Set FromTo = result
        Exit Function
    Else
        For c = FromOffset To ToOffset
            result.Add myCollection(c)
        Next
    End If

    Set myCollection = result
    Set FromTo = Me
End Function

'------------------------------------------------------------------------'
'Function LeftColl : returns left-most elements of a collection          '
'  Similar to Left$() on strings.                                        '
'See also: MidColl, RightColl, ButLastColl                               '
'------------------------------------------------------------------------'
Public Function Left(ByVal Length As Long) As aCollection
    Dim result As New Collection
    Dim c As Long

    If Length > myCollection.Count Then Length = myCollection.Count
    For c = 1 To Length
        result.Add myCollection(c)
    Next
    Set myCollection = result
    Set Left = Me
End Function

'------------------------------------------------------------------------'
'Function RightColl : returns right-most elements of a collection        '
'  Similar to Right$() on strings.                                       '
'See also: MidColl, LeftColl                                             '
'------------------------------------------------------------------------'
Public Function Right(ByVal Length As Long) As aCollection
    Dim result As New Collection
    Dim c As Long

    If Length > myCollection.Count Then Length = myCollection.Count
    For c = myCollection.Count - Length + 1 To myCollection.Count
        result.Add myCollection(c)
    Next
    Set myCollection = result
    Set Right = Me

End Function

Function Filter( _
            match As String, _
            IgnoreCase As Boolean, _
            Include As Boolean) As aCollection
    Dim result As New Collection
    If IgnoreCase Then match = UCase(match)
    Dim element
    For Each element In myCollection
        If IgnoreCase Then
            If Include Then
                If UCase(element) Like match Then result.Add element
            Else
                If Not UCase(element) Like match Then result.Add element
            End If
        Else
            If Include Then
                If UCase(element) Like match Then result.Add element
            Else
                If UCase(element) Like match Then result.Add element
            End If
        End If
    Next
    Set myCollection = result
    Set Filter = Me
End Function


Public Function CollectionsToArray2D(collections As Collection) As Variant
    If collections.Count = 0 Then Exit Function
    Dim columnCount As Long:    columnCount = collections.Count
    Dim rowCount As Long:       rowCount = collections.item(1).Count
    Dim var As Variant
    ReDim var(1 To rowCount, 1 To columnCount)
    Dim cols As Long
    Dim rows As Long
    For rows = 1 To rowCount
        For cols = 1 To collections.Count
            var(rows, cols) = collections(cols).item(rows)
        Next cols
    Next rows
    CollectionsToArray2D = var
End Function

'F_Settings_INI	Module


'* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
'* Module     : F_Settings_INI
'* Purpose    :
'* Copyright  :
'*
'* Author     : Anastasiou Alex
'* Contacts   : AnastasiouAlex@gmail.com
'*
'* BLOG       : https://alexofrhodes.github.io/
'* GITHUB     : https://github.com/alexofrhodes/
'* YOUTUBE    : https://www.youtube.com/channel/UC5QH3fn1zjx0aUjRER_rOjg
'* VK         : https://vk.com/video/playlist/735281600_1
'*
'* Modified   : Date and Time       Author              Description
'* Created    : 30-06-2023 14:11    Alex
'* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *

'____API METHOD______

#If VBA7 Then
    Public Declare PtrSafe Function GetPrivateProfileString Lib "kernel32" Alias "GetPrivateProfileStringA" (ByVal lpApplicationName As String, ByVal lpKeyName As Any, ByVal lpDefault As String, ByVal lpReturnedString As String, ByVal nSize As Long, ByVal lpFileName As String) As Long
    Public Declare PtrSafe Function WritePrivateProfileString Lib "kernel32" Alias "WritePrivateProfileStringA" (ByVal lpApplicationName As String, ByVal lpKeyName As Any, ByVal lpString As Any, ByVal lpFileName As String) As Long
    Public Declare PtrSafe Function GetPrivateProfileSection Lib "kernel32" Alias "GetPrivateProfileSectionA" (ByVal lpAppName As String, ByVal lpReturnedString As String, ByVal nSize As Long, ByVal lpFileName As String) As Long
#Else
    public declare Function GetPrivateProfileString Lib "kernel32" Alias "GetPrivateProfileStringA" (ByVal lpApplicationName As String, ByVal lpKeyName As Any, ByVal lpDefault As String, ByVal lpReturnedString As String, ByVal nSize As Long, ByVal lpFileName As String) As Long
    public declare Function WritePrivateProfileString Lib "kernel32" Alias "WritePrivateProfileStringA" (ByVal lpApplicationName As String, ByVal lpKeyName As Any, ByVal lpString As Any, ByVal lpFileName As String) As Long
    public declare Function GetPrivateProfileSection Lib "kernel32" Alias "GetPrivateProfileSectionA" (ByVal lpAppName As String, ByVal lpReturnedString As String, ByVal nSize As Long, ByVal lpFileName As String) As Long
#End If

Sub TestINI()
'@AssignedModule F_Settings_INI
'@INCLUDE PROCEDURE FollowLink
'@INCLUDE PROCEDURE IniSections
'@INCLUDE PROCEDURE IniReadSection
'@INCLUDE PROCEDURE IniSectionKeys
'@INCLUDE PROCEDURE IniReadKey
'@INCLUDE PROCEDURE IniWrite
'@INCLUDE PROCEDURE dp
    Dim filepath As String: filepath = ThisWorkbook.Path & "\test.INI"
    FollowLink ThisWorkbook.Path
    
    IniWrite filepath, "Settings1", "KeyName1", "Value1"
    IniWrite filepath, "Settings1", "KeyName2", "2"
    IniWrite filepath, "Settings1", "KeyName3", "3"     'SEE THE FILE
    Stop
    IniWrite filepath, "Settings1", "KeyName1", "Updated Value" 'SEE THE FILE
    Stop
    
    Dim i  As Long
    For i = 1 To 5
        IniWrite filepath, "Settings" & i, "KeyName" & i, i
    Next
    'SEE THE FILE
    Stop
    dp String(20, "~") & " Printing sections of " & filepath
    dp IniSections(filepath)
    Stop
    dp String(20, "~") & " Printing keys of section Settings1"
    dp IniSectionKeys(filepath, "Settings1")
    Stop
    dp String(20, "~") & " Printing all lines of section Settings1"
    dp IniReadSection(filepath, "Settings1")
    Stop
    dp String(20, "~") & " Printing value of Section: Settings1, Keyname: Keyname1"
    dp IniReadKey(filepath, "Settings1", "KeyName1")
    
End Sub

Public Function IniSections(iniFile As String) As Variant
'---sample file content---
'[settings1]
'    string1 = aaa
'    string2 = bbb
'[settings2]
'    string1 = ccc
'    string2 = ddd
'-------------------------
'@AssignedModule F_Settings_INI
'@INCLUDE PROCEDURE TxtRead
    IniSections = Split(Replace(Replace(Join(Filter(Split(Replace(TxtRead(iniFile), vbLf, vbNewLine), vbNewLine), "[", True), vbNewLine), "[", ""), "]", ""), vbNewLine)
'------Result------------------
'Array("settings1","settings2")
End Function

Public Function IniReadSection(FileName As String, Section As String) As Variant
'@AssignedModule F_Settings_INI
'@INCLUDE PROCEDURE ArrayRemoveEmptyElements
'@INCLUDE DECLARATION GetPrivateProfileSection
    Dim retVal As String * 255
    Dim v As Long:      v = GetPrivateProfileSection(Section, retVal, 255, FileName)
    Dim s As String:    s = Left(retVal, v + 0)
    Dim VL As Variant:  VL = Split(s, Chr$(0))
    VL = ArrayRemoveEmptyElements(VL)
    IniReadSection = VL
'-----result for reading "settings1"-----
'Array("string1=aaa","string2=bbb")
End Function

Public Function ArrayRemoveEmptyElements(varArray As Variant) As Variant
'@AssignedModule F_Settings_INI
    Dim tempArray() As Variant
    Dim OldIndex As Integer
    Dim NewIndex As Integer
    ReDim tempArray(LBound(varArray) To UBound(varArray))
    For OldIndex = LBound(varArray) To UBound(varArray)
        If Not Trim(varArray(OldIndex) & " ") = "" Then
            tempArray(NewIndex) = varArray(OldIndex)
            NewIndex = NewIndex + 1
        End If
    Next OldIndex
    ReDim Preserve tempArray(LBound(varArray) To NewIndex - 1)
    ArrayRemoveEmptyElements = tempArray
    varArray = tempArray
End Function

Public Function IniSectionKeys(FileName As String, Section As String) As Variant
'@AssignedModule F_Settings_INI
'@INCLUDE PROCEDURE IniReadSection
'@INCLUDE PROCEDURE IniSectionExists
    Dim arr() As Variant
    If Not IniSectionExists(FileName, Section) Then
        IniSectionKeys = arr
        Exit Function
    End If
    arr = IniReadSection(FileName, Section)
    Dim out As Variant
    ReDim out(UBound(arr))
    Dim i As Long
    For i = LBound(arr) To UBound(arr)
        out(i) = Trim(Split(arr(i), "=")(0))
    Next i
    IniSectionKeys = out
'-----result for reading "settings1"-----
'string1
'string2
End Function

Public Function IniReadKey(IniFileName As String, ByVal Sect As String, ByVal Keyname As String) As String
'@AssignedModule F_Settings_INI
'@INCLUDE DECLARATION GetPrivateProfileString
    Dim Worked As Long
    Dim RetStr As String * 128
    Dim StrSize As Long
    Dim iNoOfCharInIni As Long: iNoOfCharInIni = 0
    Dim sIniString As String: sIniString = ""
    If Sect = "" Or Keyname = "" Then
        MsgBox "Section Or Key To Read Not Specified !!!", vbExclamation, "INI"
    Else
        Dim sProfileString As String: sProfileString = ""
        RetStr = Space(128)
        StrSize = Len(RetStr)
        Worked = GetPrivateProfileString(Sect, Keyname, "", RetStr, StrSize, IniFileName)
        If Worked Then
            iNoOfCharInIni = Worked
            sIniString = Left$(RetStr, Worked)
        End If
    End If
    IniReadKey = sIniString
'---- result for reading "settings1", "string1" ----
'aaa
End Function

Public Sub IniWrite(IniFileName As String, ByVal Sect As String, ByVal Keyname As String, ByVal Wstr As String)
'This macro also creates the file & section & key if they doesn't exist
'@AssignedModule F_Settings_INI
'@INCLUDE DECLARATION WritePrivateProfileString

    Dim Worked As Long
    Dim iNoOfCharInIni As Long

    iNoOfCharInIni = 0
    Dim sIniString As String: sIniString = ""
    If Sect = "" Or Keyname = "" Then
        MsgBox "Section Or Key To Write Not Specified !!!", vbExclamation, "INI"
    Else
        Worked = WritePrivateProfileString(Sect, Keyname, Wstr, IniFileName)
        If Worked Then
            iNoOfCharInIni = Worked
            sIniString = Wstr
        End If
    End If
    
'---- result for writing "settings1", "string1", "newval" ----
'[settings1]
'    string1 = newval
'    string2 = bbb
'[settings2]
'    string1 = ccc
'    string2 = ddd

'---- result for writing "settings1", "string3", "newkey" ----
'[settings1]
'    string1 = newval
'    string2 = bbb
'    string3 = newkey
'[settings2]
'    string1 = ccc
'    string2 = ddd
End Sub

'___NO API METHOD______
'
Public Sub TestReadKey()
    Debug.Print "INI File: " & ThisWorkbook.Path & "\MyIniFile.ini" & vbCrLf & _
           "Section: SETTINGS" & vbCrLf & _
           "Section Exist: " & IniSectionExists(ThisWorkbook.Path & "\MyIniFile.ini", "SETTINGS") & vbCrLf & _
           "Key: License" & vbCrLf & _
           "Key Exist: " & IniKeyExists(ThisWorkbook.Path & "\MyIniFile.ini", "SETTINGS", "License") & vbCrLf & _
           "Key Value: " & Ini_ReadKeyVal(ThisWorkbook.Path & "\MyIniFile.ini", "SETTINGS", "License")
    'You can validate the value by checking the bSectionExists and bKeyExists variable to ensure they were actually found in the ini file
'@AssignedModule F_Settings_INI
'@INCLUDE PROCEDURE IniSectionExists
'@INCLUDE PROCEDURE IniKeyExists
End Sub

Public Function IniSectionExists(iniFile As String, Section As String) As Boolean
    'Alex
'@AssignedModule F_Settings_INI
'@INCLUDE PROCEDURE TxtRead
    IniSectionExists = InStr(1, TxtRead(iniFile), "[" & Section & "]") > 0
End Function

Public Function IniKeyExists(iniFile As String, Section As String, key As String) As Boolean
    'Alex
'@AssignedModule F_Settings_INI
    IniKeyExists = (Ini_ReadKeyVal(iniFile, Section, key) <> "")
End Function

Public Sub TestWriteKey()
'@AssignedModule F_Settings_INI
    If Ini_WriteKeyVal(ThisWorkbook.Path & "\MyIniFile.ini", "SETTINGS", "License", "JBXR-HHTY-LKIP-HJNB-GGGT") = True Then
        MsgBox "The key was written"
    Else
        MsgBox "An error occurred!"
    End If
End Sub

'---------------------------------------------------------------------------------------
' Procedure : Ini_ReadKeyVal
' Author    : Daniel Pineault, CARDA Consultants Inc.
' Website   : http://www.cardaconsultants.com
' Purpose   : Read an Ini file's Key
' Copyright : The following may be altered and reused as you wish so long as the
'             copyright notice is left unchanged (including Author, Website and
'             Copyright).  It may not be sold/resold or reposted on other sites (links
'             back to this site are allowed).
' Req'd Refs: Uses Late Binding, so none required
'             No APIs either! 100% VBA
'
' Input Variables:
' ~~~~~~~~~~~~~~~~
' sIniFile  : Full path and filename of the ini file to read
' sSection  : Ini Section to search for the Key to read the Key from
' sKey      : Name of the Key to read the value of
'
' Usage:
' ~~~~~~
' ? Ini_Read(Application.CurrentProject.Path & "\MyIniFile.ini", "LINKED TABLES", "Path")
'
' Revision History:
' Rev       Date(yyyy/mm/dd)        Description
' **************************************************************************************
' 1         2012-08-09              Initial Release
'---------------------------------------------------------------------------------------
Public Function Ini_ReadKeyVal(ByVal sIniFile As String, _
                        ByVal sSection As String, _
                        ByVal sKey As String) As String
'@AssignedModule F_Settings_INI
'@INCLUDE PROCEDURE FileExists
'@INCLUDE PROCEDURE TxtRead
'@INCLUDE PROCEDURE ArrayTrim
    On Error GoTo Error_Handler
    Dim bSectionExists         As Boolean
    Dim bKeyExists             As Boolean
    Dim sIniFileContent       As String
    Dim aIniLines()           As String
    Dim sLine                 As String
    Dim i                     As Long

    sIniFileContent = ""
    bSectionExists = False
    bKeyExists = False

    'Validate that the file actually exists
    If FileExists(sIniFile) = False Then
        MsgBox "The specified ini file: " & vbCrLf & vbCrLf & _
               sIniFile & vbCrLf & vbCrLf & _
               "could not be found.", vbCritical + vbOKOnly, "File not found"
        GoTo Error_Handler_Exit
    End If

    sIniFileContent = TxtRead(sIniFile)    'Read the file into memory
    aIniLines = Split(sIniFileContent, vbLf)
    For i = 0 To UBound(aIniLines)
        sLine = Trim(aIniLines(i))
        sLine = VBA.Replace(sLine, vbTab, vbNullString)
        If InStr(1, sLine, "=") > 0 Then sLine = Join(ArrayTrim(Split(sLine, "=")), "=") '<- Alex added this
        If bSectionExists = True And Left(sLine, 1) = "[" And Right(sLine, 1) = "]" Then
            Exit For    'Start of a new section
        End If
        If sLine = "[" & sSection & "]" Then
            bSectionExists = True
        End If
        If bSectionExists = True Then
            If sLine Like sKey & "=*" Then
                bKeyExists = True
                Ini_ReadKeyVal = Mid(sLine, InStr(sLine, "=") + 1)
            End If
        End If
    Next i

Error_Handler_Exit:
    On Error Resume Next
    Exit Function

Error_Handler:
    'Err.Number = 75 'File does not exist, Permission issues to write is denied,
    MsgBox "The following error has occurred" & vbCrLf & vbCrLf & _
           "Error Number: " & Err.Number & vbCrLf & _
           "Error Source: Ini_ReadKeyVal" & vbCrLf & _
           "Error Description: " & Err.Description & _
           Switch(Erl = 0, "", Erl <> 0, vbCrLf & "Line No: " & Erl) _
           , vbOKOnly + vbCritical, "An Error has Occurred!"
    Resume Error_Handler_Exit
End Function

'---------------------------------------------------------------------------------------
' Procedure : Ini_WriteKeyVal
' Author    : Daniel Pineault, CARDA Consultants Inc.
' Website   : http://www.cardaconsultants.com
' Purpose   : Writes a Key value to the specified Ini file's Section
'               If the file does not exist, it will be created
'               If the Section does not exist, it will be appended to the existing content
'               If the Key does not exist, it will be appended to the existing Section content
' Copyright : The following may be altered and reused as you wish so long as the
'             copyright notice is left unchanged (including Author, Website and
'             Copyright).  It may not be sold/resold or reposted on other sites (links
'             back to this site are allowed).
' Req'd Refs: Uses Late Binding, so none required
'             No APIs either! 100% VBA
'
' Input Variables:
' ~~~~~~~~~~~~~~~~
' sIniFile  : Full path and filename of the ini file to edit
' sSection  : Ini Section to search for the Key to edit
' sKey      : Name of the Key to edit
' sValue    : Value to associate to the Key
'
' Usage:
' ~~~~~~
' Call Ini_WriteKeyVal(Application.CurrentProject.Path & "\MyIniFile.ini", "LINKED TABLES", "Paths", "D:\")
'
' Revision History:
' Rev       Date(yyyy/mm/dd)        Description
' **************************************************************************************
' 1         2012-08-09              Initial Release
' 2         2020-01-27              Fix to address issue flagged by users
'---------------------------------------------------------------------------------------
Public Function Ini_WriteKeyVal(ByVal sIniFile As String, _
                         ByVal sSection As String, _
                         ByVal sKey As String, _
                         ByVal sValue As String) As Boolean
'@AssignedModule F_Settings_INI
'@INCLUDE PROCEDURE FileExists
'@INCLUDE PROCEDURE TxtRead
'@INCLUDE PROCEDURE TxtOverwrite
    On Error GoTo Error_Handler
    Dim bSectionExists         As Boolean
    Dim bKeyExists             As Boolean
    Dim sIniFileContent       As String
    Dim aIniLines()           As String
    Dim sLine                 As String
    Dim sNewLine              As String
    Dim i                     As Long
    Dim bFileExist            As Boolean
    Dim bInSection            As Boolean
    Dim bKeyAdded             As Boolean

    sIniFileContent = ""
    bSectionExists = False
    bKeyExists = False

    'Validate that the file actually exists
    If FileExists(sIniFile) = False Then
        GoTo SectionDoesNotExist
    End If
    bFileExist = True

    sIniFileContent = TxtRead(sIniFile)    'Read the file into memory
    aIniLines = Split(sIniFileContent, vbLf)    'Break the content into individual lines
    sIniFileContent = ""    'Reset it
    For i = 0 To UBound(aIniLines)    'Loop through each line
        sNewLine = ""
        sLine = Trim(aIniLines(i))
        If sLine = "[" & sSection & "]" Then
            bSectionExists = True
            bInSection = True
        End If
        If bInSection = True Then
            If sLine <> "[" & sSection & "]" Then
                If Left(sLine, 1) = "[" And Right(sLine, 1) = "]" Then
                    'Our section exists, but the key wasn't found, so append it
                    sNewLine = sKey & "=" & sValue
                    i = i - 1
                    bInSection = False    ' we're switching section
                    bKeyAdded = True
                End If
            End If
            If Len(sLine) > Len(sKey) Then
                If Left(sLine, Len(sKey) + 1) = sKey & "=" Then
                    sNewLine = sKey & "=" & sValue
                    bKeyExists = True
                    bKeyAdded = True
                End If
            End If
        End If
        If Len(sIniFileContent) > 0 Then sIniFileContent = sIniFileContent & vbCrLf
        If sNewLine = "" Then
            sIniFileContent = sIniFileContent & sLine
        Else
            sIniFileContent = sIniFileContent & sNewLine
        End If
    Next i

SectionDoesNotExist:
    'if not found, add it to the end
    If bSectionExists = False Then
        If Len(sIniFileContent) > 0 Then sIniFileContent = sIniFileContent & vbCrLf
        sIniFileContent = sIniFileContent & "[" & sSection & "]"
    End If
    If bKeyAdded = False Then
        sIniFileContent = sIniFileContent & vbCrLf & sKey & "=" & sValue
    End If

    'Write to the ini file the new content
    Call TxtOverwrite(sIniFile, sIniFileContent)
    Ini_WriteKeyVal = True

Error_Handler_Exit:
    On Error Resume Next
    Exit Function

Error_Handler:
    MsgBox "The following error has occurred" & vbCrLf & vbCrLf & _
           "Error Number: " & Err.Number & vbCrLf & _
           "Error Source: Ini_WriteKeyVal" & vbCrLf & _
           "Error Description: " & Err.Description & _
           Switch(Erl = 0, "", Erl <> 0, vbCrLf & "Line No: " & Erl) _
           , vbOKOnly + vbCritical, "An Error has Occurred!"
    Resume Error_Handler_Exit
End Function

'_____________________________________________________________


'uAuthor	UserForm




'* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
'* Userform   : uAuthor
'* Created    : 06-10-2022 10:34
'* Author     : Anastasiou Alex
'* Contacts   : AnastasiouAlex@gmail.com
'*
'* GITHUB     : https://github.com/AlexOfRhodes
'* YOUTUBE    : https://www.youtube.com/channel/UC5QH3fn1zjx0aUjRER_rOjg
'* VK         : https://vk.com/video/playlist/735281600_1
'* DONATE     : http://paypal.me/alexofrhodes
'* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *

Option Explicit

Const AUTHOR_EMAIL = "anastasioualex@gmail.com"

#If VBA7 Then
    Private Declare PtrSafe Function InternetGetConnectedState Lib "wininet.dll" (ByRef dwFlags As Long, ByVal dwReserved As Long) As Long
    Private Declare PtrSafe Function GetSystemMetrics Lib "user32" (ByVal nIndex As Long) As Long
    
    Private Declare PtrSafe Function SetWindowLong Lib "user32" Alias "SetWindowLongA" (ByVal hwnd As Long, ByVal nIndex As Long, ByVal dwNewLong As Long) As Long
    Private Declare PtrSafe Function FindWindow Lib "user32" Alias "FindWindowA" (ByVal lpClassName As String, ByVal lpWindowName As String) As Long
    Private Declare PtrSafe Function FindWindowA Lib "user32" (ByVal lpClassName As String, ByVal lpWindowName As String) As Long
    Private Declare PtrSafe Function GetWindowLong Lib "user32" Alias "GetWindowLongA" (ByVal hwnd As Long, ByVal nIndex As Long) As Long
    Private Declare PtrSafe Function DrawMenuBar Lib "user32" (ByVal hwnd As Long) As Long
    Private Declare PtrSafe Function SetLayeredWindowAttributes Lib "user32" (ByVal hwnd As Long, ByVal crKey As Long, ByVal bAlpha As Byte, ByVal dwFlags As Long) As Long
#Else
    Private Declare Function InternetGetConnectedState Lib "wininet.dll" (ByRef dwflags As Long, ByVal dwReserved As Long) As Long
    Private Declare Function GetSystemMetrics Lib "user32" (ByVal nIndex As Long) As Long
    
    
    Private Declare Function SetWindowLong Lib "user32" Alias "SetWindowLongA" (ByVal hWnd As Long, ByVal nIndex As Long, ByVal dwNewLong As Long) As Long
    Private Declare Function FindWindow Lib "user32" Alias "FindWindowA" (ByVal lpClassName As String, ByVal lpWindowName As String) As Long
    Private Declare Function FindWindowA Lib "user32" (ByVal lpClassName As String, ByVal lpWindowName As String) As Long
    Private Declare Function GetWindowLong Lib "user32" Alias "GetWindowLongA" (ByVal hWnd As Long, ByVal nIndex As Long) As Long
    Private Declare Function DrawMenuBar Lib "user32" (ByVal hWnd As Long) As Long
    Private Declare Function SetLayeredWindowAttributes Lib "user32" (ByVal hWnd As Long, ByVal crKey As Long, ByVal bAlpha As Byte, ByVal dwFlags As Long) As Long
#End If


Private Const GWL_STYLE As Long = (-16)
Private Const GWL_EXSTYLE As Long = (-20)
Private Const WS_CAPTION As Long = &HC00000
Private Const WS_EX_DLGMODALFRAME As Long = &H1
Private Const WS_EX_LAYERED = &H80000
Private Const LWA_COLORKEY = &H1
Private Const LWA_ALPHA = &H2
Private m_sngDownX As Single
Private m_sngDownY As Single

Public Function GetInternetConnectedState() As Boolean
    GetInternetConnectedState = InternetGetConnectedState(0&, 0&)
End Function

Private Sub MakeFormTransparent(frm As Object, Optional color As Variant)
    Dim formhandle As Long
    Dim bytOpacity As Byte
    formhandle = CLng(FindWindow(vbNullString, frm.Caption))
    If IsMissing(color) Then color = vbWhite
    bytOpacity = 100
    SetWindowLong formhandle, GWL_EXSTYLE, GetWindowLong(formhandle, GWL_EXSTYLE) Or WS_EX_LAYERED
    frm.BackColor = color
    SetLayeredWindowAttributes formhandle, color, bytOpacity, LWA_COLORKEY
End Sub

Private Sub MakeFormBorderless(frm As Object)
    Dim lngWindow As Long
    Dim lFrmHdl As Long
    lFrmHdl = CLng(FindWindow(vbNullString, frm.Caption))
    lngWindow = GetWindowLong(lFrmHdl, GWL_STYLE)
    lngWindow = lngWindow And (Not WS_CAPTION)
    SetWindowLong lFrmHdl, GWL_STYLE, lngWindow
    lngWindow = GetWindowLong(lFrmHdl, GWL_EXSTYLE)
    lngWindow = lngWindow And Not WS_EX_DLGMODALFRAME
    SetWindowLong lFrmHdl, GWL_EXSTYLE, lngWindow
    DrawMenuBar lFrmHdl
End Sub

Private Sub LVK_Click()
    FollowLink ("https://vk.com/video/playlist/735281600_1")
End Sub

Private Sub LExit_Click()
    Unload Me
End Sub

Private Sub LBLOG_Click()
    FollowLink ("https://alexofrhodes.github.io")
End Sub

Private Sub LGitHub_Click()
    FollowLink ("https://github.com/alexofrhodes")
End Sub

Private Sub LYouTube_Click()
    FollowLink ("https://www.youtube.com/channel/UC5QH3fn1zjx0aUjRER_rOjg")
End Sub

Private Sub LBuyMeACoffee_Click()
    FollowLink "http://paypal.me/alexofrhodes"
End Sub

Private Function CLIP(Optional StoreText As String) As String
    Dim x As Variant
    x = StoreText
    With CreateObject("htmlfile")
        With .parentWindow.clipboardData
            Select Case True
            Case Len(StoreText)
                .SetData "text", x
            Case Else
                CLIP = .GetData("text")
            End Select
        End With
    End With
End Function

Private Sub LEmail_Click()
    If GetInternetConnectedState = False Then
        MsgBox "Seems Internet is not available"
        Exit Sub
    End If
    If OutlookCheck = True Then
        MailDev
    Else
        Dim out As String
        out = AUTHOR_EMAIL
        CLIP out
        MsgBox ("Seems Outlook is not available" & Chr(10) & _
        "DEV's email address " & vbNewLine & out & vbNewLine & "copied to clipboard")
    End If
End Sub

Function OutlookCheck() As Boolean
    Dim xOLApp As Object
    Set xOLApp = CreateObject("Outlook.Application")
    If Not xOLApp Is Nothing Then
        OutlookCheck = True
        Set xOLApp = Nothing
        Exit Function
    End If
    OutlookCheck = False
End Function


Sub MailDev()
    Dim OutApp As Object
    Dim OutMail As Object
    Dim strBody As String
    Set OutApp = CreateObject("Outlook.Application")
    Set OutMail = OutApp.CreateItem(0)
    On Error Resume Next
    With OutMail
        .To = AUTHOR_EMAIL
        .cc = vbNullString
        .BCC = vbNullString
        .Subject = "Feedback or request - " & ThisWorkbook.Name
        .body = strBody
        .display
    End With
    On Error GoTo 0
    Set OutMail = Nothing
    Set OutApp = Nothing
End Sub

Private Sub UserForm_Initialize()

    Dim screenWidth As Long
    Dim screenHeight As Long
    
    screenWidth = GetSystemMetrics(0)  ' SM_CXSCREEN
    screenHeight = GetSystemMetrics(1)  ' SM_CYSCREEN
    
    Me.Width = screenWidth
    Me.Height = screenHeight
    
    Frame1.Left = Me.Width / 2 - Image1.Width
    Frame1.Top = Me.Height / 2 - Image1.Height
    
    MakeFormBorderless Me
'    MakeFormTransparent Me, vbYellow
End Sub




'm_DebugPrint	Module

Option Explicit

Public Sub dp(var As Variant)
'@AssignedModule m_DebugPrint
'@INCLUDE PROCEDURE PrintXML
'@INCLUDE PROCEDURE printArray
'@INCLUDE PROCEDURE printCollection
'@INCLUDE PROCEDURE printDictionary
    Dim element     As Variant
    Dim i As Long
'    Debug.Print TypeName(var)
    Select Case TypeName(var)
    Case Is = "String", "Long", "Integer", "Double", "Boolean"
        Debug.Print var
    Case Is = "Variant()", "String()", "Long()", "Integer()"
        printArray var
    Case Is = "Collection"
        printCollection var
    Case Is = "Dictionary"
        printDictionary var
    Case Is = "Date"
        Debug.Print var
    Case Is = "IXMLDOMElement"
        PrintXML var
    Case Else
    End Select
End Sub

Sub PrintXML(var)
'@AssignedModule m_DebugPrint
    Debug.Print var.XML
End Sub

'Sub PrintXML(NodeList)
''   Parse all levels recursively
'    Dim obj
'    On Error Resume Next
'    Set obj = NodeList.ChildNodes
'    If Err.Number = 0 Then
'
'    Else
'        Err.clear
'        Set obj = NodeList.NodeList
'        If Err.Number <> 0 Then: Err.clear: Exit Sub
'    End If
'    On Error GoTo 0
'    Dim child
'    For Each child In obj
'        If Not Left(child.nodename, 1) = "#" Then Debug.Print child.nodename & ":" & child.Text
'        If child.ChildNodes.Length > 0 Then PrintXML child.ChildNodes
'    Next
'End Sub

Private Sub printArray(var As Variant)
'@AssignedModule m_DebugPrint
'@INCLUDE PROCEDURE dp
'@INCLUDE PROCEDURE PrintXML
'@INCLUDE PROCEDURE DPH
'@INCLUDE PROCEDURE ArrayDimensions
    Dim element
    If ArrayDimensions(var) = 1 Then
'        Debug.Print Join(var, vbNewLine)
        For Each element In var
            dp element
        Next
    ElseIf ArrayDimensions(var) > 1 Then
        DPH var
    End If
End Sub

Private Sub printCollection(var As Variant)
'@AssignedModule m_DebugPrint
'@INCLUDE PROCEDURE dp
    Dim elem        As Variant
    For Each elem In var
        dp elem
    Next elem
End Sub

Private Sub printDictionary(var As Variant)
'@TODO detect error cause I met when printing a dic from JSON related modules
'@AssignedModule m_DebugPrint
'@INCLUDE PROCEDURE dp
    Dim i As Long: Dim iCount As Long
    Dim arrKeys
    Dim sKey        As String
    Dim varItem
    
    Dim key As Variant
    For Each key In var.Keys
        dp var(key)
        
    Next key
    
'    Stop
    
'    With var
'        iCount = .Count
'        arrKeys = .Keys
'        iCount = UBound(arrKeys, 1)
'        For i = 0 To iCount
'            sKey = arrKeys(i)
'            Debug.Print "Key " & sKey
'            Debug.Print String(20, "-")
'            If IsObject(.item(sKey)) Then
'                dp (.item(sKey))
'            Else
'                Debug.Print "Key " & sKey & " : "
'                dp .item(sKey)
'            End If
'        Next i
'    End With
End Sub

Private Sub DPH(ByVal Hairetu, Optional HyoujiMaxNagasa%, Optional HairetuName$)
'https://gist.github.com/YujiFukami/15c922d41ff06c9b12ad450a14131080#file-
'@AssignedModule m_DebugPrint
'@INCLUDE PROCEDURE DebugPrintHairetu
    Call DebugPrintHairetu(Hairetu, HyoujiMaxNagasa, HairetuName)
End Sub

Public Function ArrayDimensions(ByVal vArray As Variant) As Long
'@AssignedModule m_DebugPrint
    Dim dimnum      As Long
    Dim ErrorCheck As Long
    On Error GoTo FinalDimension
    For dimnum = 1 To 60000
        ErrorCheck = LBound(vArray, dimnum)
    Next
FinalDimension:
    ArrayDimensions = dimnum - 1
End Function

Private Sub DebugPrintHairetu(ByVal Hairetu, Optional HyoujiMaxNagasa%, Optional HairetuName$)
'https://gist.github.com/YujiFukami/15c922d41ff06c9b12ad450a14131080#file-
'@AssignedModule m_DebugPrint
'@INCLUDE PROCEDURE Transpose2DArray
'@INCLUDE PROCEDURE ReptText
'@INCLUDE PROCEDURE MaxValue
'@INCLUDE PROCEDURE ShortenToByteCharacters
    Dim i&, j&, k&, M&, N&
    Dim TateMin&, TateMax&, YokoMin&, YokoMax&
    Dim WithTableHairetu
    Dim NagasaList, MaxNagasaList
    Dim NagasaOnajiList
    Dim OutputList
    Const SikiriMoji$ = "|"
    Dim Jigen2%
    On Error Resume Next
    Jigen2 = UBound(Hairetu, 2)
'    On Error GoTo 0

    If Jigen2 = 0 Then
        Hairetu = Transpose2DArray(Hairetu)
    End If
    TateMin = LBound(Hairetu, 1)
    TateMax = UBound(Hairetu, 1)
    YokoMin = LBound(Hairetu, 2)
    YokoMax = UBound(Hairetu, 2)
    ReDim WithTableHairetu(1 To TateMax - TateMin + 1 + 1, 1 To YokoMax - YokoMin + 1 + 1)
    For i = 1 To TateMax - TateMin + 1
        WithTableHairetu(i + 1, 1) = TateMin + i - 1
        For j = 1 To YokoMax - YokoMin + 1
            WithTableHairetu(1, j + 1) = YokoMin + j - 1
            WithTableHairetu(i + 1, j + 1) = Hairetu(i - 1 + TateMin, j - 1 + YokoMin)
        Next j
    Next i
    N = UBound(WithTableHairetu, 1)
    M = UBound(WithTableHairetu, 2)
    ReDim NagasaList(1 To N, 1 To M)
    ReDim MaxNagasaList(1 To M)
    Dim tmpStr$
    For j = 1 To M
        For i = 1 To N
            If j > 1 And HyoujiMaxNagasa <> 0 Then
                tmpStr = WithTableHairetu(i, j)
                WithTableHairetu(i, j) = ShortenToByteCharacters(tmpStr, HyoujiMaxNagasa)
            End If
            NagasaList(i, j) = LenB(StrConv(WithTableHairetu(i, j), vbFromUnicode))
            MaxNagasaList(j) = MaxValue(MaxNagasaList(j), NagasaList(i, j))
        Next i
    Next j
    ReDim NagasaOnajiList(1 To N, 1 To M)
    Dim TmpMaxNagasa&
    For j = 1 To M
        TmpMaxNagasa = MaxNagasaList(j)
        For i = 1 To N
            NagasaOnajiList(i, j) = WithTableHairetu(i, j) & ReptText(" ", IIf(WithTableHairetu(i, j) = "", 1, TmpMaxNagasa - NagasaList(i, j)))
        Next i
    Next j
    ReDim OutputList(1 To N)
    For i = 1 To N
        For j = 1 To M
            If j = 1 Then
                OutputList(i) = NagasaOnajiList(i, j)
            Else
                OutputList(i) = OutputList(i) & SikiriMoji & NagasaOnajiList(i, j)
            End If
        Next j
    Next i
    Debug.Print HairetuName
    For i = 1 To N
        Debug.Print OutputList(i)
    Next i
End Sub

Function ReptText(ByVal Text As String, ByVal Count As Integer) As String
'@AssignedModule m_DebugPrint
    Dim result As String
    result = ""
    
    If Count > 0 Then
        Dim i As Integer
        For i = 1 To Count
            result = result & Text
        Next i
    End If
    
    ReptText = result
End Function

Function MaxValue(ParamArray values() As Variant) As Variant
'@AssignedModule m_DebugPrint
'@INCLUDE PROCEDURE GetErrorValue
    If Not IsArray(values) Then
        MaxValue = GetErrorValue
        Exit Function
    End If
    
    Dim i As Long
    Dim Max As Double
    
    If UBound(values) >= LBound(values) Then
        Max = values(LBound(values))
        For i = LBound(values) + 1 To UBound(values)
            If IsNumeric(values(i)) Then
                If values(i) > Max Then
                    Max = values(i)
                End If
            End If
        Next i
    End If
    
    MaxValue = IIf(Max = 0, GetErrorValue, Max)
End Function

Function GetErrorValue() As Variant
'@AssignedModule m_DebugPrint
    GetErrorValue = CVErr(2042) ' 2042 represents the xlErrValue error number in Excel
End Function

Public Function ShortenToByteCharacters(Mojiretu$, ByteNum%)
'https://gist.github.com/YujiFukami/15c922d41ff06c9b12ad450a14131080#file-
'@AssignedModule m_DebugPrint
'@INCLUDE PROCEDURE CalculateByteCharacters
'@INCLUDE PROCEDURE TextDecomposition
    Dim OriginByte%
    Dim Output
    OriginByte = LenB(StrConv(Mojiretu, vbFromUnicode))
    If OriginByte <= ByteNum Then
        Output = Mojiretu
    Else
        Dim RuikeiByteList, BunkaiMojiretu
        RuikeiByteList = CalculateByteCharacters(Mojiretu)
        BunkaiMojiretu = TextDecomposition(Mojiretu)
        Dim AddMoji$
        AddMoji = "."
        Dim i&, N&
        N = Len(Mojiretu)
        For i = 1 To N
            If RuikeiByteList(i) < ByteNum Then
                Output = Output & BunkaiMojiretu(i)
            ElseIf RuikeiByteList(i) = ByteNum Then
                If LenB(StrConv(BunkaiMojiretu(i), vbFromUnicode)) = 1 Then
                    Output = Output & AddMoji
                Else
                    Output = Output & AddMoji & AddMoji
                End If
                Exit For
            ElseIf RuikeiByteList(i) > ByteNum Then
                Output = Output & AddMoji
                Exit For
            End If
        Next i
    End If
    ShortenToByteCharacters = Output
End Function

Private Function CalculateByteCharacters(Mojiretu$)
'https://gist.github.com/YujiFukami/15c922d41ff06c9b12ad450a14131080#file-
'@AssignedModule m_DebugPrint
    Dim MojiKosu%
    MojiKosu = Len(Mojiretu)
    Dim Output
    ReDim Output(1 To MojiKosu)
    Dim i&
    Dim TmpMoji$
    For i = 1 To MojiKosu
        TmpMoji = Mid(Mojiretu, i, 1)
        If i = 1 Then
            Output(i) = LenB(StrConv(TmpMoji, vbFromUnicode))
        Else
            Output(i) = LenB(StrConv(TmpMoji, vbFromUnicode)) + Output(i - 1)
        End If
    Next i
    CalculateByteCharacters = Output
End Function

Private Function TextDecomposition(Mojiretu$)
'https://gist.github.com/YujiFukami/15c922d41ff06c9b12ad450a14131080#file-
'@AssignedModule m_DebugPrint
    Dim i&, N&
    Dim Output
    N = Len(Mojiretu)
    ReDim Output(1 To N)
    For i = 1 To N
        Output(i) = Mid(Mojiretu, i, 1)
    Next i
    TextDecomposition = Output
End Function

Function DpHeader( _
                 str As Variant, _
                 Optional lvl As Integer = 1, _
                 Optional Character As String = "'", _
                 Optional Top As Boolean, _
                 Optional Bottom As Boolean) As String
'@AssignedModule m_DebugPrint
'@INCLUDE PROCEDURE LargestLength
    If lvl < 1 Then lvl = 1
    If Character = "" Then Character = "'"
    Dim indentation As Integer
    indentation = (lvl * 4) - 4 + 1
    Dim quote As String: quote = "'"
    Dim s As String
    Dim element As Variant
    If Top = True Then s = vbNewLine & quote & String(indentation + LargestLength(str), Character) & vbNewLine
    If TypeName(str) <> "String" Then
        For Each element In str
            s = s & quote & Character & Space(indentation) & element & vbNewLine
        Next
    Else
        s = s & quote & String(indentation, Character) & str
    End If
    If Bottom = True Then s = s & quote & String(indentation + LargestLength(str), Character)
    DpHeader = s
End Function

Function LargestLength(MyObj) As Long
'@AssignedModule m_DebugPrint
'@INCLUDE PROCEDURE dp
    LargestLength = 0
    Dim element As Variant
    Select Case TypeName(MyObj)
    Case Is = "String"
        LargestLength = Len(MyObj)
    Case "Collection"
        For Each element In MyObj
            If TypeName(element) = "String" Then
                If Len(element) > LargestLength Then LargestLength = Len(element)
            Else
                If element.Width > LargestLength Then LargestLength = element.Width
            End If
        Next element
    Case "Variant", "Variant()", "String()"
       For Each element In MyObj
            If TypeName(element) = "String" Then
                If Len(element) > LargestLength Then LargestLength = Len(element)
'                dp element & vbTab & Len(element)
            End If
        Next
    Case Else
    End Select
End Function





'README	Document

Option Explicit

'm_Compare	Module

Option Explicit

Public Enum operator
    IS_LIKE
    IS_EQUAL
    NOT_EQUAL
    IS_CONTAINS
    NOT_CONTAINS
    STARTS_WITH
    ENDS_WITH
    GREATER_THAN
    GREATER_OR_EQUAL
    LESS_THAN
    LESS_OR_EQUAL
End Enum

Function CompareValues(ByVal value1 As Variant, ByVal value2 As Variant, ByVal operator As operator) As Boolean
'@AssignedModule m_Compare
'@INCLUDE DECLARATION operator
    Select Case operator
        Case IS_LIKE
            CompareValues = (UCase(CStr(value1)) Like "*" & UCase(CStr(value2)) & "*")
        Case IS_EQUAL
            CompareValues = (value1 = value2)
        Case NOT_EQUAL
            CompareValues = (value1 <> value2)
        Case IS_CONTAINS
            CompareValues = (InStr(1, CStr(value1), CStr(value2), vbTextCompare) > 0)
        Case NOT_CONTAINS
            CompareValues = (InStr(1, CStr(value1), CStr(value2), vbTextCompare) = 0)
        Case STARTS_WITH
            CompareValues = (Left(CStr(value1), Len(CStr(value2))) = CStr(value2))
        Case ENDS_WITH
            CompareValues = (Right(CStr(value1), Len(CStr(value2))) = CStr(value2))
        Case GREATER_THAN
            CompareValues = (value1 > value2)
        Case GREATER_OR_EQUAL
            CompareValues = (value1 >= value2)
        Case LESS_THAN
            CompareValues = (value1 < value2)
        Case LESS_OR_EQUAL
            CompareValues = (value1 <= value2)
    End Select
End Function


Function FilterArray2d(inputArray As Variant, HasHeader As Boolean, MatchString As String, _
        ComparisonOperator As operator, Optional ColumnIndex As Long = -1) As Variant
'@AssignedModule m_Compare
'@INCLUDE PROCEDURE CompareValues
'@INCLUDE PROCEDURE ArrayCombine
'@INCLUDE DECLARATION operator
    Dim numRows As Long
    Dim numCols As Long
    Dim resultArray() As Variant
    Dim resultIndex As Long
    Dim i As Long
    Dim firstRow As Long: firstRow = LBound(inputArray, 1)
    Dim firstColumn As Long: firstColumn = LBound(inputArray, 2)
    numRows = UBound(inputArray, 1) - firstRow + 1
    numCols = UBound(inputArray, 2) - firstColumn + 1
    
    ' Define the result array to have the same number of columns as the input array
    ReDim resultArray(firstRow To 1, firstColumn To numCols)
    resultIndex = 0
    
    ' Check if the array has a header
    Dim startRow As Long
    If HasHeader Then
        startRow = LBound(inputArray, 1)
        ' Copy the header row to the result array
        For i = 1 To numCols
            resultArray(1, i) = inputArray(1, i)
        Next i
        startRow = startRow + 1
        resultIndex = resultIndex + 1
    Else
        startRow = LBound(inputArray, 1) + 1
    End If
    
    ' Loop through the rows of the input array
    For i = startRow To numRows
        Dim rowMatches As Boolean
        rowMatches = False
        
        ' Check if the row matches the specified criteria
        If ColumnIndex < 0 Or ColumnIndex > numCols Then
            ' Match any cell in the row
            Dim j As Long
            For j = firstColumn To numCols
                If CompareValues(inputArray(i, j), MatchString, ComparisonOperator) Then
                    rowMatches = True
                    Exit For
                End If
            Next j
        Else
            ' Match the specified column
            If CompareValues(inputArray(i, ColumnIndex), MatchString, ComparisonOperator) Then
                rowMatches = True
            End If
        End If
        
        ' Copy the matching row to the result array
        If rowMatches Then
            Dim tmp()
            ReDim tmp(1 To 1, firstColumn To numCols)
            For j = firstColumn To numCols
                tmp(1, j) = inputArray(i, j)
            Next
            resultArray = ArrayCombine(resultArray, tmp, True)
        End If
    Next i
    
    ' Return the filtered array
    If resultIndex = 0 Then
        FilterArray2d = Array()
    Else
        FilterArray2d = resultArray
    End If
End Function

Private Function ArrayCombine(a As Variant, b As Variant, Optional stacked As Boolean = True) As Variant
    'assumes that A and B are 2-dimensional variant arrays
    'if stacked is true then A is placed on top of B
    'in this case the number of rows must be the same,
    'otherwise they are placed side by side A|B
    'in which case the number of columns are the same
    'LBound can be anything but is assumed to be
    'the same for A and B (in both dimensions)
    'False is returned if a clash
'@AssignedModule m_Compare

    Dim LB As Long, m_A As Long, n_A As Long
    Dim m_B As Long, n_B As Long
    Dim M As Long, N As Long
    Dim i As Long, j As Long, k As Long
    Dim c As Variant

    If TypeName(a) = "Range" Then a = a.Value
    If TypeName(b) = "Range" Then b = b.Value

    LB = LBound(a, 1)
    m_A = UBound(a, 1)
    n_A = UBound(a, 2)
    m_B = UBound(b, 1)
    n_B = UBound(b, 2)

    If stacked Then
        M = m_A + m_B + 1 - LB
        N = n_A
        If n_B <> N Then
            ArrayCombine = False
            Exit Function
        End If
    Else
        M = m_A
        If m_B <> M Then
            ArrayCombine = False
            Exit Function
        End If
        N = n_A + n_B + 1 - LB
    End If
    ReDim c(LB To M, LB To N)
    For i = LB To M
        For j = LB To N
            If stacked Then
                If i <= m_A Then
                    c(i, j) = a(i, j)
                Else
                    c(i, j) = b(LB + i - m_A - 1, j)
                End If
            Else
                If j <= n_A Then
                    c(i, j) = a(i, j)
                Else
                    c(i, j) = b(i, LB + j - n_A - 1)
                End If
            End If
        Next j
    Next i
    ArrayCombine = c
End Function

Public Function NumberOfArrayDimensions(arr As Variant) As Byte
'@AssignedModule m_Compare
    Dim Ndx As Byte
    Dim Res As Long
    On Error Resume Next
    Do
        Ndx = Ndx + 1
        Res = UBound(arr, Ndx)
    Loop Until Err.Number <> 0
    NumberOfArrayDimensions = Ndx - 1
End Function

Function isUserform(thing) As Boolean
'@AssignedModule m_Compare
    On Error Resume Next
    Dim Module As VBComponent
    Set Module = ThisWorkbook.VBProject.VBComponents(thing.Name)
    isUserform = Module.Type = vbext_ct_MSForm
End Function


'EventListenerEmitter	Class

'------------------------
'userform declararations
'------------------------
'   Private WithEvents Emitter As EventListeneRemitter

'------------------------
'in userform_initialise
'------------------------
'   Set Emitter = New EventListeneRemitter
'   Emitter.AddEventListenerAll Me

Rem Author Robert Todar

Option Explicit
Option Compare Text

' Array of all the different event listeners for every userform control and the form itself
Private EventList() As New EventListenerItem

' All the current possible events that can be emitted.
' Note, EmittedEvent is sent for all events!
Public Event EmittedEvent(ByRef control As Object, ByVal EventType As EmittedEvent, ByRef EventParameters As Collection)
Public Event Click(ByRef control As Object)
Public Event DblClick(ByRef control As Object, ByRef Cancel As MSForms.ReturnBoolean)
Public Event KeyUp(ByRef control As Object, ByRef KeyCode As MSForms.ReturnInteger, ByRef Shift As Integer)
Public Event KeyDown(ByRef control As Object, ByRef KeyCode As MSForms.ReturnInteger, ByRef Shift As Integer)
Public Event MouseOver(ByRef control As Object)
Public Event MouseOut(ByRef control As Object)
Public Event MouseMove(ByRef control As Object, ByRef Shift As Integer, ByRef x As Single, ByRef y As Single)
Public Event Focus(ByRef control As Object)
Public Event Blur(ByRef control As Object)
Public Event Change(ByRef control As Object)

' Events to Labels
Public Event LabelMouseOver(ByRef Label As MSForms.Label)
Public Event LabelMouseOut(ByRef Label As MSForms.Label)
Public Event LabelClick(ByRef Label As MSForms.Label)
Public Event LabelDoubleClick(ByRef Label As MSForms.Label, ByRef Cancel As MSForms.ReturnBoolean)
Public Event LabelMouseMove(ByRef Label As MSForms.Label, ByRef Shift As Integer, ByRef x As Single, ByRef y As Single)

' Events to Textboxes
Public Event TextboxFocus(ByRef Textbox As MSForms.Textbox)
Public Event TextboxBlur(ByRef Textbox As MSForms.Textbox)
Public Event TextboxMouseOver(ByRef Textbox As MSForms.Textbox)
Public Event TextboxMouseOut(ByRef Textbox As MSForms.Textbox)
Public Event TextboxClick(ByRef Textbox As MSForms.Textbox)
Public Event TextboxDoubleClick(ByRef Textbox As MSForms.Textbox, ByRef Cancel As MSForms.ReturnBoolean)
Public Event TextboxMouseMove(ByRef Textbox As MSForms.Textbox, ByRef Shift As Integer, ByRef x As Single, ByRef y As Single)

' Events to CommandButtons
Public Event CommandButtonMouseOver(ByRef CommandButton As MSForms.CommandButton)
Public Event CommandButtonMouseOut(ByRef CommandButton As MSForms.CommandButton)
Public Event CommandButtonClick(ByRef CommandButton As MSForms.CommandButton)
Public Event CommandButtonDoubleClick(ByRef CommandButton As MSForms.CommandButton, ByRef Cancel As MSForms.ReturnBoolean)
Public Event CommandButtonMouseMove(ByRef CommandButton As MSForms.CommandButton, ByRef Shift As Integer, ByRef x As Single, ByRef y As Single)

' Types of events that can occur
Public Enum EmittedEvent
    Change
    Click
    DoubleClick
    MouseMove
    MouseOut
    MouseOver
    MouseDown
    MouseUp
    KeyUp
    KeyDown
    Focus
    Blur
End Enum

' Called by EventListenerItem class - main entryway of emitting all events
Public Sub EmitEvent(ByRef control As Object, ByVal EventType As EmittedEvent, Optional ByRef EventParameters As Collection)
    ' Event raised for all events. This is a way for the user to collect from a single location.
    RaiseEvent EmittedEvent(control, EventType, EventParameters)

    ' Specific events
    Select Case EventType

    Case Click
        RaiseEvent Click(control)

    Case DoubleClick
        RaiseEvent DblClick(control, EventParameters("Cancel"))

    Case KeyUp
        RaiseEvent KeyUp(control, EventParameters("KeyCode"), EventParameters("Shift"))

    Case KeyDown
        RaiseEvent KeyDown(control, EventParameters("KeyCode"), EventParameters("Shift"))

    Case MouseOver
        RaiseEvent MouseOver(control)

    Case MouseOut
        RaiseEvent MouseOut(control)

    Case Focus
        RaiseEvent Focus(control)

    Case Blur
        RaiseEvent Blur(control)

    Case MouseMove
        RaiseEvent MouseMove(control, EventParameters("Shift"), EventParameters("X"), EventParameters("Y"))
    
    Case Change 'added
        RaiseEvent Change(control)
    End Select

    ' Call the specific control type events
    Select Case TypeName(control)
    Case "Label"
        EmitLabelEvent control, EventType, EventParameters

    Case "Textbox"
        EmitTextboxEvent control, EventType, EventParameters

    Case "CommandButton"
        EmitCommandButtonEvent control, EventType, EventParameters

    End Select
End Sub

' Events for Labels
Private Sub EmitLabelEvent(ByRef Label As MSForms.Label, ByVal EventType As String, ByRef EventParameters As Collection)
    Select Case EventType
    Case EmittedEvent.Click
        RaiseEvent LabelClick(Label)

    Case EmittedEvent.DoubleClick
        RaiseEvent LabelDoubleClick(Label, EventParameters("Cancel"))

    Case EmittedEvent.MouseOver
        RaiseEvent LabelMouseOver(Label)

    Case EmittedEvent.MouseOut
        RaiseEvent LabelMouseOut(Label)

    Case EmittedEvent.MouseMove
        RaiseEvent MouseMove(Label, EventParameters("Shift"), EventParameters("X"), EventParameters("Y"))
    End Select
End Sub

' Events for Textboxes
Private Sub EmitTextboxEvent(ByRef Textbox As MSForms.Textbox, ByVal EventType As String, ByRef EventParameters As Collection)
    Select Case EventType
    Case EmittedEvent.Blur
        RaiseEvent TextboxBlur(Textbox)

    Case EmittedEvent.Focus
        RaiseEvent TextboxFocus(Textbox)

    Case EmittedEvent.Click
        RaiseEvent TextboxClick(Textbox)

    Case EmittedEvent.DoubleClick
        RaiseEvent TextboxDoubleClick(Textbox, EventParameters("Cancel"))

    Case EmittedEvent.MouseOver
        RaiseEvent TextboxMouseOver(Textbox)

    Case EmittedEvent.MouseOut
        RaiseEvent TextboxMouseOut(Textbox)

    Case EmittedEvent.MouseMove
        RaiseEvent MouseMove(Textbox, EventParameters("Shift"), EventParameters("X"), EventParameters("Y"))
    End Select
End Sub

' Events for CommandButton
Private Sub EmitCommandButtonEvent(ByRef CommandButton As MSForms.CommandButton, ByVal EventType As String, ByRef EventParameters As Collection)
    Select Case EventType
    Case EmittedEvent.Click
        RaiseEvent CommandButtonClick(CommandButton)

    Case EmittedEvent.DoubleClick
        RaiseEvent CommandButtonDoubleClick(CommandButton, EventParameters("Cancel"))

    Case EmittedEvent.MouseOver
        RaiseEvent CommandButtonMouseOver(CommandButton)

    Case EmittedEvent.MouseOut
        RaiseEvent CommandButtonMouseOut(CommandButton)

    Case EmittedEvent.MouseMove
        RaiseEvent MouseMove(CommandButton, EventParameters("Shift"), EventParameters("X"), EventParameters("Y"))
    End Select
End Sub

' MUST CALL THIS IF YOU WANT TO programmatically SET CONTROL! OTHERWISE, EVENT'S WILL BE OFF!
Public Sub SetFocusToControl(ByRef control As Object)
    'If the user was to set focus through VBA then this code will fall apart considering
    'it is unaware of that event occurring.
    If Not control Is Nothing Then
        control.SetFocus
        EmitEvent control, Focus
    End If
End Sub

' ADD EVENT Listeners ON SPECIFIC CONTROLS - ALSO CALLED BY AddEventListenerAll
Public Sub AddEventListener(ByRef control As Object)
    ' Events are stored in a private EventListenerItem array
    If IsArrayEmpty(EventList) Then
        ReDim EventList(0 To 0)
    Else
        ReDim Preserve EventList(0 To UBound(EventList) + 1)
    End If

    'CALL AddEventListener IN EventListenerItem. THIS IS KEPT IN
    EventList(UBound(EventList)).AddEventListener control, Me
End Sub

'ADD EVENT Listener TO ALL CONTROLS INCLUDING THE FORM
Public Sub AddEventListenerAll(ByRef Form As Object)
    AddEventListener Form

    Dim control As MSForms.control
    For Each control In Form.Controls
        Select Case TypeName(control)
        Case "MultiPage"
        Case Else
            AddEventListener control
        End Select
    Next control
End Sub

Private Function IsArrayEmpty(arr As Variant) As Boolean
    ''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
    ' CPEARSON: http://www.cpearson.com/excel/VBAArrays.htm
    ' This function tests whether the array is empty (unallocated). Returns TRUE or FALSE.
    '
    ' The VBA IsArray function indicates whether a variable is an array, but it does not
    ' distinguish between allocated and unallocated arrays. It will return TRUE for both
    ' allocated and unallocated arrays. This function tests whether the array has actually
    ' been allocated.
    '''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''

    Err.Clear
    On Error Resume Next
    If IsArray(arr) = False Then
        ' we weren't passed an array, return True
        IsArrayEmpty = True
    End If

    ' Attempt to get the UBound of the array. If the array is
    ' unallocated, an error will occur.
    Dim UB As Long
    UB = UBound(arr, 1)
    If (Err.Number <> 0) Then
        IsArrayEmpty = True
    Else
        ''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
        ' On rare occasion, under circumstances I cannot reliably replicate, Err.Number
        ' will be 0 for an unallocated, empty array. On these occasions, LBound is 0 and
        ' UBound is -1. To accommodate the weird behavior, test to see if LB > UB.
        ' If so, the array is not allocated.
        ''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''''
        Err.Clear
        Dim LB As Long
        LB = LBound(arr)
        If LB > UB Then
            IsArrayEmpty = True
        Else
            IsArrayEmpty = False
        End If
    End If
End Function


'EventListenerItem	Class

Rem Author Robert Todar

Option Explicit
Option Compare Text

'SET FROM AddEventListener - NEEDED TO EMIT EVENT BACK TO IT.
Private WithEvents Emitter As EventListenerEmitter

'CONTROLS THAT HAVE THE EVENTS
Private WithEvents Form As MSForms.UserForm
Private WithEvents Textbox As MSForms.Textbox
Private WithEvents Label As MSForms.Label
Private WithEvents CommandButton As MSForms.CommandButton
Private WithEvents ComboBox As MSForms.ComboBox
Private WithEvents Frame As MSForms.Frame

Private Type state
    control As Object
    IsHoveredControl As Boolean
    IsFocusedControl As Boolean
End Type

Private This As state

'TODO
' - ADD EVENTS FOR ALL USERFORM CONTROLS
' - ADD MORE EVENTS THAN THE LIST BELOW
' - DOUBLE CHECK THAT EMITTS ARE ALL CURRENT

'CURRENT EVENTS (WILL ADD MORE AS I HAVE NEED)
' - MouseOver
' - MouseOut
' - MouseMove
' - Click
' - DblClick
' - Focus
' - Blur
' - KeyUp
' - KeyDown

' The Only public method. This will be called from EventListeneRemitter class module
Public Sub AddEventListener(ByRef ControlOrForm As Object, ByRef EmitterReference As EventListenerEmitter)
    ' Capture the emitter class. This will be used to emit events from each control.
    Set Emitter = EmitterReference

    ' This is used to compare and check to see if this is the control triggering the event.
    Set This.control = ControlOrForm

    ' Set control (or form) based on its type
    Select Case TypeName(ControlOrForm)
    Case "CommandButton"
        Set CommandButton = ControlOrForm

    Case "ComboBox"
        Set ComboBox = ControlOrForm

    Case "Frame"
        Set Frame = ControlOrForm

    Case "Label"
        Set Label = ControlOrForm

    Case "TextBox"
        Set Textbox = ControlOrForm

    Case Else

        If TypeOf ControlOrForm Is MSForms.UserForm Then
            Set Form = ControlOrForm
        Else
            Rem todo
            Rem err.Raise 5, TypeName(Me), "Invalid control: Currently unable to listen to events on " & TypeName(ControlOrForm)
        End If

    End Select
End Sub

'***********************************************************************************
' Helper functions
'***********************************************************************************

' Called on mousemove event. This is a way of creating a mouseover and mouseout event.
Private Sub CheckIfHoveredControl()
    If Not This.IsHoveredControl Then
        This.IsHoveredControl = True
        Emitter.EmitEvent This.control, MouseOver
    End If
End Sub

' Called on mousemove event. This is a way of creating a mouseover and mouseout event.
Private Sub CheckIfFocusedControl()
    If Not This.IsFocusedControl Then
        If TypeName(This.control) = "Frame" Then
            Emitter.SetFocusToControl This.control.ActiveControl
        Else
            This.IsFocusedControl = True
            Emitter.EmitEvent This.control, Focus
        End If
    End If
End Sub

' Simple Collection factory  for ease of use.
Private Function ToCollection(ParamArray keyValuePairs() As Variant) As Collection
    ' Check to see if there is even number of parameters
    Dim ArrayLenght As Long
    ArrayLenght = UBound(keyValuePairs) - LBound(keyValuePairs) + 1

    If ArrayLenght Mod 2 <> 0 Then
        Err.Raise 5, TypeName(Me), "Invalid parameters: expecting key/value pairs, but received an odd number of arguments."
    End If

    Set ToCollection = New Collection
    Dim index As Long
    For index = LBound(keyValuePairs) To UBound(keyValuePairs) Step 2
        ToCollection.Add keyValuePairs(index + 1), keyValuePairs(index)
    Next index
End Function

'***********************************************************************************
' EVENTS
'***********************************************************************************

' ONCE AN EVENT HAS EMMITED, EACH EVENTListenerITEM WILL LISTEN FOR THAT EVENT
Private Sub Emitter_EmittedEvent(ByRef control As Object, ByVal EventName As EmittedEvent, ByRef EventParameters As Collection)
    ' CREATE A MOUSEOVER MOUSEOUT EVENTS
    On Error GoTo EH '<-- TODO proper error handling
    Select Case EventName
    Case MouseOver
        If This.control.Name <> control.Name And This.IsHoveredControl Then 'todo catastrophic error occurs with dynamic controls
            This.IsHoveredControl = False
            Emitter.EmitEvent This.control, MouseOut
        End If

    Case Focus
        If This.control.Name <> control.Name And This.IsFocusedControl Then
            This.IsFocusedControl = False
            Emitter.EmitEvent This.control, Blur
        ElseIf This.control.Name = control.Name And This.IsFocusedControl = False Then
            This.IsFocusedControl = True
        End If

    End Select
EH:
End Sub

'------------------------------------------------------------------------
' USERFORM
'------------------------------------------------------------------------
Private Sub Form_Click()
    Emitter.EmitEvent This.control, Click
End Sub

Private Sub Form_DblClick(ByVal Cancel As MSForms.ReturnBoolean)
    Emitter.EmitEvent This.control, DoubleClick, ToCollection("Cancel", Cancel)
End Sub

Private Sub Form_MouseMove(ByVal Button As Integer, ByVal Shift As Integer, ByVal x As Single, ByVal y As Single)
    CheckIfHoveredControl
    Emitter.EmitEvent This.control, MouseMove, ToCollection("Button", Button, "Shift", Shift, "X", x, "Y", y)
End Sub

Private Sub Form_MouseDown(ByVal Button As Integer, ByVal Shift As Integer, ByVal x As Single, ByVal y As Single)
    Emitter.EmitEvent This.control, MouseDown, ToCollection("Button", Button, "Shift", Shift, "X", x, "Y", y)
End Sub

Private Sub Form_MouseUp(ByVal Button As Integer, ByVal Shift As Integer, ByVal x As Single, ByVal y As Single)
    Emitter.EmitEvent This.control, MouseUp, ToCollection("Button", Button, "Shift", Shift, "X", x, "Y", y)
End Sub

'------------------------------------------------------------------------
' COMMAND BUTTON
'------------------------------------------------------------------------
Private Sub CommandButton_Click()
    Emitter.EmitEvent This.control, Click
End Sub

Private Sub CommandButton_DblClick(ByVal Cancel As MSForms.ReturnBoolean)
    Emitter.EmitEvent This.control, DoubleClick, ToCollection("Cancel", Cancel)
End Sub

Private Sub CommandButton_MouseMove(ByVal Button As Integer, ByVal Shift As Integer, ByVal x As Single, ByVal y As Single)
    CheckIfHoveredControl
    Emitter.EmitEvent This.control, MouseMove, ToCollection("Button", Button, "Shift", Shift, "X", x, "Y", y)
End Sub

Private Sub CommandButton_MouseUp(ByVal Button As Integer, ByVal Shift As Integer, ByVal x As Single, ByVal y As Single)
    CheckIfFocusedControl
    Emitter.EmitEvent This.control, MouseUp, ToCollection("Button", Button, "Shift", Shift, "X", x, "Y", y)
End Sub

Private Sub CommandButton_MouseDown(ByVal Button As Integer, ByVal Shift As Integer, ByVal x As Single, ByVal y As Single)
    Emitter.EmitEvent This.control, MouseDown, ToCollection("Button", Button, "Shift", Shift, "X", x, "Y", y)
End Sub

Private Sub CommandButton_KeyUp(ByVal KeyCode As MSForms.ReturnInteger, ByVal Shift As Integer)
    CheckIfFocusedControl
    Emitter.EmitEvent This.control, KeyUp, ToCollection("KeyCode", KeyCode, "Shift", Shift)
End Sub

Private Sub CommandButton_KeyDown(ByVal KeyCode As MSForms.ReturnInteger, ByVal Shift As Integer)
    Emitter.EmitEvent This.control, KeyDown, ToCollection("KeyCode", KeyCode, "Shift", Shift)
End Sub

'------------------------------------------------------------------------
' LABEL
'------------------------------------------------------------------------
Private Sub Label_Click()
    Emitter.EmitEvent This.control, Click
End Sub

Private Sub Label_DblClick(ByVal Cancel As MSForms.ReturnBoolean)
    Emitter.EmitEvent This.control, DoubleClick, ToCollection("Cancel", Cancel)
End Sub

Private Sub Label_MouseMove(ByVal Button As Integer, ByVal Shift As Integer, ByVal x As Single, ByVal y As Single)
    CheckIfHoveredControl
    Emitter.EmitEvent This.control, MouseMove, ToCollection("Button", Button, "Shift", Shift, "X", x, "Y", y)
End Sub

Private Sub Label_MouseDown(ByVal Button As Integer, ByVal Shift As Integer, ByVal x As Single, ByVal y As Single)
    Emitter.EmitEvent This.control, MouseDown, ToCollection("Button", Button, "Shift", Shift, "X", x, "Y", y)
End Sub

Private Sub Label_MouseUp(ByVal Button As Integer, ByVal Shift As Integer, ByVal x As Single, ByVal y As Single)
    Emitter.EmitEvent This.control, MouseUp, ToCollection("Button", Button, "Shift", Shift, "X", x, "Y", y)
End Sub

'------------------------------------------------------------------------
' Frame
'------------------------------------------------------------------------
Private Sub Frame_Click()
    Emitter.EmitEvent This.control, Click
End Sub

Private Sub Frame_DblClick(ByVal Cancel As MSForms.ReturnBoolean)
    Emitter.EmitEvent This.control, DoubleClick, ToCollection("Cancel", Cancel)
End Sub

Private Sub Frame_MouseMove(ByVal Button As Integer, ByVal Shift As Integer, ByVal x As Single, ByVal y As Single)
    CheckIfHoveredControl
    Emitter.EmitEvent This.control, MouseMove, ToCollection("Button", Button, "Shift", Shift, "X", x, "Y", y)
End Sub

Private Sub Frame_MouseDown(ByVal Button As Integer, ByVal Shift As Integer, ByVal x As Single, ByVal y As Single)
    CheckIfFocusedControl        'FRAME DOESN'T TAKE FOCUS BUT ACTIVE CONTROL IN FRAME DOES
    Emitter.EmitEvent This.control, MouseDown
End Sub

Private Sub Frame_MouseUp(ByVal Button As Integer, ByVal Shift As Integer, ByVal x As Single, ByVal y As Single)
    Emitter.EmitEvent This.control, MouseUp, ToCollection("Button", Button, "Shift", Shift, "X", x, "Y", y)
End Sub

'------------------------------------------------------------------------
' Textbox
'------------------------------------------------------------------------
Private Sub Textbox_Click()
    Emitter.EmitEvent This.control, Click
End Sub

Private Sub Textbox_DblClick(ByVal Cancel As MSForms.ReturnBoolean)
    Emitter.EmitEvent This.control, DoubleClick, ToCollection("Cancel", Cancel)
End Sub

Private Sub Textbox_MouseMove(ByVal Button As Integer, ByVal Shift As Integer, ByVal x As Single, ByVal y As Single)
    CheckIfHoveredControl
    Emitter.EmitEvent This.control, MouseMove, ToCollection("Button", Button, "Shift", Shift, "X", x, "Y", y)
End Sub

Private Sub Textbox_MouseUp(ByVal Button As Integer, ByVal Shift As Integer, ByVal x As Single, ByVal y As Single)
    CheckIfFocusedControl
    Emitter.EmitEvent This.control, MouseUp, ToCollection("Button", Button, "Shift", Shift, "X", x, "Y", y)
End Sub

Private Sub Textbox_MouseDown(ByVal Button As Integer, ByVal Shift As Integer, ByVal x As Single, ByVal y As Single)
    Emitter.EmitEvent This.control, MouseDown, ToCollection("Button", Button, "Shift", Shift, "X", x, "Y", y)
End Sub

Private Sub Textbox_KeyUp(ByVal KeyCode As MSForms.ReturnInteger, ByVal Shift As Integer)
    CheckIfFocusedControl
    Emitter.EmitEvent This.control, KeyUp, ToCollection("KeyCode", KeyCode, "Shift", Shift)
End Sub

Private Sub Textbox_KeyDown(ByVal KeyCode As MSForms.ReturnInteger, ByVal Shift As Integer)
    Emitter.EmitEvent This.control, KeyDown, ToCollection("KeyCode", KeyCode, "Shift", Shift)
End Sub

Private Sub TextBox_Change() 'added
    Emitter.EmitEvent This.control, Change
End Sub

'------------------------------------------------------------------------
' Combobox
'------------------------------------------------------------------------
Private Sub ComboBox_Click()
    Emitter.EmitEvent This.control, Click
End Sub

Private Sub ComboBox_DblClick(ByVal Cancel As MSForms.ReturnBoolean)
    Emitter.EmitEvent This.control, DoubleClick, ToCollection("Cancel", Cancel)
End Sub

Private Sub ComboBox_MouseMove(ByVal Button As Integer, ByVal Shift As Integer, ByVal x As Single, ByVal y As Single)
    CheckIfHoveredControl
    Emitter.EmitEvent This.control, MouseMove, ToCollection("Button", Button, "Shift", Shift, "X", x, "Y", y)
End Sub

Private Sub ComboBox_KeyDown(ByVal KeyCode As MSForms.ReturnInteger, ByVal Shift As Integer) 'added
    Emitter.EmitEvent This.control, KeyDown, ToCollection("KeyCode", KeyCode, "Shift", Shift)
End Sub

Private Sub ComboBox_KeyUp(ByVal KeyCode As MSForms.ReturnInteger, ByVal Shift As Integer) 'added
    CheckIfFocusedControl
    Emitter.EmitEvent This.control, KeyUp, ToCollection("KeyCode", KeyCode, "Shift", Shift)
End Sub

Private Sub ComboBox_Change() 'added
    Emitter.EmitEvent This.control, Change
End Sub

Private Sub ComboBox_MouseUp(ByVal Button As Integer, ByVal Shift As Integer, ByVal x As Single, ByVal y As Single) 'added
    CheckIfFocusedControl
    Emitter.EmitEvent This.control, MouseUp, ToCollection("Button", Button, "Shift", Shift, "X", x, "Y", y)
End Sub

'uCalendar	UserForm




Option Explicit

Public gDate As New clsDate

Public Function Datepicker(Optional DateInput As Object) As String
    Dim str As String
    If VBA.TypeName(DateInput) = "Textbox" Or VBA.TypeName(DateInput) = "Range" Then str = DateInput.Value
    If VBA.TypeName(DateInput) = "CommandButton" Or VBA.TypeName(DateInput) = "Label" Then str = DateInput.Caption

    'If DatepInput <> "" Then <--- replaced with next line
    If str <> "" Then

        Dim curDate As String
        With uCalendar
            .txtYearName = Year(DateInput)
            .txtMonthNumber = Format(DateInput, "mm")

        End With

        With gDate
            .createDates txtYearName, txtMonthNumber
            .SelectDate .dFrame.Controls("lblDate" & Day(DateInput))
        End With
    Else

        With uCalendar
            .lblSelectedDate = Day(Date)
            .lblSelectedMonth = Format(Date, "mmmm")
            .lblSelectedYear = Year(Date)
            curDate = Day(Date) & "." & .txtMonthNumber Mod 12 & "." & .txtYearName
            .lblSelectedDateName = Format(curDate, "dddd")
            .txtSelectedDate = Format(curDate, "dd.mm.yyyy")
            .txtMonthNumber = Format(Date, "mm")
        End With

        With gDate.lblDateBack

        End With

    End If

    Me.Show

    Datepicker = Me.txtSelectedDate.Value

    If VBA.TypeName(DateInput) = "TextBox" Or VBA.TypeName(DateInput) = "Range" Then
        DateInput.Value = Me.txtSelectedDate.Value
    ElseIf VBA.TypeName(DateInput) = "CommandButton" Or VBA.TypeName(DateInput) = "Label" Then
        DateInput.Caption = Me.txtSelectedDate.Value
    Else
        'Datepicker = Me.txtSelectedDate.Value <--- put this before If to return the value anyway
    End If

End Function

Private Sub frameDate_Click()
    frameMonth.Visible = False
    frameYear.Visible = False
End Sub

'added by alex for TableManager.xlam needs

'* Modified   : Date and Time       Author              Description
'* Updated    : 10-10-2023 21:07    Alex                (uCalendar.frm > frameDate_KeyUp)

Private Sub frameDate_KeyUp(ByVal KeyCode As MSForms.ReturnInteger, ByVal Shift As Integer)
'@LastModified 2310102107
    Select Case KeyCode
        Case Is = 27
            uTableManager.canceledDatePicker = True
            Unload Me
        Case Is = vbKeyReturn
            Unload Me
    End Select
End Sub

Private Sub lblChoose_Click()
    Unload Me
End Sub

Private Sub lblChoose_MouseMove(ByVal Button As Integer, ByVal Shift As Integer, ByVal x As Single, ByVal y As Single)
    gDate.dFrame.Controls("lblDateBack").Visible = False
    gDate.dayMouseOut

End Sub

Private Sub lblClose_Click()
    txtSelectedDate = ""
    Unload Me
End Sub

Private Sub lblMonthName_Click()
    frameYear.Visible = False
    With frameMonth
        .Visible = True
        .Left = lblMonthName.Left
        .Top = lblMonthName.Top + 20

    End With
    gDate.createMonth txtMonthNumber
End Sub

Private Sub lblNextMonth_Click()
    With txtMonthNumber
        .Text = .Text + 1
        lblMonthName = getMonth(.Text)

        If lblMonthName = "OCAK" Then
            txtYearName = txtYearName + 1
        End If
        '        gDate.createDates txtYearName, .Text

    End With
End Sub

Private Sub lblPreviewMonth_Click()
    With txtMonthNumber
        .Text = .Text - 1

        lblMonthName = getMonth(.Text)
        If lblMonthName.Caption = "ARALIK" Then
            txtYearName = txtYearName - 1
        End If
        '       gDate.createDates txtYearName, .Text
    End With
End Sub

Private Sub lblRightBar_MouseDown(ByVal Button As Integer, ByVal Shift As Integer, ByVal x As Single, ByVal y As Single)
    moverForm Me, Me, Button
End Sub

Private Sub lblToday_Click()
    lblMonthName = getMonth(Format(Date, "mm"))
    txtYearName = Format(Date, "yyyy")
    txtMonthNumber = Format(Date, "m")
    gDate.createDates Format(Date, "yyyy"), Format(Date, "mm")
    
    gDate.SelectDate Controls("lblDate" & Day(Now)) 'added by alex
End Sub

Private Sub txtMonthNumber_Change()
    lblMonthName = getMonth(txtMonthNumber)
    gDate.createDates txtYearName, txtMonthNumber
End Sub

Private Sub txtYearName_Change()
    If Len(txtYearName.Text) = 4 Then
        gDate.createDates txtYearName, txtMonthNumber
    End If
End Sub

Private Sub txtYearName_MouseDown(ByVal Button As Integer, ByVal Shift As Integer, ByVal x As Single, ByVal y As Single)
    frameMonth.Visible = False
    With frameYear
        .Left = txtYearName.Left
        .Top = txtYearName.Top + 20
        .Visible = True
    End With
    gDate.createYear txtYearName
End Sub

Private Sub UserForm_Activate()
    lblToday_Click
End Sub

Private Sub UserForm_Click()
    Me.frameMonth.Visible = False
    Me.frameYear.Visible = False
End Sub

Private Sub UserForm_Initialize()
    Dim sMonth As Integer
    SelectedDay = ""
    removeTudo Me
    HideTitleBarAndBorder Me

    With Me
        .Height = 308
        .Width = 480
    End With

    IconDesign lblPreviewMonth, "&HE26C"
    IconDesign lblNextMonth, "&HE26B"

End Sub

Private Sub IconDesign(Ctrl As control, IconCode As String)
    With Ctrl
        .Font.Name = "Segoe MDL2 Assets"
        .Caption = ChrW(IconCode)
        .Font.Size = 12
        .ForeColor = RGB(191, 191, 191)
        .TextAlign = fmTextAlignLeft
        .BorderStyle = fmBorderStyleNone
        .BackStyle = fmBackStyleTransparent
    End With
End Sub

Private Sub UserForm_MouseDown(ByVal Button As Integer, ByVal Shift As Integer, ByVal x As Single, ByVal y As Single)
        moverForm Me, Me, Button
End Sub


'M_Calendar_API	Module

'-----------------------------------------------------------------------------------------------------------
#If VBA7 Then

    Public Declare PtrSafe Sub ReleaseCapture Lib "user32" ()
    Public Declare PtrSafe Function SendMessage Lib "user32" Alias "SendMessageA" (ByVal hwnd As Long, _
                                                                            ByVal wMsg As Long, _
                                                                            ByVal wParam As Long, _
                                                                            lParam As Any) As Long
    '----------------------------------------------------------------------------------------------------
    Public Declare PtrSafe Sub Sleep Lib "kernel32" (ByVal dwNilliseconds As Long)
    Public Declare PtrSafe Function SetLayeredWindowAttributes Lib "user32" (ByVal hwnd As Long, ByVal crKey As Long, ByVal bAlpha As Byte, ByVal dwFlags As Long) As Long
    Public Declare PtrSafe Function GetWindowLong Lib "user32.dll" Alias "GetWindowLongA" (ByVal hwnd As Long, ByVal nIndex As Long) As Long
    Public Declare PtrSafe Function SetWindowLong Lib "user32" Alias "SetWindowLongA" (ByVal hwnd As Long, ByVal nIndex As Long, ByVal dwNewLong As Long) As Long
    Public Declare PtrSafe Function FindWindow Lib "user32" Alias "FindWindowA" (ByVal lpClassName As String, ByVal lpWindowName As String) As Long
    Public Declare PtrSafe Function DrawMenuBar Lib "user32" (ByVal hwnd As Long) As Long
    Public Declare PtrSafe Function FindWindowA Lib "user32" (ByVal lpClassName As String, ByVal lpWindowName As String) As Long
    Public Declare PtrSafe Function LoadCursorBynum Lib "user32" Alias "LoadCursorA" (ByVal hInstance As Long, ByVal lpCursorName As Long) As Long
    Public Declare PtrSafe Function SetCursor Lib "user32" (ByVal hCursor As Long) As Long
    Public Declare PtrSafe Function MoveJanela Lib "user32" Alias "SetWindowLongA" (ByVal hwnd As Long, ByVal nIndex As Long, ByVal dwNewLong As Long) As Long
    Public Declare PtrSafe Function CreateRoundRectRgn Lib "gdi32" (ByVal X1 As Long, ByVal Y1 As Long, ByVal X2 As Long, ByVal Y2 As Long, ByVal X3 As Long, ByVal Y3 As Long) As Long
    Public Declare PtrSafe Function SetWindowRgn Lib "user32" (ByVal hwnd As Long, ByVal hRgn As Long, ByVal bRedraw As Boolean) As Long
    
#Else
    Public Declare Sub ReleaseCapture Lib "user32" ()
    Public Declare Function SendMessage Lib "user32" Alias "SendMessageA" (ByVal hWnd As Long, _
                                                                            ByVal wMsg As Long, _
                                                                            ByVal wParam As Long, _
                                                                            lParam As Any) As Long
    '----------------------------------------------------------------------------------------------------
    Public Declare Sub Sleep Lib "kernel32" (ByVal dwNilliseconds As Long)
    Public Declare Function SetLayeredWindowAttributes Lib "user32" (ByVal hWnd As Long, ByVal crKey As Long, ByVal bAlpha As Byte, ByVal dwFlags As Long) As Long
    Public Declare Function GetWindowLong Lib "user32.dll" Alias "GetWindowLongA" (ByVal hWnd As Long, ByVal nIndex As Long) As Long
    Public Declare Function SetWindowLong Lib "user32" Alias "SetWindowLongA" (ByVal hWnd As Long, ByVal nIndex As Long, ByVal dwNewLong As Long) As Long
    Public Declare Function FindWindow Lib "user32" Alias "FindWindowA" (ByVal lpClassName As String, ByVal lpWindowName As String) As Long
    Public Declare Function DrawMenuBar Lib "user32" (ByVal hWnd As Long) As Long
    Public Declare Function FindWindowA Lib "user32" (ByVal lpClassName As String, ByVal lpWindowName As String) As Long
    Public Declare Function LoadCursorBynum Lib "user32" Alias "LoadCursorA" (ByVal hInstance As Long, ByVal lpCursorName As Long) As Long
    Public Declare Function SetCursor Lib "user32" (ByVal hCursor As Long) As Long
    Public Declare Function MoveJanela Lib "user32" Alias "SetWindowLongA" (ByVal hWnd As Long, ByVal nIndex As Long, ByVal dwNewLong As Long) As Long
    Public Declare Function CreateRoundRectRgn Lib "gdi32" (ByVal X1 As Long, ByVal Y1 As Long, ByVal X2 As Long, ByVal Y2 As Long, ByVal X3 As Long, ByVal Y3 As Long) As Long
    Public Declare Function SetWindowRgn Lib "user32" (ByVal hWnd As Long, ByVal hRgn As Long, ByVal bRedraw As Boolean) As Long

#End If

Private Const GWL_EXSTYLE As Long = (-20)
Private Const WS_EX_DLGMODALFRAME As Long = &H1
Private Const GWL_STYLE As Long = (-16)
Private Const WS_CAPTION = 55000000
Private Const WS_EX_LAYERED = &H80000
'Private Const LWA_COLORKEY = &H1
Private Const LWA_ALPHA = &H2
Private Const IDC_HAND = 32649&
Public MeuForm As Long
Public ESTILO As Long
Public Const ESTILO_ATUAL As Long = (-16)
Public Const HTCAPTION = 2
Public Const WM_NCLBUTTONDOWN = &HA1

Private lngPixelsX As Long
Private lngPixelsY As Long
Private strThunder As String
Private blnCreate As Boolean
Private lnghWnd_Form As Long
Private lnghWnd_Sub As Long

Private Const cstMask As Long = &H7FFFFFFF

Public PassiveMonth, PassiveDay, PassiveYear, PassiveDayTag, SelectedDay, SelectedDayTag

Public Function getMonth(iMonth As Integer, Optional language As String)
'    Select Case languege
'@AssignedModule M_Calendar_API
        Select Case iMonth Mod 12
           Case Is = 1, "-11"
                getMonth = "JANUARY"
           Case Is = 2, "-10"
                getMonth = "FEBRUARY"
           Case Is = 3, "-9"
                getMonth = "MARCH"
           Case Is = 4, "-8"
                getMonth = "APRIL"
           Case Is = 5, "-7"
                getMonth = "MAY"
           Case Is = 6, "-6"
                getMonth = "JUNE"
           Case Is = 7, "-5"
                getMonth = "JULY"
           Case Is = 8, "-4"
                getMonth = "AUGUST"
           Case Is = 9, "-3"
                getMonth = "SEPTEMBER"
           Case Is = 10, "-2"
                getMonth = "OCTOBER"
           Case Is = 11, "-1"
                getMonth = "NOVEMBER"
           Case Is = 0, 12
                getMonth = "DECEMBER"

        End Select
'    End Select
End Function

Function HideTitleBarAndBorder(frm As Object)
'@AssignedModule M_Calendar_API
'@INCLUDE DECLARATION GWL_EXSTYLE
'@INCLUDE DECLARATION GWL_STYLE
'@INCLUDE DECLARATION WS_CAPTION
'@INCLUDE DECLARATION WS_EX_DLGMODALFRAME
'@INCLUDE DECLARATION DrawMenuBar
'@INCLUDE DECLARATION FindWindow
'@INCLUDE DECLARATION GetWindowLong
'@INCLUDE DECLARATION SetWindowLong
    Dim lngWindow As Long
    Dim lFrmHdl As Long
    lFrmHdl = FindWindow(vbNullString, frm.Caption)
    lngWindow = GetWindowLong(lFrmHdl, GWL_STYLE)
    lngWindow = lngWindow And (Not WS_CAPTION)
    SetWindowLong lFrmHdl, GWL_STYLE, lngWindow
    lngWindow = GetWindowLong(lFrmHdl, GWL_EXSTYLE)
    lngWindow = lngWindow And Not WS_EX_DLGMODALFRAME
    SetWindowLong lFrmHdl, GWL_EXSTYLE, lngWindow
    DrawMenuBar lFrmHdl

End Function

Function MakeUserformTransparent(frm As Object, colorKey As Variant, Optional color As Variant)
'@AssignedModule M_Calendar_API
'@INCLUDE DECLARATION GWL_EXSTYLE
'@INCLUDE DECLARATION LWA_COLORKEY
'@INCLUDE DECLARATION WS_EX_LAYERED
'@INCLUDE DECLARATION FindWindow
'@INCLUDE DECLARATION GetWindowLong
'@INCLUDE DECLARATION SetLayeredWindowAttributes
'@INCLUDE DECLARATION SetWindowLong
LWA_COLORKEY = colorKey

Dim formhandle As Long
Dim bytOpacity As Byte

formhandle = FindWindow(vbNullString, frm.Caption)
If IsMissing(color) Then color = &H8000&        '//rgbWhite
bytOpacity = 130

SetWindowLong formhandle, GWL_EXSTYLE, GetWindowLong(formhandle, GWL_EXSTYLE) Or WS_EX_LAYERED

frm.BackColor = color
SetLayeredWindowAttributes formhandle, color, bytOpacity, LWA_COLORKEY

End Function

Public Function MouseCursor(CursorType As Long)
'@AssignedModule M_Calendar_API
'@INCLUDE DECLARATION LoadCursorBynum
'@INCLUDE DECLARATION SetCursor
  Dim lngRet As Long
  lngRet = LoadCursorBynum(0&, CursorType)
  lngRet = SetCursor(lngRet)
End Function

Public Function MouseMoveIcon()
'@AssignedModule M_Calendar_API
'@INCLUDE PROCEDURE MouseCursor
'@INCLUDE DECLARATION IDC_HAND
    Call MouseCursor(IDC_HAND)
End Function

Public Sub moverForm(Form As Object, obj As Object, Button As Integer)
'@AssignedModule M_Calendar_API
'@INCLUDE DECLARATION HTCAPTION
'@INCLUDE DECLARATION WM_NCLBUTTONDOWN
'@INCLUDE DECLARATION FindWindowA
'@INCLUDE DECLARATION SendMessage
'@INCLUDE DECLARATION ReleaseCapture
    Dim lngMyHandle As Long, lngCurrentStyle As Long, lngNewStyle As Long
    If val(Application.Version) < 9 Then
        lngMyHandle = FindWindowA("ThunderXFrame", Form.Caption)
    Else
        lngMyHandle = FindWindowA("ThunderDFrame", Form.Caption)
    End If
    
    If Button = 1 Then
        With obj
            Call ReleaseCapture
            Call SendMessage(lngMyHandle, WM_NCLBUTTONDOWN, HTCAPTION, 0&)
        End With
    End If
End Sub
Public Sub removeTudo(ObjForm As Object)
'@AssignedModule M_Calendar_API
'@INCLUDE DECLARATION ESTILO_ATUAL
'@INCLUDE DECLARATION WS_CAPTION
'@INCLUDE DECLARATION FindWindowA
'@INCLUDE DECLARATION MoveJanela
'@INCLUDE DECLARATION ESTILO
'@INCLUDE DECLARATION MeuForm
    MeuForm = FindWindowA(vbNullString, ObjForm.Caption)
    ESTILO = ESTILO Or WS_CAPTION
    MoveJanela MeuForm, ESTILO_ATUAL, (ESTILO)
End Sub



'clsDate	Class

Option Explicit
Public WithEvents dForm As MSForms.UserForm
Public WithEvents dFrame As MSForms.Frame
Public WithEvents mFrame As MSForms.Frame
Public WithEvents yFrame As MSForms.Frame
Public WithEvents lblDate As MSForms.Label
Public WithEvents lblMonth As MSForms.Label
Public WithEvents lblYear As MSForms.Label
Public WithEvents lblTodayBack As MSForms.Label
Public WithEvents lblSelectedDateBack As MSForms.Label
Public WithEvents lblDateBack As MSForms.Label
Public WithEvents lblMonthBack As MSForms.Label
Public WithEvents lblYearBack As MSForms.Label

Public colDate As New Collection
Public aDate As New clsDate
Public colMonth As New Collection
Public aMonth As New clsDate
Public colYear As New Collection
Public aYear As New clsDate

Public Sub createDates(cYear As Integer, cMonth As Integer)
    Dim lLeft As Integer, lTop As Integer
    Dim fDate As Integer
    Set dForm = uCalendar
    Set dFrame = dForm.frameDate
    PassiveDay = "": SelectedDay = ""
    '  MsgBox cYear & "-" & cMonth
    '************************************
    '***********//frame date start//*****
    With dFrame
        .Clear
    End With
    '***********//frame date start//*****
    '************************************
    Dim dt As Date
    If cMonth Mod 12 = 0 Then cMonth = 12 Else cMonth = cMonth Mod 12
    dt = DateSerial(cYear, cMonth, 1)
    Dim firstDate As Long
    firstDate = Weekday(dt, 2)
    Dim ilk As Date
    ilk = DateSerial(Year(dt), Month(dt), 1 - (firstDate - 1))

    lLeft = 6: lTop = 6
    Dim gunsayi As String
    Dim i As Long
    For i = ilk To ilk + 41
        gunsayi = Format(Day(i), "#0")

        Set lblDate = dFrame.Controls.Add("Forms.Label.1", "lblDate" & gunsayi)
        With lblDate
'            Debug.Print lblDate.Name
            .Left = lLeft
            .Top = lTop
            .Width = 32
            .Font.Name = "MontSerrat Medium"
            .Font.Size = 9
            .ForeColor = &H48372C
            .TextAlign = fmTextAlignCenter
            .BackStyle = fmBackStyleTransparent
            .Caption = gunsayi
            .Tag = i

            If CInt(Year(CDate(i))) = CInt(Year(CDate(dt))) And CInt(Month(CDate(i))) = CInt(Month(CDate(dt))) Then
                .Enabled = True
            Else
                .Enabled = False
            End If

            If Weekday(i, 2) = 7 Or Weekday(i, 2) = 6 Then
                .ForeColor = &H2144FF
            End If

            If i = Date And .Enabled = True Then

                Set lblTodayBack = dFrame.Controls.Add("Forms.Label.1", "lblTodayBack")
                With lblTodayBack
                    .Height = 1.2
                    .Width = 22
                    .Top = lblDate.Top + 16
                    .Left = lblDate.Left + 5
                    .BackColor = &H2144FF
                    .ZOrder (1)
                End With
            End If

            Set aDate = New clsDate
            Set aDate.lblDate = lblDate
            Set aDate.dFrame = dFrame
            Set aDate.dForm = dForm
            colDate.Add aDate

        End With

        If lLeft <= 238 Then
            lLeft = lLeft + 44

        Else
            lLeft = 6
            lTop = lTop + 30
        End If

    Next i
    Set lblSelectedDateBack = dFrame.Controls.Add("Forms.Label.1", "lblSelectedDateBack")
    With lblSelectedDateBack
        .Height = 26
        .Width = 30
        .Picture = dForm.lblSelectedDateBack.Picture

        .Visible = False
        .ZOrder (1)
    End With

    Set lblDateBack = dFrame.Controls.Add("Forms.Label.1", "lblDateBack")
    With lblDateBack
        .Height = 26
        .Width = 30
        .Picture = dForm.lblDateBack.Picture
        .Visible = False
        .ZOrder (1)

    End With

End Sub

Public Sub createMonth(snMonth As Integer)
    Dim lTop As Integer, i As Integer
    Dim fDate As Integer

    Set dForm = uCalendar
    Set mFrame = dForm.frameMonth

    With mFrame
        .Clear
    End With

    lTop = 4
    For i = 1 To 12
        Set lblMonth = mFrame.Controls.Add("Forms.Label.1", "lblMonth" & i)
        With lblMonth
            .Left = 0
            .Top = lTop
            .Width = 90
            .Height = 14
            .Font.Name = "MontSerrat Medium"
            .Font.Size = 9

            If i = snMonth Then
                .ForeColor = vbWhite
                PassiveMonth = lblMonth.Name
            Else
                .ForeColor = vbGrayText
            End If

            .TextAlign = fmTextAlignCenter
            .BackStyle = fmBackStyleTransparent
            .Caption = getMonth(i)
            .Tag = i
        End With
        lTop = lTop + 18

        Set aMonth = New clsDate
        Set aMonth.lblMonth = lblMonth
        Set aMonth.mFrame = mFrame
        Set aMonth.dForm = dForm
        colMonth.Add aMonth

    Next i
    Set lblMonth = Nothing

    Set lblMonthBack = mFrame.Controls.Add("Forms.Label.1", "lblMonthBack")
    With lblMonthBack
        .Left = 0
        .Top = (snMonth - 1) * 18
        .Width = 90
        .BackColor = &H2144FF
        .TextAlign = fmTextAlignCenter
        .BorderStyle = fmBorderStyleNone
        .ZOrder (1)
    End With

End Sub

Public Sub createYear(snYear As Integer)
    Dim lTop As Integer, i As Integer

    Set dForm = uCalendar
    Set yFrame = dForm.frameYear

    With yFrame
        .Clear
    End With

    lTop = 4
    For i = 1 To 12
        Set lblYear = yFrame.Controls.Add("Forms.Label.1", "lblYear" & i)
        With lblYear
            .Left = 0
            .Top = lTop
            .Width = 90
            .Height = 14
            .Font.Name = "MontSerrat Medium"
            .Font.Size = 9
            .ForeColor = vbGrayText
            .Caption = Format(Date, "yyyy") + i - 4

            If .Caption = snYear Then
                .ForeColor = vbWhite
                PassiveYear = lblYear.Name
            Else
                .ForeColor = vbGrayText
            End If

            .TextAlign = fmTextAlignCenter
            .BackStyle = fmBackStyleTransparent

            .Tag = i
        End With
        lTop = lTop + 18

        Set aYear = New clsDate
        Set aYear.lblYear = lblYear
        Set aYear.yFrame = yFrame
        Set aYear.dForm = dForm
        colYear.Add aYear

    Next i

    Set lblYearBack = yFrame.Controls.Add("Forms.Label.1", "lblYearBack")
    With lblYearBack
        .Left = 0
        .Top = yFrame.Controls(PassiveYear).Top - 4
        .Width = 90
        .BackColor = &H2144FF
        .TextAlign = fmTextAlignCenter
        .BorderStyle = fmBorderStyleNone
        .ZOrder (1)
    End With

End Sub

Private Sub dForm_Click()
    framevisibleFalse
End Sub

Private Sub dFrame_Click()
    framevisibleFalse
End Sub

Sub framevisibleFalse()
    uCalendar.frameMonth.Visible = False
    uCalendar.frameYear.Visible = False
End Sub

Private Sub lblDate_Click()

    If SelectedDay <> "" Then
        If Weekday(SelectedDayTag, 2) = 7 Or Weekday(SelectedDayTag, 2) = 6 Then
            dFrame.Controls(SelectedDay).ForeColor = &H2144FF
        Else
            dFrame.Controls(SelectedDay).ForeColor = &H48372C
        End If

    Else

    End If
    SelectDate lblDate
End Sub

Private Sub lblDate_DblClick(ByVal Cancel As MSForms.ReturnBoolean)
    SelectDate lblDate
    Unload uCalendar

End Sub

Sub SelectDate(Ctrl As control)
    Dim curDate As Date
    With uCalendar
        .lblSelectedDate = Ctrl.Caption
        .lblSelectedMonth = .lblMonthName
        .lblSelectedYear = .txtYearName
        .lblSelectedDateName = Format(Ctrl.Tag, "dddd")
        .txtSelectedDate = Format(Ctrl.Tag, "dd.mm.yyyy")

        lblSelectedDateBackPosition Ctrl
        Ctrl.ForeColor = vbWhite
        SelectedDay = Ctrl.Name
        SelectedDayTag = Ctrl.Tag
    End With
End Sub

Private Sub lblDate_MouseMove(ByVal Button As Integer, ByVal Shift As Integer, ByVal x As Single, ByVal y As Single)
    framevisibleFalse
    lblDateBackPosition lblDate
    dayMouseOut
    lblDate.ForeColor = vbWhite
    PassiveDay = lblDate.Name
    PassiveDayTag = lblDate.Tag
End Sub

Private Sub lblDateBackPosition(Ctrl As control)
    MouseMoveIcon
    With dFrame.Controls("lblDateBack")
        .Visible = True
        .Top = Ctrl.Top - 5
        .Left = Ctrl.Left + 1
    End With
End Sub

Private Sub lblMonth_Click()
    With uCalendar
        .txtMonthNumber = lblMonth.Tag
        .frameMonth.Visible = False
        .lblMonthName = getMonth(.txtMonthNumber)
        createDates .txtYearName, .txtMonthNumber
    End With
End Sub

Private Sub lblYear_Click()
    With uCalendar
        .txtYearName = lblYear
        .frameYear.Visible = False
        createDates .txtYearName, .txtMonthNumber
    End With

End Sub

Private Sub lblMonth_MouseMove(ByVal Button As Integer, ByVal Shift As Integer, ByVal x As Single, ByVal y As Single)
    '    MsgBox PassiveMonth
    lblMonthBackPosition lblMonth
    monthMouseOut
    lblMonth.ForeColor = vbWhite
    PassiveMonth = lblMonth.Name

End Sub

Private Sub lblYear_MouseMove(ByVal Button As Integer, ByVal Shift As Integer, ByVal x As Single, ByVal y As Single)

    lblYearBackPosition lblYear
    yearMouseOut
    lblYear.ForeColor = vbWhite
    PassiveYear = lblYear.Name

End Sub

Private Sub lblSelectedDateBackPosition(Ctrl As control)
    With dFrame.Controls("lblSelectedDateBack")
        .Visible = True
        .Top = Ctrl.Top - 5
        .Left = Ctrl.Left + 1
    End With
End Sub

Private Sub lblYearBackPosition(Ctrl As control)
    MouseMoveIcon
    dForm.Controls("lblYearBack").Top = Ctrl.Top - 4
End Sub

Private Sub lblMonthBackPosition(Ctrl As control)
    MouseMoveIcon
    dForm.Controls("lblMonthBack").Top = Ctrl.Top - 4
End Sub

Public Sub dayMouseOut()
    On Error Resume Next
    If PassiveDay <> "" Then

        If Weekday(PassiveDayTag, 2) = 7 Or Weekday(PassiveDayTag, 2) = 6 Then
            dFrame.Controls(PassiveDay).ForeColor = &H2144FF
        Else
            dFrame.Controls(PassiveDay).ForeColor = &H48372C
        End If

    End If
    If SelectedDay <> "" Then
        dFrame.Controls(SelectedDay).ForeColor = vbWhite
    End If

End Sub

Private Sub monthMouseOut()

    If PassiveMonth <> "" Then
        mFrame.Controls(PassiveMonth).ForeColor = vbGrayText
    End If
End Sub

Private Sub yearMouseOut()

    If PassiveYear <> "" Then
        yFrame.Controls(PassiveYear).ForeColor = vbGrayText
    End If
End Sub


'UserForm1	UserForm

Option Explicit

Public LoadedList As Variant
Private previousInputLength As Long
Public TargetControl As MSForms.control

Private Sub ListBox1_Enter()
    If ListBox1.ListCount > 0 Then
        If ListBox1.ListIndex = -1 Then ListBox1.ListIndex = 0
    End If
End Sub
Private Sub ListBox1_DblClick(ByVal Cancel As MSForms.ReturnBoolean)
    TargetControl.Value = ListBox1.List(ListBox1.ListIndex)
    uTableManager.Show
    Unload Me
End Sub

Private Sub ListBox1_KeyUp(ByVal KeyCode As MSForms.ReturnInteger, ByVal Shift As Integer)
    Select Case KeyCode
        Case Is = vbKeyReturn
            If ListBox1.ListIndex <> -1 Then TargetControl.Value = ListBox1.List(ListBox1.ListIndex)
            uTableManager.Show
            Unload Me
        Case Is = 27
            ListBox1.ListIndex = -1
            TextBox1.SetFocus
    End Select
End Sub

Private Sub TextBox1_KeyUp(ByVal KeyCode As MSForms.ReturnInteger, ByVal Shift As Integer)
    Select Case KeyCode
        Case 27
            If TextBox1.Value <> "" Then
                TextBox1.Value = ""
            Else
                uTableManager.Show
                Unload Me
            End If
    End Select
End Sub

Private Sub TextBox1_Change()
    Select Case Len(TextBox1.Value)
    Case Is = 0 'show whole range.value
        ListBox1.List = LoadedList
    Case Is = 1 'filter the whole array
        ListBox1.List = ArrayFilterLike(LoadedList, TextBox1.Value, False)
    Case Is > 1
        If Len(TextBox1.Value) > previousInputLength Then
            ListBox1.List = ArrayFilterLike(ListBox1.List, TextBox1.Value, False)
        Else
            ListBox1.List = ArrayFilterLike(LoadedList, TextBox1.Value, False)
        End If
    End Select
    previousInputLength = Len(TextBox1.Value)
End Sub

'ChangeLog	Document

Option Explicit

